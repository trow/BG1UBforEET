BACKUP ~bg1ub/backup~


// AUTHOR ~icelus, SimDing0, Ascension64, devSin, Idobek, CamDawg, SixOfSpades, and Echon~
AUTHOR ~http://forums.pocketplane.net/index.php/board,79.0.html~

//MODDER

// ASK_EVERY_COMPONENT

ALLOW_MISSING
// BG: TotSC files
ANDRIS.CRE
APPAR.CRE
AR0502.BCS
AR0512.ARE
AR0512.BCS
AR1008.ARE
AR1009.ARE
AR1010.ARE
AR1010.MOS
AR1010.TIS
BARESH2.CRE
BARWLF.CRE
BAYARD.CRE
BEARPO3.CRE
BEYN.CRE
BISHOP.CRE
BLUN10.ITM
CLAIRD.CRE
CORPSE1.CRE
CORPSE2.CRE
CRYPTCRA.CRE
CUCHOL.CRE
CULT1.CRE
CULT2.CRE
CULT3.CRE
CULT3.DLG
CULT4.CRE
CULTASS.CRE
CULTT1.CRE
CULTT2.CRE
CULTT3.CRE
CULTT4.CRE
DAESE.CRE
DAESEWLF.CRE
DAITEL.CRE
DELAIN2.CRE
DELSVIR.CRE
DEZKIE.CRE
DOGBLINK.CRE
DOOMDUR.CRE
DRADEE.CRE
DRADEE2.CRE
DURLYL2.CRE
DUSHAI.CRE
ERDANE.STO
FEARM.CRE
FTOWUB_A.CRE
FTOWUB_B.CRE
FTOWUB_E.CRE
FTOWUBSC.CRE
FTOWUBSN.CRE
FTOWUBX.CRE
GALKEN.CRE
GARAN.CRE
GBLUN06.BAM
GHASTD.CRE
GRAEL.CRE
HACK.CRE
HELM14.ITM
IKE.CRE
ISLSIR.CRE
KAISHA.CRE
KAISHWLF.CRE
KAROUG.CRE
KING.CRE
KRYLA.CRE
LEAGGU1.CRE
LEAGGU2.CRE
LEAGGU3.CRE
LEAGGU4.CRE
LEAGGU5.CRE
LEAGGU6.CRE
LEAGGU7.CRE
LOUPGAR.CRE
LOVE.BCS
LOVEM.CRE
MARALE2.CRE
MARCEL.CRE
MEMNIS.CRE
MISC1H.ITM
MISC2P.ITM
MTOWUB_A.CRE
MTOWUB_B.CRE
MTOWUB_C.CRE
MTOWUB_D.CRE
MTOWUB_E.CRE
MTOWUBSC.CRE
MTOWUBSN.CRE
MTOWUBST.CRE
MTOWUBX.CRE
PALIN.CRE
PHEOARCH.CRE
PLYOGRE.CRE
PRIDEM.CRE
QUEEN.CRE
SHANDAL2.CRE
SKELDED.CRE
SPIDPHAS.CRE
SPPR405.SPL
SPPR982.SPL
SPWI411.SPL
SPWI506.SPL
SPWI929.SPL
SPWI930.SPL
SPWI939.SPL
SPWI943.SPL
SPWI996.SPL
TANAR01.WAV
TANAR02.WAV
TANAR03.WAV
TANAR04.WAV
TANAR05.WAV
TANAR06.WAV
TARNOR.CRE
TRACEA.CRE
ULF.CRE
ULGOTH.STO
WOLFWE.CRE
WYVERNBI.CRE

AX1H07.ITM
BRAC11.ITM
MISC1A.ITM
MISC1B.ITM
MISC92.ITM
MISC94.ITM
MISC95.ITM

//Tutu: TotSC files
_ANDRIS.CRE
_APPAR.CRE
_AR0502.BCS
FW0512.ARE
_AR0512.BCS
FW1008.ARE
FW1009.ARE
FW1010.ARE
FW1010.MOS
_AR1010.TIS
_BARESH2.CRE
_BARWLF.CRE
_BAYARD.CRE
_BEARPO3.CRE
_BEYN.CRE
_BISHOP.CRE
_BLUN10.ITM
_CLAIRD.CRE
_CORPSE1.CRE
_CORPSE2.CRE
_RYPTCRA.CRE
_CUCHOL.CRE
_CULT1.CRE
_CULT2.CRE
_CULT3.CRE
_CULT3.DLG
_CULT4.CRE
_CULTASS.CRE
_CULTT1.CRE
_CULTT2.CRE
_CULTT3.CRE
_CULTT4.CRE
_DAESE.CRE
_AESEWLF.CRE
_DAITEL.CRE
_DELAIN2.CRE
_DELSVIR.CRE
_DEZKIE.CRE
_OGBLINK.CRE
_DOOMDUR.CRE
_DRADEE.CRE
_DRADEE2.CRE
_DURLYL2.CRE
_DUSHAI.CRE
_ERDANE.STO
_FEARM.CRE
_23_FTOW.CRE
_24_FTOW.CRE
_27_FTOW.CRE
_28_FTOW.CRE
_29_FTOW.CRE
_FTOWUBX.CRE
_GALKEN.CRE
_GARAN.CRE
_GBLUN06.BAM
_GHASTD.CRE
_GRAEL.CRE
_HACK.CRE
_HELM14.ITM
_IKE.CRE
_ISLSIR.CRE
_KAISHA.CRE
_AISHWLF.CRE
_KAROUG.CRE
_KING.CRE
_KRYLA.CRE
_LEAGGU1.CRE
_LEAGGU2.CRE
_LEAGGU3.CRE
_LEAGGU4.CRE
_LEAGGU5.CRE
_LEAGGU6.CRE
_LEAGGU7.CRE
_LOUPGAR.CRE
_LOVE.BCS
_LOVEM.CRE
_MARALE2.CRE
_MARCEL.CRE
_MEMNIS.CRE
_MISC1H.ITM
_MISC2P.ITM
_TOWUB_A.CRE
_TOWUB_B.CRE
_TOWUB_C.CRE
_TOWUB_D.CRE
_TOWUB_E.CRE
_TOWUBSC.CRE
_2A_MTOW.CRE
_2B_MTOW.CRE
_MTOWUBX.CRE
_PALIN.CRE
_HEOARCH.CRE
_PLYOGRE.CRE
_PRIDEM.CRE
_QUEEN.CRE
_HANDAL2.CRE
_SKELDED.CRE
_PIDPHAS.CRE
SPPR405.SPL
SPPR982.SPL
SPWI411.SPL
SPWI506.SPL
SPWI929.SPL
SPWI930.SPL
SPWI939.SPL
SPWI943.SPL
SPWI996.SPL
_TANAR01.WAV
_TANAR02.WAV
_TANAR03.WAV
_TANAR04.WAV
_TANAR05.WAV
_TANAR06.WAV
_TARNOR.CRE
_TRACEA.CRE
_ULF.CRE
_ULGOTH.STO
_WOLFWE.CRE
_YVERNBI.CRE

_AX1H07.ITM
_BRAC11.ITM
_MISC1A.ITM
_MISC1B.ITM
_MISC92.ITM
_MISC94.ITM
_MISC95.ITM

//BG only
GIANDAM.EFF
MONSTDAM.EFF
UNDEDDAM.EFF
GIANHIT.EFF
MONSTHIT.EFF
UNDEDHIT.EFF


VERSION ~v14.0_beta_151114~
README ~bg1ub/BG1UB-Readme.html~

ALWAYS

  INCLUDE ~bg1ub/lib/cre_project_macros.tpa~
  INCLUDE ~bg1ub/lib/are_project_functions.tpa~
  APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~ UNLESS ~STATE_REALLY_DEAD~

// @@@ Support for polish translation @@@
    //set the proper values of string variables
  OUTER_SPRINT "Z!PL_TRA_Patch" "bg1ub/tra/polish"
  OUTER_SPRINT "Z!Polish_Choosen" "polish"
    //main conversion code
  INCLUDE ~%Z!PL_TRA_Patch%/BG1&BG2UniversalPolishFont.tpa~
    //all TRAs declared in LANGUAGE must be reloaded
  LOAD_TRA "bg1ub/tra/%LANGUAGE%/setup.tra"
  LOAD_TRA "bg1ub/tra/%LANGUAGE%/bg1ub.tra"

  // INCLUDE ~%MOD_FOLDER%/handle_charsets.tpa~
  ACTION_DEFINE_ARRAY ub#noconvert BEGIN setup END
  ACTION_DEFINE_ARRAY ub#reload BEGIN bg1ub END

/*  ACTION_DEFINE_ASSOCIATIVE_ARRAY ub#charsets BEGIN
    deutsch => cp1252
    english => cp1252
    espanol => cp1252
    francias => cp1252
    italiano => cp1252
    polish => cp1250
    russian => cp1251
  END
*/

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/tra~
      charset_table = ub#charsets
      noconvert_array = ub#noconvert
      reload_array = ub#reload
  END

  COPY ~bg1ub/tra/%LANGUAGE%/extra_tmp.tra~ ~bg1ub/tra/%LANGUAGE%/extra.tra~
	EVALUATE_BUFFER
  LOAD_TRA ~bg1ub/tra/%LANGUAGE%/extra.tra~

	ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN //Tutu
		PRINT @90010
      	INCLUDE ~bg1ub/lib/g3_tutu_cpmvars.tpa~
	  	INCLUDE ~bg1ub/lib/rr_tutubgtbgee_addvars.tpa~
    END
    ACTION_IF GAME_IS ~bgt~ THEN BEGIN //BGT
        PRINT @90011
        INCLUDE ~bg1ub/lib/g3_bgt_cpmvars.tpa~
		INCLUDE ~bg1ub/lib/rr_tutubgtbgee_addvars.tpa~
    END
    ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN //BG
        PRINT @90012
        INCLUDE ~bg1ub/lib/g3_bg_cpmvars.tpa~
		/* Extra stuff needed for cross platform code: BG1 with TotSC installed */
		ACTION_IF GAME_IS ~totsc~ THEN BEGIN
     		INCLUDE ~bg1ub/lib/rr_totsc_addvars.tpa~
     		APPEND ~action.ids~ ~181 ForceSpellRES(S:RES*, O:Target)~ UNLESS ~ForceSpellRES~
		END ELSE BEGIN
			/* BG1 without TotSC installed */
     		INCLUDE ~bg1ub/lib/rr_bg1_addvars.tpa~
		END //still bg1+totsc
    END
    ACTION_IF GAME_IS ~bgee~ THEN BEGIN //bgee
      	PRINT @90017
      	INCLUDE ~bg1ub/lib/g3_bgee_cpmvars.tpa~
		INCLUDE ~bg1ub/lib/rr_tutubgtbgee_addvars.tpa~
    END
    ACTION_IF !(GAME_IS ~bg1 totsc bgt bgee tutu tutu_totsc~) THEN BEGIN //Not a supported game
         FAIL @90013
    END

  // BGT/Tutu compatibility
  ACTION_IF GAME_IS ~tutu tutu_totsc bgt~ AND //BGT or Tutu only
    !(FILE_EXISTS ~override/BG1UBBGTTutuCompat.UB~) THEN BEGIN //do once

	COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBBGTTutuCompat.UB~
	INCLUDE ~bg1ub/lib/BG1UBBGTTutuCompat.tph~
  END
  
  // BGEE Fix Area Scripts for AR2400.BCS and AR2900.BCS
  ACTION_IF GAME_IS ~bgee~ AND 
    !(FILE_EXISTS ~override/AY#BGEEARBCSFIX.000~) THEN BEGIN //do once

	COPY ~bg1ub/bg1ubplaceholder~ ~override/AY#BGEEARBCSFIX.000~
	
	COPY_EXISTING ~AR2400.bcs~ ~override~
		DECOMPILE_AND_PATCH BEGIN
  			//Add Exists(Raiken) check to TazokSays script block
  			REPLACE_TEXTUALLY ~Global("TazokSays","GLOBAL",1)~ ~Global("TazokSays","GLOBAL",1) Exists("Raiken")~
  		END

	COPY_EXISTING ~AR2900.bcs~ ~override~
		DECOMPILE_AND_PATCH BEGIN
  			//Add Exists(Teven) check to TazokSays script block
  			REPLACE_TEXTUALLY ~Global("TazokSays","GLOBAL",1)~ ~Global("TazokSays","GLOBAL",1) Exists("Teven")~
  		END  	
  	
  END // BGEE Area Script Fix
END

AUTO_TRA ~bg1ub/tra/%s~

DEFINE_ACTION_MACRO ~getcpmvara~ BEGIN
  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN //Tutu
    OUTER_SPRINT temp_cpmvar ~%temp_tutu%~
  END
  ELSE BEGIN
    ACTION_IF GAME_IS ~bgt~ THEN BEGIN //BGT
      OUTER_SPRINT temp_cpmvar ~%temp_bgt%~
    END
    ELSE BEGIN
    	ACTION_IF GAME_IS ~bgee~ THEN BEGIN //BGEE
      		OUTER_SPRINT temp_cpmvar ~%temp_bgee%~
    	END
      	ELSE BEGIN // BG
      		OUTER_SPRINT temp_cpmvar ~%temp_bg%~
      	END
    END
  END
END

DEFINE_PATCH_MACRO ~getcpmvarp~ BEGIN
  PATCH_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN //Tutu
    SPRINT temp_cpmvar ~%temp_tutu%~
  END
  ELSE BEGIN
    PATCH_IF GAME_IS ~bgt~ THEN BEGIN //BGT
      SPRINT temp_cpmvar ~%temp_bgt%~
    END
  	ELSE BEGIN
    	PATCH_IF GAME_IS ~bgee~ THEN BEGIN //BGEE
      		SPRINT temp_cpmvar ~%temp_bgee%~
    	END
    	ELSE BEGIN //BG
      		SPRINT temp_cpmvar ~%temp_bg%~
      	END
    END
  END
END

LANGUAGE ~English~
	 ~english~
	 ~bg1ub/tra/english/setup.tra~
     ~bg1ub/tra/english/bg1ub.tra~

LANGUAGE ~Deutsch~ // Timothy
	 ~deutsch~
	 ~bg1ub/tra/deutsch/setup.tra~
     ~bg1ub/tra/deutsch/setup.tra~

LANGUAGE ~Español~ // Immortality (clandlan.net)
	 ~espanol~
	 ~bg1ub/tra/espanol/setup.tra~
     ~bg1ub/tra/espanol/bg1ub.tra~

LANGUAGE ~Français~ // IronFistedGiant, Isaya
	 ~francais~
	 ~bg1ub/tra/francais/setup.tra~
     ~bg1ub/tra/francais/bg1ub.tra~

LANGUAGE ~Italiano~ // Andrea Colombo, Stone Angel, ilot, Salk
	 ~italiano~
	 ~bg1ub/tra/italiano/setup.tra~
     ~bg1ub/tra/italiano/bg1ub.tra~

LANGUAGE ~ãááª¨© (Russian)~ // by aerie.ru & Darktech (now maintained by arcanecoast.ru team)
     ~russian~
     ~bg1ub/tra/russian/setup.tra~
     ~bg1ub/tra/russian/bg1ub.tra~

LANGUAGE ~Polish~ // Bartek
	 ~polish~
	 ~bg1ub/tra/polish/setup.tra~
     ~bg1ub/tra/polish/bg1ub.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ice Island Level Two Restoration                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @0

// Author: devSin
// Email:
// Website:

// variables not set yet
REQUIRE_PREDICATE GAME_IS ~totsc tutu_totsc bgt bgee~ @1

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBIceIsland.UB~

ACTION_IF GAME_IS ~tutu_totsc~ THEN BEGIN
  LAF HANDLE_TILESETS END
  MOVE ~override/ub1010.tis~ ~override/_AR1010.tis~
END ELSE BEGIN
  ACTION_IF GAME_IS ~bgt~ THEN BEGIN
    LAF HANDLE_TILESETS END
    MOVE ~override/ub1010.tis~ ~override/ARU010.tis~
  END ELSE BEGIN
    ACTION_IF GAME_IS ~bgee~ THEN BEGIN
      LAF HANDLE_TILESETS END
      MOVE ~override/ub1010.tis~ ~override/AR1010.tis~
    END
  END
END 
// AR1010.TIS and AR1010.MOS already included with TotSC

ACTION_IF !GAME_IS ~bgee~ THEN BEGIN
	COPY ~bg1ub/AR1010/%tutuorbgt%~ ~override~
END

COPY_EXISTING ~%IceIsland%.ARE~ ~override~
              ~%IceIslandMaze_L1%.ARE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    // Make default transitions autosave; enable travel to AR1010
    READ_LONG 0x5c trigOff
    FOR (READ_SHORT 0x5a numTrig; numTrig; numTrig -= 0x1) BEGIN
      READ_SHORT trigOff + 0x20 type
      PATCH_IF (type == 0x2) BEGIN
        READ_ASCII trigOff + 0x38 destAre
        PATCH_IF (("%destAre%" STR_EQ "%IceIsland%") OR
                   ("%destAre%" STR_EQ "%IceIslandMaze_L1%")) BEGIN
          READ_BYTE trigOff + 0x60 flag
          WRITE_BYTE trigOff + 0x60 flag | 0x4 // Add party-required flag
        END
      END
      PATCH_IF ("%SOURCE_RES%" STR_EQ "%IceIslandMaze_L1%") BEGIN
        READ_ASCII trigOff name
        SPRINT temp_tutu "1" SPRINT temp_bgt "U" SPRINT temp_bgee "1" SPRINT temp_bg "1" LAUNCH_PATCH_MACRO ~getcpmvarp~
        PATCH_IF ("%name%" STR_EQ "DOOR%temp_cpmvar%010") BEGIN
          WRITE_ASCIIE trigOff + 0x38 ~%IceIslandMaze_L2%~ #8 // Set destination area
        END
      END
      trigOff += 0xc4
    END
  END
BUT_ONLY

// Add entrance to AR1008 from AR1010
COPY_EXISTING ~%IceIsland%.ARE~ ~override~
    SPRINT temp_tutu "1" SPRINT temp_bgt "U" SPRINT temp_bgee "1" SPRINT temp_bg "1" LAUNCH_PATCH_MACRO ~getcpmvarp~
	LPF fj_are_structure
		INT_VAR
		fj_loc_x       = 842  // Location X
		fj_loc_y       = 307  // Location Y
		fj_orientation = 15   // Facing direction
		STR_VAR
		fj_structure_type = entrance
		fj_name           = EVALUATE_BUFFER ~EXIT%temp_cpmvar%010~
	END
BUT_ONLY

COPY_EXISTING ~%IceIslandMaze_L1%.ARE~ ~override~
	// Remove Dezkiel from AR1009 (he's now in AR1010)
	READ_LONG 0x54 actOff
	actIdx = 0
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
    	READ_ASCII actOff + 0x80 char
    	PATCH_IF ("%char%" STR_EQ "%tutu_var%DEZKIE") BEGIN
			LPF fj_are_structure
				INT_VAR
				fj_delete_mode = actIdx  // Actor to delete
				STR_VAR
				fj_structure_type = actor
			END
		END
		actIdx += 1
		actOff += 0x110
	END
	// Add entrance to AR1009 from AR1010
	PATCH_IF GAME_IS ~totsc tutu_totsc bgt~ THEN BEGIN  /* Not needed for BGEE */
		SPRINT temp_tutu "1" SPRINT temp_bgt "U" SPRINT temp_bg "1" LAUNCH_PATCH_MACRO ~getcpmvarp~
		LPF fj_are_structure
			INT_VAR
			fj_loc_x       = 697  // Location X
			fj_loc_y       = 259  // Location Y
			fj_orientation = 0   // Facing direction
			STR_VAR
			fj_structure_type = entrance
			fj_name           = EVALUATE_BUFFER ~EXIT%temp_cpmvar%010~
		END
	END
BUT_ONLY

COPY_EXISTING ~%IceIslandMaze_L2%.ARE~ ~override~
	PATCH_IF GAME_IS ~totsc tutu_totsc bgt~ THEN BEGIN  /* Not needed for BGEE */
    	// Make default transitions autosave
    	READ_LONG 0x5c trigOff
    	FOR (READ_SHORT 0x5a numTrig; numTrig; numTrig -= 0x1) BEGIN
    		READ_SHORT trigOff + 0x20 type
    		PATCH_IF (type == 0x2) BEGIN
    			READ_ASCII trigOff + 0x38 destAre
        		PATCH_IF (("%destAre%" STR_EQ "%IceIsland%") OR ("%destAre%" STR_EQ "%IceIslandMaze_L1%")) BEGIN
	          		READ_BYTE trigOff + 0x60 flag
					WRITE_BYTE trigOff + 0x60 flag | 0x4 // Add party-required flag
        		END
      		END
      		trigOff += 0xc4
    	END
    	//Correct exit point used for transition to AR1009 (BGT)
    	PATCH_IF ("%tutuorbgt%" STRING_EQUAL "BGT") BEGIN //BGT only
      		READ_LONG 0x5c trigOff
      		FOR (READ_SHORT 0x5a numTrig; numTrig; numTrig -= 0x1) BEGIN
        		READ_ASCII trigOff name
        		PATCH_IF ("%name%" STR_EQ "DOORU009") BEGIN
          			WRITE_ASCII trigOff + 0x40 ~EXITU010~ #32 // Set entrance name
        		END
      		END
    	END
    	// Add usable transition to AR1008
    	SPRINT temp_tutu "1" SPRINT temp_bgt "U" SPRINT temp_bg "1" LAUNCH_PATCH_MACRO ~getcpmvarp~
		LPF fj_are_structure
			INT_VAR
			fj_type         = 2    // travel
			fj_flags		= 0x4  // Party required flag
			fj_box_left     = 345
			fj_box_top      = 2719
			fj_box_right    = 545
			fj_box_bottom   = 2815
			fj_cursor_idx   = 28   // stairs
			fj_vertex_0     = 478 + (2719 << 16)
			fj_vertex_1     = 545 + (2762 << 16)
			fj_vertex_2     = 497 + (2815 << 16)
			fj_vertex_3     = 345 + (2815 << 16)
    		STR_VAR
    		fj_structure_type   = region
		    fj_name             = EVALUATE_BUFFER ~DOOR%temp_cpmvar%008~
    		fj_destination_area = EVALUATE_BUFFER ~%IceIslandMaze_L1%~
    		fj_destination_name = EVALUATE_BUFFER ~EXIT%temp_cpmvar%010~
		END
	END
	// Add dispelling traps on bridges to Dezkiel's "island"
	LPF fj_are_structure
    	INT_VAR
    	fj_type         = 0    // Proximity
    	fj_box_left     = 617
    	fj_box_top      = 1941
    	fj_box_right    = 667
    	fj_box_bottom   = 1998
    	fj_cursor_idx   = 0 
    	fj_flags		= 0x8 // Trap detectable
    	fj_vertex_0     = 647 + (1941 << 16)
    	fj_vertex_1     = 667 + (1947 << 16)
    	fj_vertex_2     = 637 + (1998 << 16)
    	fj_vertex_3     = 617 + (1994 << 16)
    	fj_trap_detect 	= 60 // trap detection difficulty percentage;
		fj_trap_remove 	= 90 // trap removal difficulty percentage;
		fj_trap_active 	= 1  // whether the region is trapped (0=no, 1=yes)
		fj_trap_status 	= 0  // whether the trap is detected (0=no, 1=yes)
		fj_loc_x 		= 700  // trap launch X coordinate
		fj_loc_y 		= 2150 // trap launch Y coordinate
    	STR_VAR
    	fj_structure_type   = region
    	fj_name             = UBTrap2
    	fj_reg_script		= EVALUATE_BUFFER "%tutu_var%GTAR7"
  	END
	LPF fj_are_structure
    	INT_VAR
    	fj_type         = 0    // Proximity
    	fj_box_left     = 2120
    	fj_box_top      = 2245
    	fj_box_right    = 2130
    	fj_box_bottom   = 2285
    	fj_cursor_idx   = 0 
    	fj_flags		= 0x8 // Trap detectable
    	fj_vertex_0     = 2120 + (2245 << 16)
    	fj_vertex_1     = 2130 + (2245 << 16)
    	fj_vertex_2     = 2130 + (2285 << 16)
    	fj_vertex_3     = 2120 + (2285 << 16)
    	fj_trap_detect 	= 60 // trap detection difficulty percentage;
		fj_trap_remove 	= 90 // trap removal difficulty percentage;
		fj_trap_active 	= 1  // whether the region is trapped (0=no, 1=yes)
		fj_trap_status 	= 0  // whether the trap is detected (0=no, 1=yes)
		fj_loc_x 		= 1960  // trap launch X coordinate
		fj_loc_y 		= 2280 // trap launch Y coordinate
    	STR_VAR
    	fj_structure_type   = region
    	fj_name             = UBTrap4
    	fj_reg_script		= EVALUATE_BUFFER "%tutu_var%GTAR7"
  	END
	// Move Polar bears to a side cave and replace with snow golems
	READ_LONG 0x54 actOff
	actIdx = 0
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
    	READ_ASCII actOff + 0x80 char
    	PATCH_IF ("%char%" STR_EQ "%tutu_var%BEARPO") BEGIN
			LPF fj_are_structure
				INT_VAR
				fj_delete_mode = actIdx  // Actor to delete
				STR_VAR
				fj_structure_type = actor
			END
		END ELSE BEGIN
			actIdx += 1
			actOff += 0x110
		END
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 200
    	fj_loc_y          = 2000
    	fj_dest_x         = 200
    	fj_dest_y         = 2000
    	fj_animation      = 0x7203 // Polar bear
    	fj_orientation    = 6      // NW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Polar Bear 1"
    	fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%BEARPO"
  	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 300
    	fj_loc_y          = 2050
    	fj_dest_x         = 300
    	fj_dest_y         = 2050
    	fj_animation      = 0x7203 // Polar bear
    	fj_orientation    = 6      // NW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Polar Bear 2"
    	fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%BEARPO"
  	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 1550
    	fj_loc_y          = 2410
    	fj_dest_x         = 1550
    	fj_dest_y         = 2410
    	fj_animation      = 0x7001 // Ogrillon
    	fj_orientation    = 6      // NW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Snow Golem 1"
    	fj_cre_resref     = ubsnogol
  	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 1050
    	fj_loc_y          = 2330
    	fj_dest_x         = 1050
    	fj_dest_y         = 2330
    	fj_animation      = 0x7001  // Ogrillon
    	fj_orientation    = 10      // NE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Snow Golem 2"
    	fj_cre_resref     = ubsnogol
  	END
BUT_ONLY

// Add variable to Dezkiel's Enemy() actions
<<<<<<<< .../bg1ub/ub_dezkie.d
REPLACE_ACTION_TEXT ~%tutu_var%DEZKIEL~
~Enemy()~
~SetGlobal("UBFightDezkiel","GLOBAL",1) Enemy() Shout(314)~
>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../bg1ub/ub_dezkie.d~

// Add Snow Golem
ACTION_IF GAME_IS ~totsc~ THEN BEGIN
	COPY ~bg1ub/ar1010/snow_golem/ubsnorng_totsc.itm~ ~override/ubsnorng.itm~
END ELSE BEGIN
	COPY ~bg1ub/ar1010/snow_golem/ubsnorng.itm~ override
END
COPY ~bg1ub/ar1010/snow_golem/ubsnofst.itm~ override

ACTION_IF GAME_IS ~totsc~ THEN BEGIN
	COPY ~bg1ub/ar1010/snow_golem/ubsnobr_totsc.spl~ ~override/ubsnobr.spl~
END ELSE BEGIN
	COPY ~bg1ub/ar1010/snow_golem/ubsnobr.spl~ override
END
COPY_EXISTING ~ubsnobr.spl~ override
	SAY NAME1 @1002
	WRITE_ASCIIE 0x10 ~%tutu_var%REVEN01~ #8

COPY ~bg1ub/ar1010/snow_golem/ubsnogol.cre~ override
    LAUNCH_PATCH_MACRO ~source_cre_sound_cleanup~
    LAUNCH_PATCH_MACRO ~source_cre_script_cleanup~
    LAUNCH_PATCH_MACRO ~cre_source_res~
	SAY NAME1 			@1001 // Snow golem
	SAY NAME2 			@1001 // Snow golem
    SAY BATTLE_CRY1 	~[ZOMBIE 03]~ [%tutu_var%ZOMBI03]
    SAY BATTLE_CRY2 	~[ZOMBIE 01] ~ [%tutu_var%ZOMBI01]
    SAY ATTACK1 		~[REVENANT 05] ~ [%tutu_var%REVEN05]
    SAY ATTACK1 		~[REVENANT 06] ~ [%tutu_var%REVEN06] 
    SAY DAMAGE 			~[REVENANT 08] ~ [%tutu_var%REVEN08]
    SAY DYING 			~[REVENANT 09] ~ [%tutu_var%REVEN09]
	ADD_CRE_ITEM ~ubsnorng~ #0 #0 #0 ~UNDROPPABLE~ ~RRING LRING~
	ADD_CRE_ITEM ~ubsnofst~ #0 #0 #0 ~UNDROPPABLE~ ~WEAPON1~ EQUIP
	
COMPILE EVALUATE_BUFFER ~bg1ub/ar1010/snow_golem/ubsnogol.baf~
COMPILE EVALUATE_BUFFER ~bg1ub/ar1010/snow_golem/ubdezovr.baf~

COPY_EXISTING ~%tutu_var%DEZKIE.cre~ override
	// Give Dezkiel new override script
	WRITE_ASCIIE SCRIPT_OVERRIDE ~UBDEZOVR~ #8
	// Correcting errors in Dezkiel's CRE
	WRITE_BYTE 0x0235 9  // Dezkiel has 5th level spells, should be 9th level mage
	WRITE_BYTE 0x0052 13 // THAC0 for 8th level fighter is 13
	WRITE_BYTE 0x006f 2  // Small Swords
	WRITE_BYTE 0x0072 2  // Missile
	WRITE_BYTE 0x0075 2  // Blunt	
	// Give Dezkiel some weapons!
	ADD_CRE_ITEM ~%tutu_var%STAF01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
	ADD_CRE_ITEM ~%tutu_var%DAGG05~ #10 #0 #0 ~IDENTIFIED~ ~WEAPON2~
	// Replace fire-area of effect spells that might harm golems
	REMOVE_MEMORIZED_SPELL ~SPWI304~  // Remove Fireball
	REMOVE_MEMORIZED_SPELL ~SPWI217~  // Remove Aganazzar's Scorcher
	ADD_MEMORIZED_SPELL ~SPWI303~ #2 ~wizard~ ( 2 ) // Add Flame Arrow
	ADD_MEMORIZED_SPELL ~SPWI211~ #1 ~wizard~ ( 1 ) // Add Melf's Acid Arrow
BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Mysterious Vial                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2

// Author: SimDing0
// Email: SimDing0@hotmail.com
// Website: http://www.pocketplane.net

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBVial.UB~


ACTION_IF GAME_IS ~tutu tutu_totsc bgee~ THEN BEGIN
COMPILE EVALUATE_BUFFER ~bg1ub/vial/cpm/ubvial.d~

END ELSE BEGIN
  ACTION_IF GAME_IS ~bgt~ THEN BEGIN
    COMPILE EVALUATE_BUFFER ~bg1ub/vial/cpm/ubvial.d~
~bg1ub/vial/cpm/ubvial_addBGT.d~
USING ~bg1ub/tra/%LANGUAGE%/ubvial.tra~
  END ELSE BEGIN
    COMPILE ~bg1ub/vial/ubvial.d~
  END
END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Additional Elminster Encounter (Outside FAI)     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

// Author: SimDing0
// Email: SimDing0@hotmail.com
// Website: http://www.pocketplane.net

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBElminster.UB~

// Elminster encounter outside FAI
LAF replacetext_area_script STR_VAR area="%FriendlyArmInn%" oldtext=~SetGlobal("ElminsterSpawn","GLOBAL",1)~ newtext=~SetGlobal("ElminsterSpawn","GLOBAL",2)~ END
LAF replacetext_area_script STR_VAR area="%FriendlyArmInn%" oldtext=~Global("ElminsterSpawn","GLOBAL",0)~ newtext=~Global("ElminsterSpawn","GLOBAL",1)~ END

// Potential situation here where player enters FAI area during chapter 4 but doesn't meet Elminster.
// If he comes back later on, Elminster could be talking about events that have already passed.
// His dialogues would also be out of order and disjointed. Worth worrying about?

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Angelo Notices Shar-teel                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE
//FORBID_FILE ~override/X#BG1NPCPhase2.G3~ @90000

// Author: Ascension64
// Email: ascension6400@yahoo.com.au
// Website: http://forums.spellholdstudios.net/index.php?showforum=261
// Originally part of the BGT Tweak Pack

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBSharTeel.UB~

COMPILE EVALUATE_BUFFER ~bg1ub/sharteel/rANGELO.D~

LAF extend_area_script STR_VAR area="%SWBaldursGate_FlamingFistHQ_L1%" bottom="bg1ub/sharteel/xAR0607.BAF" END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Finishable Kagain Caravan Quest                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @5
FORBID_FILE ~override/X#BG1NPCPhase2.G3~ @90000

// Author: Ascension64
// Email: ascension6400@yahoo.com.au
// Website: http://forums.spellholdstudios.net/index.php?showforum=261
// Originally part of the BGT Tweak Pack

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBKagain.UB~

LAF extend_area_script STR_VAR area="%CoastWay%" bottom="bg1ub/kagain/aAR2800.BAF" END
LAF replacetext_area_script STR_VAR area="%CoastWay%" oldtext=~ActionOverride("Kagain",Dialog\(ue\)?(\[PC\]))~ newtext=~SetGlobalTimer("Kagain","GLOBAL",ONE_DAY)~ END

EXTEND_BOTTOM ~%tutu_var%KAGAIN.BCS~ ~bg1ub/kagain/aKAGAIN.BAF~

ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/kagain/bgKAGAIJ.D~
END

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/kagain/bgeeKAGAIJ.D~
END

COMPILE EVALUATE_BUFFER ~bg1ub/kagain/KAGAIJ.D~

COPY ~bg1ub/kagain/DLOSTKAG.CRE~ ~override~
  SAY NAME1 @6
  SAY NAME2 @6

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Coran and the Wyverns                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @7
FORBID_FILE ~override/X#BG1NPCCore.G3~ @90001

// Author: Idobek
// Email: idobek@gibberlings3.net
// Website: http://www.gibberlings3.net

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBCoran.UB~

//Ascension64: makes Coran's wyverns unique
ACTION_IF !GAME_IS ~bgt~ THEN BEGIN //BG, BGEE and Tutu
  COPY_EXISTING ~%tutu_var%WYVERN.CRE~ ~override/X#CORWYV.CRE~
    WRITE_ASCII 0x280 ~X#CoranWyvern~ #32 //script name


  COPY_EXISTING ~%CloakwoodWyverns_WyvernCave%.ARE~ ~override~
    READ_LONG 0x54 actorsOff
    READ_SHORT 0x58 actorsNum
    FOR (i = 0; i < %actorsNum%; i += 1) BEGIN
      READ_ASCII (%actorsOff% + %i% * 0x110 + 0x80) actorResref
      PATCH_IF !("%actorResref%" STRING_COMPARE_CASE "%tutu_var%WYVERN") BEGIN
        WRITE_ASCII (%actorsOff% + %i% * 0x110 + 0x80) X#CORWYV (8)
      END
    END
  BUT_ONLY

  COPY_EXISTING ~%tutu_var%CORAN.BCS~ ~override~
    DECOMPILE_AND_PATCH BEGIN
    	REPLACE_TEXTUALLY ~!Dead("wyvern")~ ~!Dead("X#CoranWyvern")~
    	REPLACE_TEXTUALLY ~Dead("wyvern")~ ~Dead("X#CoranWyvern")~
    END
  BUT_ONLY

  ACTION_IF GAME_IS ~tutu tutu_totsc bgee~ THEN BEGIN //Tutu BGEE
    COMPILE EVALUATE_BUFFER ~bg1ub/coran/cpm/Wyvern.d~
  END ELSE BEGIN //BG
    COMPILE ~bg1ub/coran/Wyvern.d~
  END
END

// IKCoranWyvernFix for BG1, Tutu, BGT (already included in BGEE as DXCoran)
ACTION_IF GAME_IS ~tutu tutu_totsc bgt~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/coran/cpm/Coran.d~
END ELSE BEGIN
  ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
	COMPILE ~bg1ub/coran/Coran.d~
  END
END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Kivan and Tazok                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8
FORBID_FILE ~override/X#BG1NPCPhase2.G3~ @90000

// Author: SimDing0
// Email: SimDing0@hotmail.com
// Website: http://www.pocketplane.net

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBKivan.UB~

COMPILE EVALUATE_BUFFER ~bg1ub/kivan/ubkivan.d~

EXTEND_BOTTOM ~%tutu_var%kivan.bcs~ ~bg1ub/kivan/ubkivan.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Branwen and Tranzig                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @9
FORBID_FILE ~override/X#BG1NPCPhase2.G3~ @90000

// Author: SimDing0
// Email: SimDing0@hotmail.com
// Website: http://www.pocketplane.net

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBBranwen.UB~

COMPILE EVALUATE_BUFFER ~bg1ub/branwen/ubbranwen.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Safana the Flirt                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @10
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

// Author: Dudley
// Email:
// Website: http://www.dudleyville.com

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBSafana.UB~

COPY ~BG1UB/SAFANA/D0IN108.SPL~ ~override~
SAY NAME1 @11

COPY_EXISTING ~%tutu_var%safana.cre~ ~override~
              ~%tutu_var%safana4.cre~ ~override~
              ~%tutu_var%safana6.cre~ ~override~
SPRINT temp_tutu "SPIN108" SPRINT temp_bgt "SPCL311" SPRINT temp_bg "SPIN108" LAUNCH_PATCH_MACRO ~getcpmvarp~
REPLACE_TEXTUALLY ~%temp_cpmvar%~ ~D0IN108~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Appropriate Albert and Rufie Reward              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @12
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBAlbert.UB~

COMPILE EVALUATE_BUFFER ~bg1ub/albert/albert.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Place Entar Silvershield in His Home             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @13

// Author: SimDing0
// Email: SimDing0@hotmail.com
// Website: http://www.pocketplane.net

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBEntar.UB~

// ENTAR.DLG - has a Shout(1) and runs away, but the nearby guard will still start dialogue with the player.
// Only one guard is called, no new ones are made.

// Place Entar Silvershield in his home
<<<<<<<< .../bg1ub/et_0101.baf
IF
	!GlobalGT("Chapter","GLOBAL",%tutu_chapter_6%)
	Global("D0CreateEntar","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("D0CreateEntar","GLOBAL",1)
		CreateCreature("%tutu_var%Entar",[649.904]%FACE_12%)
END

IF
	GlobalGT("Chapter","GLOBAL",%tutu_chapter_6%)
	Exists("%tutu_var%Entar")
THEN
	RESPONSE #100
		ActionOverride("Entar",DestroySelf())
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%NWBaldursGate_SilvershieldEstate_L1%" bottom=".../bg1ub/et_0101.baf" END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Scar and the Sashenstar's Daughter               \\\\\
/////        - Scar Quest Extention(s)                 \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @14

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBScar.UB~

COMPILE EVALUATE_BUFFER ~bg1ub/scar/ubscar.d~
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/scar/ubscar_fixes.d~
END

COPY_EXISTING ~%tutu_scriptbg%MISC79.ITM~ ~override/UBFEBODY.ITM~ // Create a unique item for the Sashenstar's daughter
  SAY NAME1 @15
  SAY NAME2 @15
  SAY DESC @16

COPY ~bg1ub/scar/ubscarin.itm~ ~override~ // unique item for the ring
  SAY NAME1 @113
  SAY NAME2 @113
SAY DESC @114


COPY_EXISTING ~%WSewers%.ARE~ ~override~
PATCH_IF (SOURCE_SIZE > 0xd4) BEGIN
  READ_LONG 0x70 contOff  // Offset to containers
  READ_LONG 0x78 itmOff   // Offset to item list
  FOR (READ_SHORT 0x74 numCont; numCont; numCont -= 0x1) BEGIN
    READ_ASCII contOff contName (11)    // Container name
    READ_SHORT contOff + 0x20 contXloc  // Container X location
    READ_SHORT contOff + 0x22 contYloc  // Container Y location
    // Check by name and location, in case name is modified by FotD
    PATCH_IF (!("%contName%" STRING_COMPARE_CASE "CONTAINER 1") ||
              ((contXloc == 1924) && (contYloc == 1888))) BEGIN
      READ_LONG contOff + 0x40 itmIdx // Index of first item in list
      myItmOff = itmOff + (itmIdx * 0x14)
      FOR (READ_LONG contOff + 0x44 itmCnt; itmCnt; itmCnt -= 0x1) BEGIN
        READ_ASCII myItmOff itmRef // Item resource reference
        PATCH_IF !("%itmRef%" STRING_COMPARE_CASE "%tutu_var%MISC80") BEGIN
          WRITE_ASCIIE myItmOff ~%tutu_scriptbg%MISC79~ #8 // Female body
        END
        myItmOff += 0x14
      END
    END
    contOff += 0xc0
  END
END
BUT_ONLY

COPY_EXISTING ~%ESewers%.ARE~ ~override~
PATCH_IF (SOURCE_SIZE > 0xd4) BEGIN
  READ_LONG 0x70 contOff  // Offset to containers
  READ_LONG 0x78 itmOff   // Offset to item list
  FOR (READ_SHORT 0x74 numCont; numCont; numCont -= 0x1) BEGIN
    READ_ASCII contOff contName (11)   // Container name
    READ_SHORT contOff + 0x20 contXloc // Container X location
    READ_SHORT contOff + 0x22 contYloc // Container Y location
    // Check by name and location, in case name is modified by FotD
    PATCH_IF (!("%contName%" STRING_COMPARE_CASE "CONTAINER 1") ||
              ((contXloc == 827) && (contYloc == 1509))) BEGIN
      READ_LONG contOff + 0x40 itmIdx // Index of first item in list
      myItmOff = itmOff + (itmIdx * 0x14)
      FOR (READ_LONG contOff + 0x44 itmCnt; itmCnt; itmCnt -= 0x1) BEGIN
        READ_ASCII myItmOff itmRef // Item resource reference
        PATCH_IF !("%itmRef%" STRING_COMPARE_CASE "%tutu_var%MISC80") BEGIN
          WRITE_ASCII myItmOff ~UBFEBODY~ #8
        END
        myItmOff += 0x14
      END
    END ELSE
    PATCH_IF (!("%contName%" STRING_COMPARE_CASE "CONTAINER 4") ||
              ((contXloc == 398) && (contYloc == 1414))) BEGIN
      READ_LONG contOff + 0x40 itmIdx // Index of first item in list
      READ_LONG contOff + 0x44 itmCnt // Number of items
      READ_SHORT 0x76 numItm  // Total number of items
      myItmOff = itmOff + (itmIdx * 0x14)
      PATCH_RANDOM_SEED myItmOff << (numItm / itmCnt)
      bodyCount = 0x0
      // Make sure there's more than one body before deciding which to replace
      FOR (i = itmCnt; i; i -= 0x1) BEGIN
        READ_ASCII myItmOff itmRef // Item resource reference
        PATCH_IF !("%itmRef%" STRING_COMPARE_CASE "%tutu_var%MISC80") BEGIN
          bodyCount += 0x1 // Count those bodies!
        END
        myItmOff += 0x14
      END
      PATCH_IF (bodyCount) BEGIN
        PATCH_IF (bodyCount > 1) BEGIN
          theBody = RANDOM(1 bodyCount) // Random body replacement
        END ELSE theBody = 0x1
        myItmOff = itmOff + (itmIdx * 0x14)
        bodyCount = 0x1
        FOR (i = itmCnt; i; i -= 0x1) BEGIN
          READ_ASCII myItmOff itmRef // Item resource reference
          PATCH_IF !("%itmRef%" STRING_COMPARE_CASE "%tutu_var%MISC80") BEGIN
            PATCH_IF (bodyCount == theBody) BEGIN
              WRITE_ASCIIE myItmOff ~%tutu_scriptbg%MISC79~ #8 // Female body
            END
            bodyCount += 0x1
          END
          myItmOff += 0x14
        END
      END
    END
    contOff += 0xc0
  END
END
BUT_ONLY

COPY_EXISTING ~%ESewers%.ARE~ ~override~
 // PATCH_IF SOURCE_SIZE > 0x28f BEGIN
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR container_to_add_to = 1
      STR_VAR item_to_add = UBSCARIN //custom ring
 //   END
  END
BUT_ONLY


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Quoningar, the Cleric                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @17

// Author: devSin
// Email:
// Website:

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBQuoningar.UB~

COPY ~bg1ub/quoningar/%tutuorbgt%/ubquon.cre~ ~override~

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/quoningar/cpm/ubquon.d~
END ELSE BEGIN
  COMPILE ~bg1ub/quoningar/ubquon.d~
END

ADD_JOURNAL @20176 @20177 USING ~bg1ub/tra/%LANGUAGE%/extra.tra~ // Add journal entries for BGEE

// Add Quoningar to Three Old Kegs L2
COPY_EXISTING ~%NBaldursGate_ThreeOldKegs_L2%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 464
    	fj_loc_y          = 157
    	fj_dest_x         = 464
    	fj_dest_y         = 157
    	fj_animation      = 0x6002 //cleric male dwarf
    	fj_orientation    = 0      //S
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Quoningar
   		fj_cre_resref     = ubquon
	END
BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Shilo Chen and the Ogre-Magi                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @18
//FORBID_FILE ~override/NTSHILOC.CRE~ @90003
//FORBID_FILE ~data/NTotSC1.bif~ @90003
FORBID_FILE ~backup_NTotSC/dialog.tlk~ @90003

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBShiloChen.UB~

COPY ~bg1ub/shilochen/IBRACUB.BAM~ ~override~

COPY ~bg1ub/shilochen/%tutuorbgt%/UBGLOVE1.ITM~ ~override~
  SAY NAME1 #6341
  SAY NAME2 @19
  SAY UNIDENTIFIED_DESC #6791
  SAY IDENTIFIED_DESC @20

COPY_EXISTING ~BEARBL.CRE~ ~override/UBDBEAR.CRE~
  WRITE_LONG 0x010 5 // Permanent corpse
  WRITE_LONG 0x014 0 // XP Value
  WRITE_LONG 0x020 2048 // STATE_DEAD
  WRITE_ASCII 0x280 ~UBDBEAR~

COPY_EXISTING ~%tutu_scripto%GREMA_A.CRE~ ~override/UBOGMA01.CRE~
  WRITE_ASCII 0x280 ~UBOGMA01~ #8

COPY_EXISTING ~%tutu_scripto%GREMA_B.CRE~ ~override/UBOGMA02.CRE~
  WRITE_ASCII 0x280 ~UBOGMA02~ #8

COPY_EXISTING ~%tutu_var%AOLN.CRE~ ~override/UBSHILO.CRE~
  SAY NAME1 #15640
  SAY NAME2 #15685
  WRITE_ASCII 0x250 ~~ #8
  WRITE_ASCII 0x280 ~UBShilo~ #8
  WRITE_ASCII 0x2cc ~ubshilo~ #8
  ADD_CRE_ITEM ~UBGLOVE1~ #0 #0 #0 ~NONE~ ~INV1 INV5~

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/shilochen/cpm/ubshilo.d~
END ELSE BEGIN
  COMPILE ~bg1ub/shilochen/ubshilo.d~
END

ADD_JOURNAL @20178 @20179 USING ~bg1ub/tra/%LANGUAGE%/extra.tra~ // Add journal entries for BGEE

// Add Shilo to Elfsong Tavern
COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L1%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 606
    	fj_loc_y          = 967
    	fj_dest_x         = 606
    	fj_dest_y         = 967
    	fj_animation      = 0x6100  // Fighter male human
    	fj_orientation    = 10      // NE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Shilo Chen"
   		fj_cre_resref     = ubshilo
	END
BUT_ONLY

<<<<<<<< .../bg1ub/ub_ar2900.baf
IF
	GlobalGT("Chapter","GLOBAL",4)
	Global("AcceptedUBShiloQuest","GLOBAL",1)
	Global("CreateUBOgreMagi","%Larswood%",0)
THEN
RESPONSE #100
	CreateCreature("UBDBEAR",[1025.3145]%FACE_10%)
	CreateCreature("UBOGMA01",[1075.3200]%FACE_6%)
	CreateCreature("UBOGMA02",[950.3175]%FACE_12%)
	CreateCreature("%tutu_var%OGREHA",[910.3025]%FACE_14%)
	CreateCreature("%tutu_var%OGREHA",[1000.3275]%FACE_8%)
	CreateCreature("%tutu_var%OGREHA",[1190.3240]%FACE_8%)
	SetGlobal("CreateUBOgreMagi","%Larswood%",1)
END

IF
	Global("AcceptedUBShiloQuest","GLOBAL",2)
	Dead("UBOGMA01")
	Dead("UBOGMA02")
THEN
RESPONSE #100
	ActionOverride("UBDBEAR",DestroySelf())
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Larswood%" bottom=".../bg1ub/ub_ar2900.baf" END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Edie, the Merchant League Applicant              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @21

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBEdie.UB~

COPY_EXISTING ~%tutu_var%EDIE.CRE~ ~override~
  WRITE_LONG 0x1c 50              // Give 50 gold
  WRITE_ASCII 0x2cc ~UBEDIE~ #8   // Assigns edited dialog file
  WRITE_ASCII 0x280 ~Edie~ #32     // Assigns death variable
BUT_ONLY

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/edie/cpm/ubedie.d~
END ELSE BEGIN
  COMPILE ~bg1ub/edie/ubedie.d~
END

ADD_JOURNAL @17 @18 @19 USING ~bg1ub/tra/%LANGUAGE%/ubedie.tra~ // Add journal entries for BGEE

// Add Edie to Merchant League L2
COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 1102
    	fj_loc_y          = 917
    	fj_dest_x         = 1102
    	fj_dest_y         = 917
    	fj_animation      = 0xB510  // Static peasant woman
    	fj_orientation    = 4       // W
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Edie
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%EDIE"
	END
BUT_ONLY

/* <<<<<<<< .../bg1ub/ub_ar0128.baf
IF
Global("UB_EDIE_SPAWN","GLOBAL",0)
!Exists("Edie")
!Dead("Edie")
THEN
RESPONSE #100
CreateCreature("%tutu_var%EDIE",[1102.917]%FACE_4%)
SetGlobal("UB_EDIE_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%SWBaldursGate_MerchantLeague_L2%" bottom=".../bg1ub/ub_ar0128.baf" END
*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Flaming Fist Mercenary Reinforcements            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @22
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

// Author: devSin
// Email:
// Website:

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBFlamingFist.UB~

COPY ~bg1ub/ff/%tutuorbgt%/UBFLAM5.CRE~ ~override~

COPY_EXISTING ~%tutu_var%FLAM5.CRE~ ~override~ // Flaming Fist Mercenary
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 ~FLAM5~ #32
    FOR (loc = 0x248; loc < 0x270; loc += 0x8) BEGIN
      READ_ASCII loc script
      PATCH_IF !("%script%" STRING_COMPARE_CASE "%tutu_scripts%EEENEMY") BEGIN
        // Remove SeeEnemy script, otherwise they just attack each other :D
        WRITE_ASCII loc "" #8
      END
    END
  END
BUT_ONLY

// Add Reinforcements to South Beregost Road
COPY_EXISTING ~%SouthBeregostRoad%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 3753
    	fj_loc_y          = 2954
    	fj_dest_x         = 3753
    	fj_dest_y         = 2954
    	fj_animation      = 0x6000  // Cleric Male Human
    	fj_orientation    = 9       // ENE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Flaming Fist Merc 5a"
   		fj_cre_resref     = UBFLAM5
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 3869
    	fj_loc_y          = 2962
    	fj_dest_x         = 3869
    	fj_dest_y         = 2962
    	fj_animation      = 0x6000  // Cleric Male Human
    	fj_orientation    = 7       // ENE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Flaming Fist Merc 5b"
   		fj_cre_resref     = UBFLAM5
	END
BUT_ONLY

/* <<<<<<<< .../bg1ub/eb_ar3800.baf
IF
Global("UB_FLAM5_REINFORCE","GLOBAL",0)
Exists("FLAM5")
!Dead("FLAM5")
THEN
RESPONSE #100
CreateCreature("UBFLAM5",[3753.2954]%FACE_9%) // Flaming Fist Mercenary
CreateCreature("UBFLAM5",[3869.2962]%FACE_7%) // Flaming Fist Mercenary
SetGlobal("UB_FLAM5_REINFORCE","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%SouthBeregostRoad%" bottom=".../bg1ub/eb_ar3800.baf" END
*/

<<<<<<<< .../bg1ub/ubflam5.baf
IF
Global("UB_FLAM5_SHOUT","GLOBAL",1)
Allegiance(Myself,128)
THEN
RESPONSE #100
Enemy()
END

IF
Global("UB_FLAM5_SHOUT","GLOBAL",2)
Allegiance(Myself,128)
THEN
RESPONSE #100
EscapeArea()
END

IF
Allegiance("FLAM5",255)
Allegiance(Myself,128)
THEN
RESPONSE #100
SetGlobal("UB_FLAM5_SHOUT","GLOBAL",1)
Enemy()
END

IF
AttackedBy([30],0)
Allegiance(Myself,128)
THEN
RESPONSE #100
SetGlobal("UB_FLAM5_SHOUT","GLOBAL",1)
Enemy()
END

IF
!Exists("FLAM5")
!Dead("FLAM5")
Allegiance(Myself,128)
THEN
RESPONSE #100
SetGlobal("UB_FLAM5_SHOUT","GLOBAL",2)
EscapeArea()
END

IF
!Range("FLAM5",12)
!Dead("FLAM5")
Global("UB_FLAM5_SHOUT","GLOBAL",0)
Allegiance(Myself,128)
THEN
RESPONSE #100
MoveToObject("FLAM5")
END
>>>>>>>>
COMPILE ~.../bg1ub/ubflam5.baf~

COPY_EXISTING ~%tutu_var%FLAM5.DLG~ ~override~
  DECOMPILE_AND_PATCH
  	BEGIN
  		REPLACE_TEXTUALLY ~Enemy()~ ~SetGlobal("UB_FLAM5_SHOUT","GLOBAL",1) Enemy()~
  		REPLACE_TEXTUALLY ~EscapeArea()~ ~SetGlobal("UB_FLAM5_SHOUT","GLOBAL",2) EscapeArea()~
  	END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Creature Corrections                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @23
//DEPRECATED ~Ascension64~

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBCreCorrect.UB~

// Add missing IDS entries.
APPEND RACE.IDS ~114 SUCCUBUS~ UNLESS ~114~
APPEND ANIMATE.IDS "0x1000 WYVERN_BIG"       UNLESS "^0x1000"
APPEND ANIMATE.IDS "0x1100 TANARRI"          UNLESS "^0x1100"
APPEND ANIMATE.IDS "0x2300 DEATH_KNIGHT"     UNLESS "^0x2300"
APPEND ANIMATE.IDS "0x6405 DOOM_GUARD"       UNLESS "^0x6405"
APPEND ANIMATE.IDS "0x7904 SLIME_GRAY"       UNLESS "^0x7904"
APPEND ANIMATE.IDS "0x7D00 ZOMBIE"           UNLESS "^0x7D00"
APPEND ANIMATE.IDS "0x7E00 WEREWOLF"         UNLESS "^0x7E00"
APPEND ANIMATE.IDS "0x7E01 WEREWOLF_GREATER" UNLESS "^0x7E01"
APPEND ANIMATE.IDS "0xD400 BIRD_INSIDE"      UNLESS "^0xD400"

// Change Zekar's alignment to NE.
COPY_EXISTING ~%tutu_var%ZEKAR.CRE~ ~override~
  WRITE_BYTE 0x27b 35
BUT_ONLY

// Give Karoug a script that exists. He doesn't need anything really since his dialog handles transformation.
COPY_EXISTING ~%tutu_var%KAROUG.CRE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x258 ~%tutu_scriptw%TASIGHT~ #8
  END
BUT_ONLY

// Galkin.cre isn't actually used ingame. Galken.cre is the same guy, but the version that's used. However,
// the former is classified as an "innocent", which seems more correct than the latter's choice of "mage".
// Thus, correct the classification and fix the invalid dialog reference.
COPY_EXISTING ~%tutu_var%GALKIN.CRE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x2cc ~%tutu_var%galken~ #8
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%GALKEN.CRE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_BYTE 0x273 155
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%BENTLY.CRE~ ~override~ // Bently Mirrorshade, FAI
  WRITE_ASCIIE 0x248 ~BENTLY~ #8       // Assigns BENTLY.BCS as the OVERRIDE script
  WRITE_ASCIIE 0x250 ~%tutu_var%SHOUT~ #8        // Assigns SHOUT.BCS as the Class script
BUT_ONLY

// assign creatures their correct dialogs
COPY_EXISTING ~%tutu_var%FTOBE7.CRE~ ~override~ // Female townsperson in AR3335
              ~%tutu_var%MTOB9.CRE~ ~override~  //
              ~%tutu_var%MTOB4.CRE~ ~override~  //
              ~%tutu_var%MTOB5.CRE~ ~override~  //
              ~%tutu_var%READ4.CRE~ ~override~  //
  WRITE_ASCIIE 0x2cc ~%SOURCE_RES%~ #8
BUT_ONLY

COPY_EXISTING ~%tutu_var%OGREGR.CRE~ ~override~   // Assigns the Ogrillon creatures the Ogrillon script
              ~%tutu_var%OGREGR1.CRE~ ~override~
              ~%tutu_var%OGREGR2.CRE~ ~override~
              ~%tutu_var%OGREGR3.CRE~ ~override~
              ~%tutu_var%OGREGR4.CRE~ ~override~
              ~%tutu_scripto%GREGRSU.CRE~ ~override~
              ~%tutu_scripto%GREGR_A.CRE~ ~override~
              ~%tutu_scripto%GREGR_B.CRE~ ~override~
              ~%tutu_scripto%GREGR_C.CRE~ ~override~
              ~%tutu_scripto%GREGR_D.CRE~ ~override~
              ~%tutu_scripto%GREMIRI.CRE~ ~override~
  WRITE_ASCIIE 0x258 ~%tutu_scripto%GRILLON~ #8
BUT_ONLY

COPY_EXISTING ~%tutu_var%OGREHA.CRE~ ~override~   // Assigns the Halfogres the Halfogre script
              ~%tutu_var%OGREHA1.CRE~ ~override~
              ~%tutu_var%OGREHA2.CRE~ ~override~
              ~%tutu_var%OGREHA3.CRE~ ~override~
              ~%tutu_var%OGREHA4.CRE~ ~override~
              ~%tutu_var%OGREHA5.CRE~ ~override~
              ~%tutu_scripto%GREHA_A.CRE~ ~override~
              ~%tutu_scripto%GREHA_B.CRE~ ~override~
              ~%tutu_scripto%GREHA_C.CRE~ ~override~
              ~%tutu_scripto%GREHA_D.CRE~ ~override~
              ~%tutu_scripto%GREHA_E.CRE~ ~override~
  WRITE_ASCIIE 0x258 ~%tutu_scripth%ALFOGRE~ #8
BUT_ONLY

COPY_EXISTING ~%tutu_var%ITASLOI.CRE~ ~override~  // Assigns the Taslois the TasloiA script
              ~%tutu_scriptt%ASLGURK.CRE~ ~override~
              ~%tutu_var%TASLOI.CRE~ ~override~
              ~%tutu_scriptt%ASLOI02.CRE~ ~override~
              ~%tutu_scriptt%ASLOI03.CRE~ ~override~
              ~%tutu_scriptt%ASLOISU.CRE~ ~override~
  WRITE_ASCIIE 0x258 ~%tutu_var%TASLOIA~
BUT_ONLY

COPY_EXISTING ~%tutu_var%WOLFWE.CRE~ ~override~ // Changes Class from Werewolf to Wolfwere
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_BYTE 0x273 158
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%GHOUL.CRE~ ~override~   // Assigns the Ghouls the Ghoul script
              ~%tutu_var%GHOULSU.CRE~ ~override~
  WRITE_ASCIIE 0x258 ~%tutu_var%GHOUL~
BUT_ONLY

COPY_EXISTING ~%tutu_var%WOLFWI.CRE~ ~override~ // Assigns the Winter Wolf the Winter Wolf script
  WRITE_ASCIIE 0x258 ~%tutu_scriptw%NTRWOLF~
BUT_ONLY

COPY_EXISTING ~%tutu_var%WOLFVA.CRE~ ~override~ // Assigns the Vampiric Wolf the Vampiric Wolf script
  WRITE_ASCIIE 0x258 ~%tutu_scriptv%AMPWOLF~
BUT_ONLY

COPY_EXISTING ~%tutu_var%FEARM.CRE~ ~override~ // Assigns Fear the Revenant script
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x250 ~%tutu_scriptr%EVENANT~
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%REVENT.CRE~ ~override~ // Assigns the Revenant the Revenant script
  WRITE_ASCIIE 0x258 ~%tutu_scriptr%EVENANT~
BUT_ONLY

COPY_EXISTING ~%tutu_var%WINSKI2.CRE~ ~override~ // Assigns Winski Perorate the correct script
  WRITE_ASCIIE 0x250 ~%tutu_var%WINSKI~ #8
BUT_ONLY

COPY_EXISTING ~%tutu_scriptw%YVERNBI.CRE~ ~override~ // Assigns the Greater Wyvern the Wyvern script
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x258 ~%tutu_var%WYVERN~ #8
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%SKELETB.CRE~ ~override~ // Assign death variable
  WRITE_ASCII 0x280 ~SKELETONB~
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBW4.CRE~ ~override~  // Assign death variable
  WRITE_ASCII 0x280 ~Noblewoman4~
BUT_ONLY

COPY_EXISTING ~%tutu_var%KRYLA.CRE~ ~override~ // Kryla (AR1500)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x258 ~%tutu_scriptw%EREWOLF~   // Assign the Werewolf script
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%MEMNIS.CRE~ ~override~ // Memnis (AR1500)
              ~%tutu_var%PALIN.CRE~ ~override~  // Palin (AR1500)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x258 ~%tutu_scriptw%OLFWERE~    // Assign the Wolfwere script
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%DOPPGR.CRE~ ~override~ // Greater Doppelganger
  WRITE_ASCIIE 0x250 ~%tutu_var%DGANGER~ #8  // Assigns the DGANGER script as Class script
BUT_ONLY

COPY_EXISTING ~%tutu_var%HOUSG5.CRE~ ~override~ // Guard
  WRITE_ASCIIE 0x258 ~%tutu_var%DGUARD~ #8   // Assigns the DGUARD script as Race script
BUT_ONLY

COPY_EXISTING ~%tutu_var%DRYAD.CRE~ ~override~ // Dryad of the Cloudpeaks
  WRITE_ASCIIE 0x258 ~%tutu_var%DRYAD~ #8   // Assigns the DRYAD script as Race script
BUT_ONLY

COPY_EXISTING ~%tutu_var%FLAMEN2.CRE~ ~override~ // Flaming Fist Enforcer
  WRITE_ASCIIE 0x258 ~%tutu_var%FLAMEN2~ #8   // Assigns the FLAMEN2 script as Race script
BUT_ONLY

COPY_EXISTING ~%tutu_var%ISLSIR.CRE~ ~override~ // Sirine Queen
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x268 ~%tutu_scripti%SLANDSI~    // Assigns the ISLANDSI script as Default script
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%OBERAN.CRE~ ~override~ // Oberan
  WRITE_ASCIIE 0x248 ~%tutu_scripto%BESTATE~    // Assigns the OBESTATE script as OVERRIDE script
BUT_ONLY

COPY_EXISTING ~%tutu_var%CHICKER.CRE~ ~override~ // Rabid Chicken
  WRITE_ASCIIE 0x248 ~%tutu_scriptr%CHICKEN~     // Assigns the RCHICKEN script as OVERRIDE script
BUT_ONLY

COPY_EXISTING ~%tutu_var%SIL.CRE~ ~override~ // Sil
  WRITE_ASCIIE 0x260 ~%tutu_var%SIL~ #8   // Assigns the SIL script as General script
BUT_ONLY

COPY_EXISTING ~%tutu_var%VOLOSE.CRE~ ~override~ // Serving Wench
  WRITE_ASCIIE 0x258 ~%tutu_var%VWENCH~ #8   // Assigns the VWENCH script as Race script
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE6.CRE~ ~override~ // Galteran (Commoner)
  WRITE_ASCIIE 0x258 ~%tutu_var%MTOWBE6~ #8 // Assigns the MTOWBE6 script as Race script
BUT_ONLY

// assign creatures their correct script death variable
COPY_EXISTING ~%tutu_var%ADDY.CRE~ ~override~ // Addy (BG, Ch 7 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Addy~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%BRENNA.CRE~ ~override~ // Brennan Risling (the other Brennan Risling)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Brenna~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CAEDMO.CRE~ ~override~ // Caedmon (BG, Ch 5 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Caedmo~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptbg%CARBOS.CRE~ ~override~ // Carbos (Candlekeep, Tutorial)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Carbos~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%ELMIN3.CRE~ ~override~ // Elminster (FAI, Ch 3 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Elmin3~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%FEARM.CRE~ ~override~ // Durlag: Fear warder (hostile)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~FearM~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%IVANNE.CRE~ ~override~ // Ivanne (BG, Ch 7 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Ivanne~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%JONAVI.CRE~ ~override~ // Jonavin (BG, Ch 7 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Jonavi~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%KAELLA.CRE~ ~override~ // Kaella (BG, Ch 7 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Kaella~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%KARAN1.CRE~ ~override~ // Karan (Candlekeep, Tutorial)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Karan1~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%PARDA1.CRE~ ~override~ // Parda (Candlekeep, Tutorial)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Parda1~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%PRIDEM.CRE~ ~override~ // Durlag: Pride warder (hostile)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~PrideM~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptbg%SHANK.CRE~ ~override~ // Shank (Candlekeep, Tutorial)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Shank~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scripts%LAVFREE.CRE~ ~override~ // Slave (Cloakwood mine, post-flood spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~SlavFree~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%SORREL.CRE~ ~override~ // Sorrel (BG, Ch 7 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Sorrel~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%SUGAR.CRE~ ~override~ // Sugar (BG ho, Ch 7 spawn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Sugar~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%VOLO.CRE~ ~override~ // Volo (AR4809, Nashkel Tavern)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~Volo~ #18 // dv = 18 bytes
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%ZOMBIEB.CRE~ ~override~ // Bassilus' family zombies
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCIIE 0x280 ~ZombieB~ #18 // dv = 18 bytes
  END
BUT_ONLY
// end death variable assignation

LAF replacetext_area_script STR_VAR area="%NEBaldursGate_BlushingMermaid_L1%" oldtext=~Exists(\"DETRAN\")~ newtext=~Exists("DETRANION")~ END

LAF replacetext_area_script STR_VAR area="%DurlagsTower_L1%" oldtext=~(\"Tourist4\",~ newtext=~("TOURIST3",~ END

LAF replacetext_area_script STR_VAR area="%BaldursGateDocks_IronThrone_L2%" oldtext=~Exists(\"TRALIT\")~ newtext=~Exists("TRALITHAN")~ END

LAF replacetext_area_script STR_VAR area="%BaldursGateDocks_IronThrone_L3%" oldtext=~Exists(\"KALESS\")~ newtext=~Exists("KALESSIA")~ END

LAF replacetext_area_script STR_VAR area="%BaldursGateDocks_IronThrone_L4%" oldtext=~Exists(\"WIRTHI\")~ newtext=~Exists("WIRTHING")~ END

LAF replacetext_area_script STR_VAR area="%BaldursGateDocks_IronThrone_L1%" oldtext=~Exists(\"PANGWA\")~ newtext=~Exists("PANG")~ END
LAF replacetext_area_script STR_VAR area="%BaldursGateDocks_IronThrone_L1%" oldtext=~Exists(\"DHANIA\")~ newtext=~Exists("DHANIAL")~ END

LAF replacetext_area_script STR_VAR area="%SEBaldursGate%" oldtext=~Exists(\"DHANIA\")~ newtext=~Exists("DHANIAL")~ END

COPY_EXISTING ~%tutu_var%LOVE.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  	REPLACE_TEXTUALLY ~ActionOverride(\"AVARICEM\"~ ~ActionOverride("AVARICE"~
  END
BUT_ONLY

// Bears no longer turn hostile if PC druid or ranger is visible
COPY_EXISTING ~%tutu_var%BEAR.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  	REPLACE_TEXTUALLY ~\(\(Range(\[GOODCUTOFF\],7)\|AttackedBy(\[ANYONE\],DEFAULT)\)[ ]+Allegiance(Myself,NEUTRAL)\)~ ~\1 !See([2.0.0.11]) !See([2.0.0.12]) !See([2.0.0.16]) !See([2.0.0.18])~
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%MINSC.DLG~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  	REPLACE_TEXTUALLY ~!Dead(\"Dynahe\"~ ~!Dead("Dynaheir"~
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptb%IRD_INE.CRE~ ~override~
              ~%tutu_scriptb%IRD_INN.CRE~ ~override~
              ~%tutu_scriptb%IRD_INS.CRE~ ~override~
              ~%tutu_scriptb%IRD_INW.CRE~ ~override~
  WRITE_ASCIIE 0x258 ~%tutu_var%RANDFLY~ #8
BUT_ONLY

COPY_EXISTING ~%tutu_var%ENNAHE.CRE~ ~override~ // Enna Hendrik (unused for now)
  WRITE_ASCIIE 0x2cc ~%tutu_var%ENNA~ #8       // Fixes dialog file (ENNAHE does not exist)
BUT_ONLY

COPY_EXISTING ~%tutu_var%ERLINH.CRE~ ~override~ // Erlin Hendrik (unused for now)
  WRITE_ASCIIE 0x2cc ~%tutu_var%ERLIN~ #8      // Fixes dialog file (ERLINH does not exist)
BUT_ONLY

COPY_EXISTING ~%tutu_var%GIRLBAX.CRE~ ~override~ // Girl (unused?)
  WRITE_ASCIIE 0x2cc ~%tutu_var%GIRBA3~ #8      // Fixes dialog file (GIRLBA3 does not exist)
BUT_ONLY

COPY_EXISTING ~%tutu_var%PROSLAY.CRE~ ~override~ // Ho
  WRITE_ASCIIE 0x2cc ~%tutu_var%PROSLA~ #8      // Fixes dialog file (PROSLAY does not exist)
BUT_ONLY

ACTION_IF !GAME_IS ~bgee~ THEN BEGIN  // BOOT01 exists in BGEE, so don't do this block for BGEE
	COPY_EXISTING ~%tutu_var%DEAD2.CRE~ ~override~ // Amnish Soldier (various)
              ~%tutu_scriptd%EADFUCK.CRE~ ~override~ // Amnish Soldier (AR5403, Nashkel Mines)
              ~%tutu_var%DRELIK.CRE~ ~override~ // Drelik         (AR0010, Baldur's Gate)
              ~%tutu_var%JAMIE.CRE~ ~override~ // Jamie          (AR4805, Nashkel manor)
              ~%tutu_var%LOTHAN.CRE~ ~override~ // Lothander      (various, Baldur's Gate)
              ~%tutu_var%SAKUL.CRE~ ~override~ // Sakul          (AR5506, Catacombs)
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
  			READ_LONG 0x2bc itemOff
  			FOR (READ_LONG 0x2c0 numItem; numItem; numItem -= 0x1) BEGIN
    			READ_ASCII itemOff resRef
    			PATCH_IF ("%resRef%" STR_EQ "%tutu_var%SWIH04") BEGIN // Fixes a typo (SWIH04 does not exist)
      				WRITE_ASCIIE itemOff "%tutu_var%SW1H04" #8 // Long Sword
    			END ELSE BEGIN
      				PATCH_IF ("%resRef%" STR_EQ "%tutu_var%SCRL1J") BEGIN // Fixes a typo (SCRL1J does not exist)
        				PATCH_IF ("%SOURCE_RES%" STR_EQ "%tutu_var%SAKUL") BEGIN
          					WRITE_ASCIIE itemOff "%tutu_var%SCRL1I" #8 // Hold Person
        				END ELSE BEGIN
          					WRITE_ASCIIE itemOff "%tutu_var%SCRL1L" #8 // Monster Summoning I
        				END
      				END ELSE BEGIN
        				PATCH_IF ("%resRef%" STR_EQ "%tutu_var%B00T01") BEGIN // Fixes a typo (B00T01 does not exist)
          					WRITE_ASCIIE itemOff "%tutu_var%BOOT02" #8 // Boots of Speed
        				END
      				END
    			END
    			itemOff += 0x14
  			END
		END
	BUT_ONLY
END

//////////////////////////////////////
// Assign weapons to unarmed creatures
COPY_EXISTING ~%tutu_var%ANDRIS.CRE~ ~override~ // Andris (Mage, AR1009, Ice Island)
              ~%tutu_var%POGHM9.CRE~ ~override~ // Priest of Oghma (Doppleganger, AR2627, Candlekeep)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%STAF01~ #0 #0 #0 NONE WEAPON1 EQUIP TWOHANDED // Quarterstaff
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%BEYN.CRE~ ~override~ // Beyn (Fighter/Mage, AR1009, Ice Island)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%DAGG01~ #0 #0 #0 NONE WEAPON1 // Dagger
      ADD_CRE_ITEM ~%tutu_var%DAGG05~ #10 #0 #0 NONE WEAPON2 EQUIP // Throwing Dagger
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptbg%BISHOP.CRE~ ~override~ // Bishop (Cleric, AR0506, Durlag's Tower)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%BLUN03~ #0 #0 #0 NONE WEAPON1 EQUIP // Flail +1
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%BLANE.CRE~ ~override~ // Dawn Priest Blane (Cleric, AR3401, Song of the Morning Temple Vestibule)
              ~%tutu_var%BRAN.CRE~ ~override~ // Dawn Priest Bram (Cleric, AR3401, Song of the Morning Temple Vestibule)
              ~%tutu_var%KAISHA.CRE~ ~override~ // Kaishas Gan (Cleric, AR2002, Werewolf Island)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%BLUN04~ #0 #0 #0 NONE WEAPON1 EQUIP // Mace
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CUCHOL.CRE~ ~override~ // Cuchol (Fighter/Mage, AR1009, Ice Island)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%HAMM01~ #0 #0 #0 NONE WEAPON1 // War Hammer
      ADD_CRE_ITEM ~%tutu_var%DAGG05~ #10 #0 #0 NONE WEAPON2 EQUIP // Throwing Dagger
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%DAITEL.CRE~ ~override~ // Ghost (Fighter/Mage, AR0504, Durlag's Tower)
              ~%tutu_var%DUSHAI.CRE~ ~override~ // Dushai (Cleric/Mage, AR1000, Ulgoth's Beard)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%BLUN06~ #0 #0 #0 NONE WEAPON1 EQUIP // Morning Star
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%DEZKIE.CRE~ ~override~ // Dezkiel (Fighter/Mage, AR1010, Ice Island)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%BLUN01~ #0 #0 #0 NONE WEAPON1 // Club
      ADD_CRE_ITEM ~%tutu_var%DART01~ #20 #0 #0 NONE WEAPON2 EQUIP // Dart
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptd%OGBLINK.CRE~ ~override~ //Blink Dog (created by Jacil, AR0307, NEBaldursGate CountingHouse L1)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2bc itemOff
    READ_LONG 0x2c0 itemNum
    FOR (i = 0; i < itemNum; i+=1) BEGIN
      READ_ASCII (itemOff + 0x14 * i) itemName
      PATCH_IF !("%itemName%" STRING_COMPARE_CASE "%tutu_var%P1-4") BEGIN
        READ_LONG 0x2b8 slotOff
        WRITE_SHORT (slotOff + 9 * 0x2) i //equip P1-4
      END
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%DRADEE.CRE~ ~override~ // Dradeel (Mage, AR1505, Werewolf Island)
              ~%tutu_var%DRADEE2.CRE~ ~override~ // Dradeel (Mage, AR1505, Werewolf Island)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_ASCII 0x268 script
    PATCH_IF !("%script%" STRING_COMPARE_CASE "%tutu_scriptw%TASIGHT") BEGIN
      WRITE_ASCIIE 0x268 ~%tutu_var%WTARSGT~ #8 // Needs EquipMostDamagingMelee()
    END
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%STAF01~ #0 #0 #0 NONE WEAPON2 // Quarterstaff
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%FLAMWIZ.CRE~ ~override~ // Flaming Fist Battle Wizard (Flaming Fist)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%BLUN02~ #0 #0 #0 NONE WEAPON1 EQUIP // Flail
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%GARAN.CRE~ ~override~ // Garan (Mage/Thief, AR1009, Ice Island)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%DAGG02~ #0 #0 #0 NONE WEAPON1 EQUIP // Dagger +1
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%GIBBER2.CRE~ ~override~ // Diseased Gibberling (Gibberling)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%S1-2~ #0 #0 #0 NONE WEAPON1 EQUIP // Attack: one-handed, +1d2 slashing
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%IMOEN.CRE~ ~override~ // Imoen (Thief, AR2600, Candlekeep)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%BOW05~ #0 #0 #0 NONE WEAPON1 EQUIP // Short Bow
    END
    READ_ASCII 0x34 ~ports~
    READ_ASCII 0x3c ~portm~
    TEXT_SPRINT medium ~M~
    PATCH_IF GAME_IS ~bg1 totsc~ BEGIN
      TEXT_SPRINT medium ~L~
    END
    PATCH_IF (("%ports%" STRING_EQUAL_CASE ~~) OR ("%ports%" STRING_EQUAL_CASE ~None~)) BEGIN
      WRITE_ASCIIE 0x34 ~%tutu_scripti%MOENS~ #8     // Corrects Imoen's small portrait
    END
    PATCH_IF (("%portm%" STRING_EQUAL_CASE ~~) OR ("%portm%" STRING_EQUAL_CASE ~None~)) BEGIN
      WRITE_ASCIIE 0x3c ~%tutu_scripti%MOEN%medium%~ #8     // Corrects Imoen's medium portrait
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%JHASSO.CRE~ ~override~ // Jhasso (Fighter, AR0603, Seven Suns Basement)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_ASCII 0x268 script
    PATCH_IF !("%script%" STRING_COMPARE_CASE "%tutu_scriptw%TASIGHT") BEGIN
      WRITE_ASCIIE 0x268 ~%tutu_var%WTARSGT~ #8 // Needs EquipMostDamagingMelee()
    END
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%SW1H01~ #0 #0 #0 NONE WEAPON2 // Quarterstaff
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%JOIA.CRE~ ~override~ // Joia (Fighter/Mage, AR2306, Friendly Arm Inn)
              ~%tutu_var%TRALIT.CRE~ ~override~  // Tralithan (Fighter, AR0612, Iron Throne Level 2)
              ~%tutu_var%DELAIN2.CRE~ ~override~ // Delainy (Fighter/Werewolf, AR2000, Balduran's Isle)
              ~%tutu_var%DURLYL2.CRE~ ~override~ // Durlyle (Fighter/Werewolf, AR2000, Balduran's Isle)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%SW1H04~ #0 #0 #0 NONE WEAPON1 EQUIP // Long Sword
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%LARRIA.CRE~ ~override~ // Larriaz (Fairy: Sirine, AR1209, Baldur's Gate)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%SIRINE1~ #0 #0 #0 NONE WEAPON1 EQUIP // Attack: one-handed, +1d2 crushing, confusion (save vs. death)
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%MARL.CRE~ ~override~ // Marl (Fighter, AR3351, Feldepost's Inn)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%B1-2~ #0 #0 #0 NONE WEAPON1 EQUIP // Crushing
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%SHOAL.CRE~ ~override~ // Shoal the Nereid (Fairy: Nereid, AR3100)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%NEIRED~ #0 #0 #0 NONE WEAPON1 EQUIP // Attack: one-handed, +1d2 - 1 crushing, save vs. death (-2) or die
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%TRACEA.CRE~ ~override~ // Tracea Carol (Mage, AR1002, Ritual Chamber)
              ~%tutu_scriptbg%KING.CRE~ ~override~ // King (Mage, AR0506, Durlag's Tower)
              ~%tutu_scriptbg%QUEEN.CRE~ ~override~ // Queen (Mage, AR0506, Durlag's Tower)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_LONG 0x2b8 slotOff
    READ_SHORT slotOff + 0x12 weap1
    READ_SHORT slotOff + 0x14 weap2
    READ_SHORT slotOff + 0x16 weap3
    READ_SHORT slotOff + 0x18 weap4
    // make sure the creature has no weapons
    PATCH_IF ((weap1 == 0xffff) && (weap2 == 0xffff) && (weap3 == 0xffff) && (weap4 == 0xffff)) BEGIN
      ADD_CRE_ITEM ~%tutu_var%STAF02~ #0 #0 #0 NONE WEAPON1 EQUIP TWOHANDED // Quarterstaff +1
    END
  END
BUT_ONLY

/////////////////////////////
// Gender and Sex Corrections
COPY_EXISTING ~%tutu_var%ANDRIS.CRE~ ~override~ // Andris (MAGE_MALE_HUMAN animation, AR1009: Ice Island)
              ~%tutu_var%BARESH2.CRE~ ~override~ // Baresh (CLERIC_MALE_HUMAN animation, AR1004: Ulgoth's Beard)
              ~%tutu_var%BARWLF.CRE~ ~override~ // Baresh
              ~%tutu_var%BEARPO3.CRE~ ~override~ // Polar Bear (All bears are male, AR1008: Ice Island)
              ~%tutu_var%BEYN.CRE~ ~override~ // Beyn (FIGHTER_MALE_HUMAN animation, AR1009: Ice Island)
              ~%tutu_var%CUCHOL.CRE~ ~override~ // Cuchol (CLERIC_MALE_ELF animation, AR1009: Ice Island)
              ~%tutu_var%CULT2.CRE~ ~override~ // Cult Wizard (MAGE_MALE_HUMAN animation, AR1003: Ulgoth's Beard)
              ~%tutu_var%DELSVIR.CRE~ ~override~ // Deslvirftanyon (THIEF_MALE_DWARF_LOW animation, AR1000: Ulgoth's Beard)
              ~%tutu_var%DOOMDUR.CRE~ ~override~ // Dwarven Doom Guard (FIGHTER_MALE_DWARF animation, AR0512: Durlag's Tower)
              ~%tutu_var%DRADEE.CRE~ ~override~ // Dradeel (MAGE_MALE_ELF animation, AR1505: Werewolf Island)
              ~%tutu_var%DRADEE2.CRE~ ~override~ // Dradeel (MAGE_MALE_ELF animation, AR2000: Werewolf Island)
              ~%tutu_var%GARAN.CRE~ ~override~ // Garan (THIEF_MALE_HUMAN_LOW animation, AR1009: Ice Island)
              ~%tutu_var%IKE.CRE~ ~override~ // Ike (THIEF_MALE_HUMAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_var%JONDAL.CRE~ ~override~ // Jondalar (FIGHTER_MALE_HUMAN animation, AR2600: Candlekeep)
              ~%tutu_scriptbg%KING.CRE~ ~override~ // King (CLERIC_MALE_HUMAN animation, AR0506: Durlag's Tower)
              ~%tutu_var%LEAGGU1.CRE~ ~override~ // Merchant League Guard (FIGHTER_MALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%LEAGGU4.CRE~ ~override~ // Merchant League Guard (FIGHTER_MALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%LEAGGU5.CRE~ ~override~ // Merchant League Guard (FIGHTER_MALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%LEAGGU6.CRE~ ~override~ // Merchant League Guard (FIGHTER_MALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%LEAGGU7.CRE~ ~override~ // Merchant League Guard (FIGHTER_MALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%MARCEL.CRE~ ~override~ // Marcellus (MAGE_MALE_HUMAN animation, AR1009: Ice Island)
              ~%tutu_scriptm%TOWUB_A.CRE~ ~override~ // Commoner (PEASANT_MAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_scriptm%TOWUB_B.CRE~ ~override~ // Commoner (PEASANT_MAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_scriptm%TOWUB_C.CRE~ ~override~ // Commoner (PEASANT_MAN animation, AR1000: Ulgoth's Beard, AR1001: Ulgoth's Beard)
              ~%tutu_scriptm%TOWUB_D.CRE~ ~override~ // Commoner (PEASANT_MAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_scriptm%TOWUB_E.CRE~ ~override~ // Commoner (PEASANT_MAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_scriptm%TOWUBSC.CRE~ ~override~ // Commoner (STATIC_PEASANT_MAN_CHAIR animation, AR1001: Ulgoth's Beard)
              ~%MTOWUBSN%.CRE~ ~override~ // Commoner (STATIC_PEASANT_MAN_MATTE animation)
              ~%MTOWUBST%.CRE~ ~override~ // Commoner (STATIC_PEASANT_MAN_STOOL animation)
              ~%tutu_var%MTOWUBX.CRE~ ~override~ // Commoner (STATIC_PEASANT_MAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_scriptbg%TARNOR.CRE~ ~override~ // Tarnor (FIGHTER_MALE_DWARF animation)
              ~%tutu_var%ULF.CRE~ ~override~ // Ulf (FIGHTER_MALE_ELF animation, AR0307.BCS: Merchant League)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_BYTE 0x237 1                  // Sex: Male
    WRITE_BYTE 0x275 1                  // Gender: Male
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CLAIRD.CRE~ ~override~ // Clair De'lain (FIGHTER_FEMALE_HUMAN animation, AR0514: Durlag's Tower)
              ~%tutu_var%CULTT3.CRE~ ~override~ // Cult Assassin (FIGHTER_FEMALE_HUMAN animation, AR1003: Ulgoth's Beard)
              ~%tutu_var%CULTT4.CRE~ ~override~ // Cult Archer (THIEF_FEMALE_HUMAN animation, AR1003: Ulgoth's Beard)
              ~%tutu_var%DAESE.CRE~ ~override~ // Daese (MAGE_FEMALE_HUMAN animation, AR1504: Werewolf Island)
              ~%tutu_var%DELAIN2.CRE~ ~override~ // Delainy (CLERIC_FEMALE_ELF animation, AR2600: Werewolf Island)
              ~%FTOWUB_A%.CRE~ ~override~ // Commoner (PEASANT_WOMAN animation, AR1000: Ulgoth's Beard)
              ~%FTOWUB_B%.CRE~ ~override~ // Commoner (PEASANT_WOMAN animation, AR1000: Ulgoth's Beard)
              ~%FTOWUB_E%.CRE~ ~override~ // Commoner (PEASANT_WOMAN animation, AR1001: Ulgoth's Beard, AR1006: Ulgoth's Beard)
              ~%FTOWUBSC%.CRE~ ~override~ // Commoner (STATIC_PEASANT_WOMAN_CHAIR animation)
              ~%FTOWUBSN%.CRE~ ~override~ // Commoner (STATIC_PEASANT_WOMAN_MATTE animation)
              ~%tutu_var%FTOWUBX.CRE~ ~override~ // Commoner (STATIC_PEASANT_WOMAN animation, AR1000: Ulgoth's Beard)
              ~%tutu_var%ISLSIR.CRE~ ~override~ // Sirine Queen (SIRINE animation, AR1500: Werewolf Island)
              ~%tutu_var%JAHEIR4.CRE~ ~override~ // Jaheira
              ~%tutu_var%JAHEIR6.CRE~ ~override~ // Jaheira
              ~%tutu_var%LEAGGU2.CRE~ ~override~ // Merchant League Guard (FIGHTER_FEMALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%LEAGGU3.CRE~ ~override~ // Merchant League Guard (FIGHTER_FEMALE_HUMAN animation, AR0308.BCS: Merchant League)
              ~%tutu_var%LOVEM.CRE~ ~override~ // Love (FIGHTER_FEMALE_DWARF animation)
              ~%tutu_var%MARALE2.CRE~ ~override~ // Maralee (CLERIC_FEMALE_HUMAN animation, AR2000: Werewolf Island)
              ~%tutu_scriptp%HEOARCH.CRE~ ~override~ // Phoenix Guard (MAGE_FEMALE_HUMAN animation, AR0508: Ulgoth's Beard)
              ~%tutu_var%TRACEA.CRE~ ~override~ // Tracea Carol (MAGE_FEMALE_HUMAN_LOW animation, AR1002: Ulgoth's Beard)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_BYTE 0x237 2                  // Sex: Female
    WRITE_BYTE 0x275 2                  // Gender: Female
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%APPAR.CRE~ ~override~ // Skeleton (SKELETON animation, AR0513: Durlag's Tower)
              ~%tutu_scriptc%RYPTCRA.CRE~ ~override~ // Crypt Crawler (CARRION_CRAWLER animation, AR0514: Durlag's Tower)
              ~%tutu_var%GHASTD.CRE~ ~override~ // Ghast (GHAST animation, TRPGHAST.BCS)
              ~%tutu_var%GRAEL.CRE~ ~override~ // Grael (GHAST animation, AR0514: Durlag's Tower)
              ~%tutu_var%HACK.CRE~ ~override~ // Hack (OGRE animation, TITACH.BCS)
              ~%tutu_var%PLYOGRE.CRE~ ~override~ // Ogre (OGRE animation)
              ~%tutu_var%SKELET.CRE~ ~override~ // Skeleton (SKELETON animation)
              ~%tutu_scripts%PIDPHAS.CRE~ ~override~ // Astral Phase Spider (SPIDER_PHASE animation, AR0514: Durlag's Tower)
              ~%tutu_var%STALKE.CRE~ ~override~ // Invisible Stalker (DOOM_GUARD animation, AR0146: Baldur's Gate, AR0161: Baldur's Gate, AR0509: Durlag's Tower)
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_BYTE 0x237 1                  // Sex: Male
    WRITE_BYTE 0x275 4                  // Gender: Neither
  END
BUT_ONLY

//Compatibility with NPC Portraits component
ACTION_IF NOT FILE_EXISTS ~override/X#NJNPCPortrait.G3~ THEN BEGIN
  COPY_EXISTING ~%tutu_var%ARKUSH.CRE~ ~override~ // Arkushule
                ~%tutu_var%NIEMAI.CRE~ ~override~ // Niemain
                ~%tutu_var%WINSKI.CRE~ ~override~ // Winski Perorate
    PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
      WRITE_ASCII 0x34 ~~ #8 // Removes Edwin's, Kivan's, Garrick's, Montaron's, Xzar's, Alora's, and Ajantis' portraits
      WRITE_ASCII 0x3c ~~ #8
    END
  BUT_ONLY
END

COPY_EXISTING ~%tutu_var%BAYARD.CRE~ ~override~ // Bayard
              ~%tutu_var%BERRUN.CRE~ ~override~ // Berrun Ghastkill
              ~%tutu_var%BORDA.CRE~ ~override~ // Borda
              ~%tutu_var%BRENDA.CRE~ ~override~ // Brendan
              ~%tutu_var%COWLED.CRE~ ~override~ // Cowled Wizard
              ~%tutu_var%DEGROD.CRE~ ~override~ // Degrodel
              ~%tutu_var%DENAK.CRE~ ~override~ // Denak
              ~%tutu_var%DIANA.CRE~ ~override~ // Diana
              ~%tutu_var%DRELIK.CRE~ ~override~ // Drelik
              ~%tutu_var%FAIZAH.CRE~ ~override~ // Statue
              ~%tutu_var%FORTHE.CRE~ ~override~ // Forthel August
              ~%tutu_var%GAZIB.CRE~ ~override~ // The Great Gazib
              ~%tutu_var%KAHRK.CRE~ ~override~ // Kahrk
              ~%tutu_scriptbg%KING.CRE~ ~override~ // King
              ~%tutu_var%LASALA.CRE~ ~override~ // Lasala
              ~%tutu_var%MARCEL.CRE~ ~override~ // Marcellus
              ~%tutu_var%MERLIN.CRE~ ~override~ // Elminster
              ~%tutu_var%MUTAMI.CRE~ ~override~  // Mutamin
              ~%tutu_var%NESTOR.CRE~ ~override~ // Nestor
              ~%tutu_var%OBE.CRE~ ~override~ // Obe
              ~%tutu_var%OULAM.CRE~ ~override~ // Oulam
              ~%tutu_scriptbg%QUEEN.CRE~ ~override~ // Queen
              ~%tutu_var%SEMAJ.CRE~ ~override~ // Semaj
              ~%tutu_var%SEWERF5.CRE~ ~override~ // Sewerfolk
              ~%tutu_var%SUNIN.CRE~ ~override~ // Sunin
//              ~%tutu_var%TESTER.CRE~ ~override~ // "Ajantis" - never used
              ~%tutu_var%VENKT.CRE~ ~override~ // Venkt
              ~%tutu_var%WHEBER.CRE~ ~override~ // Wheber Ott
              ~%WILLIAM%.CRE~ ~override~ // William Garst
              ~%tutu_var%WIVEN.CRE~ ~override~ // Wiven
              ~%tutu_var%ZORDRA.CRE~ ~override~ // Zordral
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x34 ~~ #8 // Removes Edwin's, Kivan's, Garrick's, Montaron's, Xzar's, Alora's, and Ajantis' portraits
    WRITE_ASCII 0x3c ~~ #8
  END
BUT_ONLY

//////////////////////////////////////////////////////////
// Fixes due to the results of CamDawg's Inventory Checker
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN  // Already included in BGEE
COPY_EXISTING ~%tutu_var%AASIM.CRE~ ~override~ // Aasim (AR0615, Iron Throne Building)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN // searches through items
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_scripts%HIELD04" = 0) BEGIN // find invalid resref
      WRITE_ASCIIE ("%itm_off%" + (0x14 * "%index%")) "%tutu_var%SHLD04" #8 // corrected resref
      SET "index" = "%itm_num%" // kills loop
    END
  END
 END
BUT_ONLY

COPY_EXISTING ~%tutu_var%RINNIE.CRE~ ~override~ // Rinnie: LEAT01 (Leather Armor)
              ~%tutu_var%ZHURLO.CRE~ ~override~ // Zhurlong: LEAT01 (Leather Armor)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_var%LEAT01" = 0)
    BEGIN
      FOR (index2 = 0 ; index2 < 36 ; index2 = index2 + 1) BEGIN // search through slots and add reference to first null slot
        READ_SHORT ("%slot_off%" + ("%index2%" + 0x02)) "ref"
        PATCH_IF ("%ref%" = 0x01) BEGIN // if incorrect reference in armor
          WRITE_SHORT ("%slot_off%" + ("%index2%" + 0x02)) "%index%" // adds reference to item
          SET "index2" = 36 // kills loop
        END
        PATCH_IF ("%index2%" = 0) BEGIN // if end of armor slots, skip ahead to inventory
          SET "index2" = 20 // otherwise go to inventory slots
        END
      END
    END
  END
 END
BUT_ONLY

COPY_EXISTING ~%tutu_var%ANDRIS.CRE~ ~override~ // Andris: CLCK12 (Knave's Robe)
              ~%tutu_var%MARCEL.CRE~ ~override~ // Marcellus: CLCK13 (Traveller's Robe)
              ~%tutu_scripts%HANDAL2.CRE~ ~override~ // Shandalar: CLCK16 (Cloak of the Neutral Archmagi)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_var%clck12" = 0) OR
             ("%item%" STRING_COMPARE_CASE "%tutu_var%clck13" = 0) OR
             ("%item%" STRING_COMPARE_CASE "%tutu_var%clck16" = 0)
    BEGIN
      READ_SHORT ("%slot_off%" + 0x02) "curr_idx"
      //move only if armor slot not already taken
      PATCH_IF ("%curr_idx%" = 0xffff) BEGIN
        WRITE_SHORT ("%slot_off%" + 0x02) "%index%" // puts in armor slot
        WRITE_SHORT ("%slot_off%" + 0x22) 0xffff    // removes entry from cloak slot
      END
    END
  END
 END
BUT_ONLY

COPY_EXISTING ~%tutu_var%DEZKIE.CRE~ ~override~ // Dezkiel: CLCK22 (Shandalar's Cloak)
              ~%tutu_var%BHEREN.CRE~ ~override~ // Bheren: CLCK01 (Cloak of Protection +1)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_var%clck22" = 0) OR
             ("%item%" STRING_COMPARE_CASE "%tutu_var%clck01" = 0)
    BEGIN
      READ_SHORT ("%slot_off%" + 0x22) "curr_idx"
      //move only if cloak slot not already taken
      PATCH_IF ("%curr_idx%" = 0xffff) BEGIN
        WRITE_SHORT ("%slot_off%" + 0x22) "%index%" // puts in cloak slot
        WRITE_SHORT ("%slot_off%" + 0x24) 0xffff    // removes entry from quick item 1 slot
      END
    END
  END
 END
BUT_ONLY

COPY_EXISTING ~%tutu_var%DRATAN.CRE~ ~override~ // Dra'tan: AROW01 (Arrows)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_var%arow01" = 0) BEGIN
      FOR (index2 = 13 ; index2 < 36 ; index2 = index2 + 1) BEGIN // search through quiver slots and add reference to first null slot
        READ_SHORT ("%slot_off%" + ("%index2%" * 0x02)) "ref"
        PATCH_IF ("%ref%" = 0xffff) BEGIN // first null reference in quivers
          WRITE_SHORT ("%slot_off%" + ("%index2%" * 0x02)) "%index%" // adds reference to item
          SET "index2" = 36 // kills loop
        END
        PATCH_IF ("%index2%" = 16) BEGIN // if end of quiver slots, skip ahead to inventory
          SET "index2" = 20
        END
      END
    END
  END
 END
BUT_ONLY

COPY_EXISTING ~%tutu_var%NADARI.CRE~ ~override~ // Nadarin: BOOT02 (Boots of Stealth)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_var%boot02" = 0)
    BEGIN
      READ_SHORT ("%slot_off%" + 0x10) "curr_idx"
      //move only if boots slot not already taken
      PATCH_IF ("%curr_idx%" = 0xffff) BEGIN
        WRITE_SHORT ("%slot_off%" + 0x10) "%index%" // puts in boots slot
        WRITE_SHORT ("%slot_off%" + 0x24) 0xffff    // removes entry from quick item 1 slot
      END
    END
  END
 END
BUT_ONLY

//devSin 'nixed' this (that ring is there because it's the key to his door, not because he actually needs it or was supposed to be wearing it)
/*
COPY_EXISTING ~%tutu_var%ALDETH.CRE~ ~override~ // Aldeth Sashenstar: MAGE04 (blur ring)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "%tutu_var%mage04" = 0) BEGIN
      FOR (index2 = 4 ; index2 < 36 ; index2 = index2 + 1) BEGIN // search through ring slots and add reference to first null slot
        READ_SHORT ("%slot_off%" + ("%index2%" * 0x02)) "ref"
        PATCH_IF ("%ref%" = 0xffff) BEGIN // first null reference in rings
          WRITE_SHORT ("%slot_off%" + ("%index2%" * 0x02)) "%index%" // adds reference to item
          WRITE_SHORT ("%slot_off%" + 0x2a) 0xffff    // removes entry from inventory 1 slot
          SET "index2" = 36 // kills loop
        END
      END
    END
  END
 END
BUT_ONLY
*/
END // Fixes due to the results of CamDawg's Inventory Checker

/////////////////////////
// Bandits without scalps
COPY_EXISTING ~%tutu_var%CREDUS.CRE~ ~override~ // Credus, AR1900
              ~%tutu_var%RAIKEN.CRE~ ~override~ // Raiken, AR2400.BCS
              ~%tutu_var%TAUGOS.CRE~ ~override~ // Taugos, AR1900
              ~%tutu_var%TEVEN.CRE~ ~override~ // Teven, AR2900.BCS
              ~%tutu_var%VAX.CRE~ ~override~ // Vax, AR5300
              ~%tutu_var%VENKT.CRE~ ~override~ // Venkt, AR1901
              ~%tutu_var%ZAL.CRE~ ~override~ // Zal, AR5300
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    ADD_CRE_ITEM ~%tutu_var%MISC86~ #1 #0 #0 ~IDENTIFIED~ ~INV1 INV2~ // Adds a bandit scalp
  END
BUT_ONLY

/////////////////////////////////////////
//NOBL9.CRE needs 2 gold for his dialogue
COPY_EXISTING ~%tutu_var%NOBL9.CRE~ ~override~ //Nobleman, AR2303
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
   WRITE_LONG 0x1c 2 //Gold
 END
BUT_ONLY

/////////////////////////////////
//Correct cultist death variables
ACTION_IF GAME_IS ~tutu tutu_totsc bg1 totsc~ THEN BEGIN //BG and Tutu only

COPY_EXISTING ~%tutu_var%CULT1.CRE~ ~override~ //Cult Guard
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultGuard1" #32 //was a duplicate "CultGuard", interferes with CULTSHT.BCS
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULT2.CRE~ ~override~ //Cult Wizard
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultWizard1" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULT3.CRE~ ~override~ //Cult Enforcer
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultWizard2" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULT4.CRE~ ~override~ //Cult Archer, unused
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "cultfodder" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULTASS.CRE~ ~override~ //Cult Assassin, unused
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultAssassin" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULTT1.CRE~ ~override~ //Cult Enforcer
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultWizard" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULTT2.CRE~ ~override~ //Cult Guard
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultGuard" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULTT3.CRE~ ~override~ //Cult Assassin
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultAssassin" #32
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%CULTT4.CRE~ ~override~ //Cult Archer
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_ASCII 0x280 "CultArcher" #32
  END
BUT_ONLY

END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Creature Restorations                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @24

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBCreRestore.UB~

COPY_EXISTING ~%Candlekeep_Library_L1%.ARE~ ~override~ // Add READ4 to Candlekeep library
              ~%Candlekeep_Library_L3%.ARE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    PATCH_IF !("%SOURCE_RES%" STRING_COMPARE_CASE "%Candlekeep_Library_L1%") BEGIN
      actLoc = 1108
    END ELSE actLoc = 1272
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      READ_SHORT actOff + 0x20 xPos
      PATCH_IF ((xPos == actLoc) && !("%actor%" STRING_COMPARE_CASE "%tutu_var%READ3")) BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%READ4~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%Candlekeep_Library_L2%.ARE~ ~override~ // Add READ4 to Candlekeep library
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      READ_SHORT actOff + 0x20 xPos
      PATCH_IF (((xPos == 547) || (xPos == 1293)) &&
                !("%actor%" STRING_COMPARE_CASE "READ3")) BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%READ4~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%Candlekeep_Library_L4%.ARE~ ~override~ // Add READ4 to Candlekeep library
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      READ_SHORT actOff + 0x20 xPos
      PATCH_IF ((xPos == 1080) && !("%actor%" STRING_COMPARE_CASE "%tutu_var%READ3")) BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%READ4~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%HAFFG2.CRE~ ~override~ // Halfling Woman (Mabledale), AR4006
  WRITE_ASCIIE 0x280 "HAFFG2" #18 // Script name
BUT_ONLY

COPY_EXISTING ~%tutu_var%HALFG2.CRE~ ~override~ // Halfling Man (Bunsen), AR4013
  WRITE_ASCIIE 0x280 "HALFG2" #18 // Script name
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTOB4.CRE~ ~override~  // Commoner, AR0104
  WRITE_ASCIIE 0x280 "MTOB4" #18 // Script name
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTOB5.CRE~ ~override~  // Commoner, AR0705
  WRITE_ASCIIE 0x280 "MTOB5" #18 // Script name
BUT_ONLY

COPY_EXISTING ~%tutu_var%TRAVEL.CRE~ ~override~ // Traveler (Wade), AR2301
  WRITE_ASCIIE 0x280 "WADE" #18 // Script name
BUT_ONLY

LAF replacetext_area_script STR_VAR area="%EBaldursGate%" oldtext=~Exists(\"BERSCH\")~ newtext=~Exists("HERSCHEL")~ END
LAF replacetext_area_script STR_VAR area="%EBaldursGate%" oldtext=~CreateCreature(\"BERSCH\",~ newtext=~CreateCreature("HERSCH",~ END

//////////////////////////////////////////////////
// Add patron to Splurging Sturgeon inn (upstairs)
COPY_EXISTING ~%NEBaldursGate_SplurgingSturgeon_L2%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 518
    	fj_loc_y          = 252
    	fj_dest_x         = 518
    	fj_dest_y         = 252
    	fj_animation      = 0xC900 //peasant man
    	fj_orientation    = 2      //SW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Commoner
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%MTOB4"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ex_ar0104.baf
IF
Global("UB_MTOB4_SPAWN","GLOBAL",0)
!Exists("MTOB4")
!Dead("MTOB4")
THEN
RESPONSE #100
CreateCreature("%tutu_var%MTOB4",[518.252]%FACE_2%) // Commoner
ActionOverride("MTOB4",Face(2))
SetGlobal("UB_MTOB4_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%NEBaldursGate_SplurgingSturgeon_L2%" bottom=".../bg1ub/ex_ar0104.baf" END
*/

////////////////////////////////////////////
// Add patron to Elfsong Tavern (main level)
COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L1%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 851
    	fj_loc_y          = 367
    	fj_dest_x         = 851
    	fj_dest_y         = 367
    	fj_animation      = 0xC900 //peasant man
    	fj_orientation    = 0      //S
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Commoner
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%MTOB5"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/eb_0705.baf
IF
Global("UB_MTOB5_SPAWN","GLOBAL",0)
!Exists("MTOB5")
!Dead("MTOB5")
THEN
RESPONSE #100
CreateCreature("%tutu_var%MTOB5",[851.367]%FACE_0%) // Commoner
SetGlobal("UB_MTOB5_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%EBaldursGate_ElfsongTavern_L1%" bottom=".../bg1ub/eb_0705.baf" END
*/

/////////////////////////////////////////////////////////////
// Create FAI traveler when bandit subplot starts (Chapter 3)
<<<<<<<< .../bg1ub/eb_2301.baf
IF
Global("CHAPTER","GLOBAL",%tutu_chapter_3%)
!Exists("WADE")
!Dead("WADE")
THEN
RESPONSE #100
CreateCreature("%tutu_var%TRAVEL",[691.398]%FACE_0%) // Traveler
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%FriendlyArmInn_L1%" bottom=".../bg1ub/eb_2301.baf" END

/////////////////////////////////////////////////
// Add Mabledale to home in Gullykin (downstairs)
COPY_EXISTING ~%Gullykin_House1_Cellar%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 357
    	fj_loc_y          = 257
    	fj_dest_x         = 357
    	fj_dest_y         = 257
    	fj_animation      = 0x6313 //Thief Halfling Woman
    	fj_orientation    = 9      //ENE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Mabledale
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%HAFFG2"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/eb_ar4006.baf
IF
Global("UB_HAFFG2_SPAWN","GLOBAL",0)
!Exists("HAFFG2")
!Dead("HAFFG2")
THEN
RESPONSE #100
CreateCreature("%tutu_var%HAFFG2",[357.257]%FACE_9%) // Halfling Woman
ActionOverride("HAFFG2",Face(9))
SetGlobal("UB_HAFFG2_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Gullykin_House1_Cellar%" bottom=".../bg1ub/eb_ar4006.baf" END
*/

//////////////////////////////////////////////
// Add Bunsen to home in Gullykin (main level)
COPY_EXISTING ~%Gullykin_House2_L1%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 311
    	fj_loc_y          = 179
    	fj_dest_x         = 311
    	fj_dest_y         = 179
    	fj_animation      = 0x6303 //Thief Halfling Man
    	fj_orientation    = 0      //S
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Bunsen
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%HALFG2"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/eb_ar4013.baf
IF
Global("UB_HALFG2_SPAWN","GLOBAL",0)
!Exists("HALFG2")
!Dead("HALFG2")
THEN
RESPONSE #100
CreateCreature("%tutu_var%HALFG2",[311.179]%FACE_0%) // Halfling Man
SetGlobal("UB_HALFG2_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Gullykin_House2_L1%" bottom=".../bg1ub/eb_ar4013.baf" END
*/

// Fix his dialogue
<<<<<<<< .../bg1ub/bunsenfix.d
REPLACE ~%tutu_var%HALFG2~
IF ~NumTimesTalkedTo(0)~ THEN BEGIN 0
  SAY @30000 /* ~Hello hello hello.  It's Bunsens home you have entered, and welcome you are!  Please, rest your somewhat less-than-hairy feet by my hearth.  ~ */
  IF ~Race(LastTalkedToBy(Myself),HALFLING)~ THEN REPLY @30001 /* ~Are you always this courteous to strangers that walk into your home?  It hardly seems safe to me. ~ */ GOTO 1
  IF ~!Race(LastTalkedToBy(Myself),HALFLING)~ THEN REPLY @30001 /* ~Are you always this courteous to strangers that walk into your home?  It hardly seems safe to me. ~ */ GOTO 4
  IF ~Race(LastTalkedToBy(Myself),HALFLING)~ THEN REPLY @30002 /* ~Spare me your smiles, shorty.  I merely came in to see if you had anything worth taking.  You are foolish to leave your burrow unlocked.~ */ GOTO 2
  IF ~!Race(LastTalkedToBy(Myself),HALFLING)~ THEN REPLY @30002 /* ~Spare me your smiles, shorty.  I merely came in to see if you had anything worth taking.  You are foolish to leave your burrow unlocked.~ */ GOTO 5
  IF ~Race(LastTalkedToBy(Myself),HALFLING)~ THEN REPLY @30003 /* ~My apologies, but the door was unlocked and I thought your burrow deserted.  I did not mean to intrude upon your home.~ */ GOTO 3
  IF ~!Race(LastTalkedToBy(Myself),HALFLING)~ THEN REPLY @30003 /* ~My apologies, but the door was unlocked and I thought your burrow deserted.  I did not mean to intrude upon your home.~ */ GOTO 6
END
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../bg1ub/bunsenfix.d~

/////////////////////////////////////////////
// Restore the Friendly Arm Inn serving wench
COPY_EXISTING ~%NEBaldursGate_BlushingMermaid_L1%.ARE~ ~override~ // Blushing Mermaid
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "%tutu_var%FRIWEN") BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%SERWEN~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L1%.ARE~ ~override~ // Elfsong
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "%tutu_var%FRIWEN") BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%SERWEN~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%FRIWEN.CRE~ ~override~ // Serving Wench
  WRITE_ASCIIE 0x2cc ~%tutu_var%FRIWEN~ // Assigns the unused dialog, FRIWEN.DLG
  WRITE_ASCII 0x280 ~FRIWEN~ // Assigns a script name
BUT_ONLY

// Add the Serving Wench to the Friendly Arm Inn
COPY_EXISTING ~%FriendlyArmInn_L1%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 1073
    	fj_loc_y          = 690
    	fj_dest_x         = 1073
    	fj_dest_y         = 690
    	fj_animation      = 0xC910 //Peasant Woman
    	fj_orientation    = 4      //W
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Serving Wench"
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%FRIWEN"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ub_ar2301.baf
IF
Global("UB_FRIWEN_SPAWN","%FriendlyArmInn_L1%",0)
!Exists("FRIWEN")
!Dead("FRIWEN")
THEN
RESPONSE #100
CreateCreature("%tutu_var%FRIWEN",[1073.690]%FACE_4%) //
SetGlobal("UB_FRIWEN_SPAWN","%FriendlyArmInn_L1%",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%FriendlyArmInn_L1%" bottom=".../bg1ub/ub_ar2301.baf" END
*/

//////////////////////////////////////////////
// Add the Lecturing Farmer to Wyrm's Crossing
COPY_EXISTING ~%tutu_var%FARM4.CRE~ ~override/UBFARM5.CRE~ // Copies FARM4
  SAY NAME1 @25
  SAY NAME2 @25
  WRITE_ASCII 0x280 ~UBFARM5~ #8                 // Sets Death Variable
  WRITE_ASCII 0x2cc ~UBFARM5~ #8                 // Sets Dialogue

COPY_EXISTING ~%WyrmsCrossing%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4716
    	fj_loc_y          = 3663
    	fj_dest_x         = 4716
    	fj_dest_y         = 3663
    	fj_animation      = 0xC900 //Peasant Man
    	fj_orientation    = 2      //SW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Farmer
   		fj_cre_resref     = UBFARM5
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ub_ar0900.baf
IF
Global("UB_FARMER5_SPAWN","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("UBFARM5",[4716.3663]%FACE_2%)
SetGlobal("UB_FARMER5_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%WyrmsCrossing%" bottom=".../bg1ub/ub_ar0900.baf" END
*/

/////////////////////////
// Feldane, an Old Friend
COPY_EXISTING ~%tutu_var%FELDAN.CRE~ ~override~
  WRITE_ASCII 0x280 ~Feldane~ #8    // Sets Death Variable
  WRITE_ASCII 0x2cc ~UBFELDAN~ #8   // Sets Dialogue
BUT_ONLY

COPY_EXISTING ~%Candlekeep_Ch6%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 2965
    	fj_loc_y          = 1824
    	fj_dest_x         = 2965
    	fj_dest_y         = 1824
    	fj_animation      = 0x6402 //Monk
    	fj_orientation    = 3      //WSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Feldane
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%FELDAN"
	END
BUT_ONLY

/* <<<<<<<< .../bg1ub/ub_ar2626.baf
IF
Global("UB_FELDANE_SPAWN","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("%tutu_var%FELDAN",[2965.1824]%FACE_3%)
SetGlobal("UB_FELDANE_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Candlekeep_Ch6%" bottom=".../bg1ub/ub_ar2626.baf" END
*/

///////////////
// Nashkel Girl
COPY_EXISTING ~%Nashkel_JosephsHouse%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 605
    	fj_loc_y          = 317
    	fj_dest_x         = 605
    	fj_dest_y         = 317
    	fj_animation      = 0xC710 //Girl
    	fj_orientation    = 1      //SSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Girl
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%GIRLBE"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ub_ar4806.baf
IF
Global("UB_GIRLBE_SPAWN","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("%tutu_var%GIRLBE",[605.317]%FACE_1%)
SetGlobal("UB_GIRLBE_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Nashkel_JosephsHouse%" bottom=".../bg1ub/ub_ar4806.baf" END
*/

/////////////////////////
// Baldur's Gate Merchant

COPY_EXISTING ~%tutu_var%FATMAN.CRE~ ~override/UBSHOPGE.CRE~
  SAY NAME1 #9432
  SAY NAME2 #9434
  WRITE_ASCIIE 0x2cc ~%tutu_var%SHOPGE~ #8

COPY_EXISTING ~%SWBaldursGate%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 3109
    	fj_loc_y          = 2627
    	fj_dest_x         = 3109
    	fj_dest_y         = 2627
    	fj_animation      = 0xC800 //Fat man
    	fj_orientation    = 0      //S
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Merchant
   		fj_cre_resref     = UBSHOPGE
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ub_ar1100.baf
IF
Global("UB_SHOPGE_SPAWN","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("UBSHOPGE",[3109.2627]%FACE_0%)
SetGlobal("UB_SHOPGE_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%SWBaldursGate%" bottom=".../bg1ub/ub_ar1100.baf" END
*/

/////////////////////////////////
// Wilton, Farmer Brun's Neighbor

COPY_EXISTING ~%tutu_var%WILTON.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%CLCK01~ #1 #0 #0 ~NONE~ ~INV1 INV2~ // Adds Cloak of Protection +1
  WRITE_ASCII 0x2cc ~UBWILTON~                      // Assigns edited dialog file
  WRITE_ASCII 0x280 ~Wilton~                        // Assigns death variable
  WRITE_BYTE 0x23f 0x8                              // Fix his morale setting
  WRITE_BYTE 0x240 0x6                              // Fix his morale break setting
BUT_ONLY

COPY_EXISTING ~%FishingVillage_House2%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 471
    	fj_loc_y          = 272
    	fj_dest_x         = 471
    	fj_dest_y         = 272
    	fj_animation      = 0xC900 //Peasant man
    	fj_orientation    = 6      //NW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Wilton
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%WILTON"
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ub_ar1403.baf
IF
Global("UB_WILTON_SPAWN","GLOBAL",0)
!Exists("Wilton")
!Dead("Wilton")
THEN
RESPONSE #100
CreateCreature("%tutu_var%WILTON",[471.272]%FACE_6%)
SetGlobal("UB_WILTON_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%FishingVillage_House2%" bottom=".../bg1ub/ub_ar1403.baf" END
*/

/////////////////////////////////
// Corianna, the Petrified Ranger

COPY ~bg1ub/creature/%tutuorbgt%/ubcorian.cre~ ~override~
  SAY NAME1 #15552
  SAY NAME2 #15574

COPY_EXISTING ~%Temple%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4611
    	fj_loc_y          = 3449
    	fj_dest_x         = 4611
    	fj_dest_y         = 3449
    	fj_animation      = 0x6110 //Fighter human woman
    	fj_orientation    = 2      //SW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Corianna
   		fj_cre_resref     = UBCORIAN
	END
BUT_ONLY

/*<<<<<<<< .../bg1ub/ub_ar3400.baf
IF
Global("UB_CORIANNA_SPAWN","GLOBAL",0)
!Exists("UBCORIAN")
!Dead("UBCORIAN")
Global("UB_HELPCORIANNA","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("UBCORIAN",[4611.3449]%FACE_2%)
SetGlobal("UB_CORIANNA_SPAWN","GLOBAL",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Temple%" bottom=".../bg1ub/ub_ar3400.baf" END
*/

/////////////////////////////////
// Compile all relevant dialogues

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/creature/cpm/ubcre.d~
END ELSE BEGIN
  COMPILE ~bg1ub/creature/ubcre.d~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Creature Name Restorations                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @26

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBCreName.UB~

COPY_EXISTING ~%tutu_var%FTOBE2.CRE~ ~override~ // AR3339
  SAY NAME1 @27
  SAY NAME2 @27
  SAY 0x198 #13638
  SAY 0x19c #13637
BUT_ONLY

COPY_EXISTING ~%tutu_var%FTOBE3.CRE~ ~override~ // AR3311
  SAY NAME1 @28
  SAY NAME2 @28
  SAY 0x198 #13640
  SAY 0x19c #13639
BUT_ONLY

COPY_EXISTING ~%tutu_var%FTOBE4.CRE~ ~override~ // AR3316
  SAY NAME1 @29
  SAY NAME2 @29
  SAY 0x198 #13641
BUT_ONLY

COPY_EXISTING ~%tutu_var%FTOBE9.CRE~ ~override~ // AR3323
  SAY NAME1 @30
  SAY NAME2 @30
  SAY 0x198 #13648
BUT_ONLY

COPY_EXISTING ~%tutu_var%HAFFG2.CRE~ ~override~ // Currently UNUSED
  SAY NAME1 @31
  SAY NAME2 @31
  SAY 0x198 #13407
  SAY 0x19c #13405
  SAY 0x1a0 #13406
BUT_ONLY

COPY_EXISTING ~%tutu_var%HALFG2.CRE~ ~override~ // Currently UNUSED
  SAY NAME1 @32
  SAY NAME2 @32
  SAY 0x198 #13410
  SAY 0x19c #13408
  SAY 0x1a0 #13409
BUT_ONLY

COPY_EXISTING ~%tutu_var%HALFG3.CRE~ ~override~ // AR4008
  SAY NAME1 @33
  SAY NAME2 @33
  SAY 0x198 #13413
  SAY 0x19c #13411
  SAY 0x1a0 #13412
BUT_ONLY

COPY_EXISTING ~%tutu_var%HALFG4.CRE~ ~override~ // AR4007
  SAY NAME1 @34
  SAY NAME2 @34
  SAY 0x198 #13416
  SAY 0x19c #13414
  SAY 0x1a0 #13415
BUT_ONLY

COPY_EXISTING ~%tutu_var%HALFG5.CRE~ ~override~ // AR4010
  SAY NAME1 @35
  SAY NAME2 @35
  SAY 0x198 #13419
  SAY 0x19c #13417
  SAY 0x1a0 #13418
BUT_ONLY

COPY_EXISTING ~%tutu_var%HOUSG1.CRE~ ~override~ // AR3320
  SAY NAME1 @36
  SAY NAME2 @36
  SAY 0x198 #13441
BUT_ONLY

COPY_EXISTING ~%tutu_var%HOUSG2.CRE~ ~override~ // AR3320
  SAY NAME1 @37
  SAY NAME2 @37
  SAY 0x198 #13442
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE2.CRE~ ~override~ // AR3341
  SAY NAME1 @38
  SAY NAME2 @38
  SAY 0x198 #13656
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE3.CRE~ ~override~ // AR3309
  SAY NAME1 @39
  SAY NAME2 @39
  SAY 0x198 #13657
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE4.CRE~ ~override~ // AR3309
  SAY NAME1 @40
  SAY NAME2 @40
  SAY 0x198 #13658
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE5.CRE~ ~override~ // AR3317
  SAY NAME1 @41
  SAY NAME2 @41
  SAY 0x198 #13661
  SAY 0x19c #13659
  SAY 0x1a0 #13660
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE6.CRE~ ~override~ // AR3319
  SAY NAME1 @42
  SAY NAME2 @42
  SAY 0x198 #13665
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTBE7.CRE~ ~override~ // AR3323
  SAY NAME1 @43
  SAY NAME2 @43
  SAY 0x198 #13655
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTOWF2.CRE~ ~override~ // AR2302
  SAY NAME1 @44
  SAY NAME2 @44
  SAY 0x198 #13502
  SAY 0x19c #13501
BUT_ONLY

COPY_EXISTING ~%tutu_var%NARRAT.CRE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    WRITE_LONG NAME1 BNOT 0x0
    WRITE_LONG NAME2 BNOT 0x0
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL4.CRE~ ~override~ // AR2616
  SAY NAME1 @45
  SAY NAME2 @45
  SAY 0x198 #13540
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL5.CRE~ ~override~ // AR2617
  SAY NAME1 @46
  SAY NAME2 @46
  SAY 0x198 #13543
  SAY 0x19c #13542
  SAY 0x1a0 #13541
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL6.CRE~ ~override~ // AR2617
  SAY NAME1 @47
  SAY NAME2 @47
  SAY 0x198 #13545
  SAY 0x19c #13544
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL7.CRE~ ~override~ // AR2302
  SAY NAME1 @48
  SAY NAME2 @48
  SAY 0x198 #13547
  SAY 0x19c #13546
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBW8.CRE~ ~override~ // AR0121
  SAY NAME1 @49
  SAY NAME2 @49
  SAY 0x198 #13558
BUT_ONLY

COPY_EXISTING ~%tutu_var%TRAVEL.CRE~ ~override~ // Currently UNUSED
  SAY NAME1 @50
  SAY NAME2 @50
  SAY 0x198 #13673
  SAY 0x19c #13671
  SAY 0x1a0 #13672
BUT_ONLY

COPY_EXISTING ~%tutu_var%FARM.CRE~ ~override~
              ~%tutu_var%FARM3.CRE~ ~override~
              ~%tutu_var%FARM4.CRE~ ~override~
              ~%tutu_var%FARMER.CRE~ ~override~
  SAY NAME1 @25
  SAY NAME2 @25
BUT_ONLY

COPY_EXISTING ~%tutu_var%GNOLL5.CRE~ ~override~ // see its dialogue for name
  SAY NAME1 @51
  SAY NAME2 @51
BUT_ONLY

COPY_EXISTING ~%tutu_var%HUNTER.CRE~ ~override~ // see his dialogue for name
  SAY NAME1 @52
  SAY NAME2 @52
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBW7.CRE~ ~override~ // see her dialogue for name
  SAY NAME1 @53
  SAY NAME2 @53
BUT_ONLY

// Corrections for TOTSC content
ACTION_IF GAME_IS ~totsc bgt tutu_totsc bgee~ THEN BEGIN

//EasyTutu modifies Baresh to change into LOUPGAR instead of KAISHWLF
ACTION_IF GAME_IS ~totsc bgt bgee~ THEN BEGIN //BGT, BGEE, BG1-TOTSC
  COPY_EXISTING ~%tutu_var%LOUPGAR.CRE~ ~override~ // only used from spell cast by Mendas
    PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
      SAY NAME1 @54 // or could be Mendas
      SAY NAME2 @54 // or could be Mendas
    END
  BUT_ONLY
END

COPY_EXISTING ~%tutu_var%BARWLF.CRE~ ~override~ // Baresh as loup garou
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    SAY NAME1 @55
    SAY NAME2 @55
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptd%AESEWLF.CRE~ ~override~ // Daese as werewolf
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    SAY NAME1 @56
    SAY NAME2 @56
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptk%AISHWLF.CRE~ ~override~ // Kaishas as loup garou
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    SAY NAME1 @57
    SAY NAME2 @57
  END
BUT_ONLY

ACTION_IF GAME_IS ~totsc tutu_totsc bgt~ THEN BEGIN  // Fixes already in BGEE
  ACTION_IF GAME_IS ~bgt~ THEN BEGIN //BGT
    COPY ~bg1ub/BGT/SPL/BGWI924.SPL~ ~override~
    COMPILE EVALUATE_BUFFER ~bg1ub/creature/BGT/beresh.d~
  END ELSE BEGIN //BG or Tutu
    ACTION_IF GAME_IS ~tutu_totsc~ THEN BEGIN //Tutu
      COMPILE EVALUATE_BUFFER ~bg1ub/creature/Tutu/beresh.d~
    END ELSE BEGIN //BG
	  COMPILE EVALUATE_BUFFER ~bg1ub/creature/beresh.d~
    END
  END
END

END // Corrections for TOTSC content


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Minor Dialogue Restorations                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @58

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBDialogue.UB~

ACTION_IF !GAME_IS ~bg1 totsc~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/dialogues/cpm/u!minor1.d~
  COMPILE EVALUATE_BUFFER ~bg1ub/dialogues/cpm/u!ch7.d~
  COMPILE EVALUATE_BUFFER ~bg1ub/dialogues/cpm/ubhobgo5.baf~
  COMPILE EVALUATE_BUFFER ~bg1ub/dialogues/cpm/ubunshey.d~
END ELSE BEGIN
  COMPILE ~bg1ub/dialogues/u!minor1.d~
  COMPILE ~bg1ub/dialogues/u!ch7.d~
  COMPILE ~bg1ub/dialogues/ubhobgo5.baf~
END

ACTION_IF GAME_IS ~tutu_totsc bgt bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/dialogues/cpm/u!totscminor.d~
END ELSE BEGIN
  ACTION_IF GAME_IS ~totsc~ THEN BEGIN
    COMPILE ~bg1ub/dialogues/u!totscminor.d~
  END
END

COPY_EXISTING ~%tutu_var%FTOBE5.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%RING01~ #0 #0 #0 NONE ~LRING RRING~

COPY_EXISTING ~%tutu_var%HALFG4.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%POTN40~ #1 #0 #0 NONE ~QITEM1 QITEM2 QITEM3~

COPY_EXISTING ~%tutu_var%HALFG6.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%SLNG02~ #0 #0 #0 NONE ~WEAPON1 WEAPON2~ EQUIP

COPY_EXISTING ~%tutu_var%MTOB2.CRE~ ~override~
  WRITE_LONG 0x1c 0x1
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTOB8.CRE~ ~override~
  WRITE_LONG 0x1c 0x2
BUT_ONLY

COPY_EXISTING ~%tutu_var%MTOB9.CRE~ ~override~
  WRITE_LONG 0x1c 0x1
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL10.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%AMUL07~ #0 #0 #0 NONE AMULET

COPY_EXISTING ~%tutu_var%NOBL11.CRE~ ~override~
  WRITE_LONG 0x1c 0xa
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL4.CRE~ ~override~
  WRITE_LONG 0x1c 0x14
BUT_ONLY

COPY_EXISTING ~%tutu_var%NOBL8.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%RING17~ #0 #0 #0 NONE LRING

COPY_EXISTING ~%tutu_var%RAMAZI.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%BRAC02~ #0 #0 #0 NONE ~INV1 INV5~

COPY_EXISTING ~%tutu_var%SMITH.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%CHAN01~ #0 #0 #0 NONE ~QITEM1 QITEM2 QITEM3~

// AR4200 Hobgoblin Fix, by Dudley~
// Author: Dudley, modified by Ascension64 (streamline init dialog script)
// Email:
// Website: http://www.dudleyville.com
COPY_EXISTING ~%tutu_var%HOBGO5.CRE~ ~override~ // Hobgoblin, AR4200
  WRITE_ASCII 0x248 ~UBHOBGO5~ #8 // Allows him to initiate dialog
  WRITE_BYTE 0x270 0x80 // Changes him from Enemy to Neutral
  WRITE_BYTE 0x272 0x6f // Sets his race as Hobgoblin
  WRITE_ASCIIE 0x2cc ~%tutu_var%HOBGO5~ #8 // Sets his dialog file
BUT_ONLY

// Corrections to Lahl's dialog on Isle of Balduran
ACTION_IF (GAME_IS ~totsc tutu_totsc bgt~) THEN BEGIN
	COMPILE EVALUATE_BUFFER ~/bg1ub/dialogues/ublahl.d~
END // Lahl correction

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Audio Restorations                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @59
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

// Author: devSin
// Email:
// Website:

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBAudio.UB~

COPY ~bg1ub/tra/%LANGUAGE%/sound_tmp.tra~ ~bg1ub/tra/%LANGUAGE%/sound.tra~
  EVALUATE_BUFFER
LOAD_TRA ~bg1ub/tra/%LANGUAGE%/sound.tra~

ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN //BGT or Tutu

/////////////////////////////////////////////////////////
// Ajantis sound fix (duplicate, probably unused strings)
COPY_EXISTING ~%tutu_var%AJANTI.CRE~ ~override~
              ~%tutu_var%AJANTI4.CRE~ ~override~
              ~%tutu_var%AJANTI6.CRE~ ~override~
 SAY 0x144 @60
 SAY 0x148 @61
 SAY 0x14c @62
 SAY 0x150 @63

/////////////////////
// Tanar'ri sound fix
ACTION_IF GAME_IS ~bgt tutu_totsc~ THEN BEGIN
 COPY_EXISTING ~%tutu_var%TANAR.CRE~ ~override~
  SAY 0xa4 @68
  SAY 0xa8 @64
  SAY 0xc8 @69
  SAY 0xcc @69
  SAY 0xd0 @69
  SAY 0xd4 @69
  SAY 0xd8 @69
  SAY 0xec @66
  SAY 0xf0 @65
  SAY 0x10c @67
  SAY 0x110 @68
  SAY 0x114 @64
  SAY 0x19c @67
  SAY 0x1b8 @67

///////////////////////////
// Delsvirftanyon sound fix
 COPY_EXISTING ~%tutu_var%DELSVIR.CRE~ ~override~
  SAY 0xa4 @70
  SAY 0xec @72
  SAY 0xf0 @73
  SAY 0x10c @71
END

///////////////////////////////
// Gandolar Luckyfoot sound fix
COPY_EXISTING ~%tutu_var%GANDOL.CRE~ ~override~
 SAY 0xa4 @74
 SAY 0xec @76
 SAY 0xf0 @77
 SAY 0x10c @75
 SAY 0x19c @75

////////////////////////////////
// Garrick sound fix (not funny)
COPY_EXISTING ~%tutu_var%GARRIC.CRE~ ~override~
              ~%tutu_var%GARRIC2.CRE~ ~override~
              ~%tutu_var%GARRIC4.CRE~ ~override~
              ~%tutu_var%GARRIC6.CRE~ ~override~
 SAY 0xfc @78

/////////////////////////////////////////////
// Xan sound fix (duplicate, probably unused)
COPY_EXISTING ~%tutu_scriptbg%XAN.CRE~ ~override~
              ~%tutu_var%XAN4.CRE~ ~override~
              ~%tutu_var%XAN6.CRE~ ~override~
 SAY 0x148 @79

///////////////////
// Misc sound fixes
/*STRING_SET 2913 @80 Gorion's dialogue, not needed*/

COPY_EXISTING ~%tutu_var%DARRYL2.CRE~ ~override~
              ~%tutu_var%ITASLOI.CRE~ ~override~
              ~%tutu_scriptt%ASLGURK.CRE~ ~override~
              ~%tutu_var%TASLOI.CRE~ ~override~
              ~%tutu_scriptt%ASLOI02.CRE~ ~override~
              ~%tutu_scriptt%ASLOI03.CRE~ ~override~
              ~%tutu_scriptt%ASLOISU.CRE~ ~override~
 SAY 0x10c @81

COPY_EXISTING ~%tutu_var%JELLMU.CRE~ ~override~
              ~%tutu_var%JELLMUL.CRE~ ~override~
              ~%tutu_var%JELLOC.CRE~ ~override~
              ~%tutu_var%JELLSPA.CRE~ ~override~
              ~%tutu_var%JELLYMU.CRE~ ~override~
 SAY 0xec @82

COPY_EXISTING ~%tutu_var%CHANTH.CRE~ ~override~
 SAY 0xa4 @83

COPY_EXISTING ~%tutu_var%DOOMDUR.CRE~ ~override~
 SAY 0xd0 @85
 SAY 0x114 @85
COPY_EXISTING ~%tutu_var%DOOMGU.CRE~ ~override~
 SAY 0xd0 @85
 SAY 0x114 @85
 SAY 0x1c0 @85

COPY_EXISTING ~%tutu_var%VULTUR.CRE~ ~override~
 SAY 0x1bc @86

/*STRING_SET 11640 @87 - unused*/

COPY_EXISTING ~%tutu_var%CULT3.CRE~ ~override~
              ~%tutu_var%KARLAT.CRE~ ~override~
              ~%tutu_scripts%LEEPFAT.CRE~ ~override~
 SAY 0xec @88

END ELSE BEGIN //BG
ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN

/////////////////////////////////////////////////////////
// Ajantis sound fix (duplicate, probably unused strings)
STRING_SET 14399 @60
STRING_SET 14400 @61
STRING_SET 14401 @62
STRING_SET 14402 @63

/////////////////////
// Tanar'ri sound fix
ACTION_IF GAME_IS ~totsc~ THEN BEGIN
STRING_SET 23827 @64
STRING_SET 23828 @65
STRING_SET 23829 @66
STRING_SET 23830 @67
STRING_SET 23831 @68
STRING_SET 23832 @69
END

///////////////////////////
// Delsvirftanyon sound fix
STRING_SET 4839 @70
STRING_SET 4842 @71
STRING_SET 12532 @72
STRING_SET 12533 @73

///////////////////////////////
// Gandolar Luckyfoot sound fix
STRING_SET 4843 @74
STRING_SET 4844 @75
STRING_SET 12534 @76
STRING_SET 12535 @77

////////////////////////////////
// Garrick sound fix (not funny)
STRING_SET 3668 @78

/////////////////////////////////////////////
// Xan sound fix (duplicate, probably unused)
STRING_SET 14388 @79

///////////////////
// Misc sound fixes
STRING_SET 2913 @80
STRING_SET 5482 @81
STRING_SET 5962 @82
STRING_SET 11342 @83
STRING_SET 11402 @85
STRING_SET 11635 @86
STRING_SET 11640 @87
STRING_SET 12355 @88

////////////////////////////////////
// Restore Delsvirftanyon's soundset
COPY_EXISTING DELSVIR.CRE ~override~
PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  SAY INITIAL_MEETING #4839
  SAY DAMAGE          #12532
  SAY DYING           #12533
  SAY SELECT_COMMON1  #4842
END
BUT_ONLY

END //BG
END //BGT/Tutu check

// Zombie Soundset Fix, by Macready~
// Author: Macready
// Email: jman@nycap.rr.com
// Website: http://www.usoutpost31.com/easytutu

/* A few zombies have incorrect soundsets */
COPY_EXISTING ~%tutu_scriptz%ombie_b.cre~ ~override/macdummy.cre~
  READ_ASCII 0x00A4 "creatureSoundset" (400) // Read creature soundset array in one chunk
 BUT_ONLY
COPY_EXISTING ~%tutu_scriptz%OMBIE_A.CRE~ ~override~
              ~%tutu_var%ZOMBIE.CRE~ ~override~
              ~%tutu_var%ZOMBIEB.CRE~ ~override~
  WRITE_ASCIIE 0x00A4 "%creatureSoundset%" #400
 BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Store, Tavern and Inn Fixes and Restorations     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @89
//DEPRECATED ~Ascension64~

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBStore.UB~



/////////////////////////////////////////////////////////
// Restore Alvanhendar's healing and sundries in Gullykin
ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN //BGT or Tutu
  ACTION_IF NOT FILE_EXISTS_IN_GAME EVALUATE_BUFFER ~%tutu_var%TEM4003.sto~ THEN BEGIN // if Gullykin does not have a store already
    COPY_EXISTING ~%tutu_var%TEM4802.sto~ ~override/%tutu_var%TEM4003.sto~ // clones and renames Temple of Helm
      SAY 0x0C @90
  END
END ELSE BEGIN
  ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN //BG
  	STRING_SET 20713 @90
  	ACTION_IF NOT FILE_EXISTS_IN_GAME EVALUATE_BUFFER ~TEM4003.sto~ THEN BEGIN // if Gullykin does not have a store already
    	COPY_EXISTING ~TEM4802.sto~ ~override/TEM4003.sto~ // clones and renames Temple of Helm
      	SAY 0x0C #20713
    END
  END
END

/////////////////////////////////////////////////////////
//  Bartender fixes
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN // Already included in BGEE
COPY_EXISTING ~%SEBaldursGate_Inn_L1%.ARE~ ~override~ // Change bartender so that TAV1215 can be renamed
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "%tutu_var%BART16") BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%BART12~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%NWBaldursGate_Helm&Cloak_L1%.ARE~ ~override~ // Restore Helm and Cloak bartender
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "%tutu_var%BART12") BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%BART9~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

////////////////////////////////////////////////////////////////////////////////////////
// Keexie Tavern
COPY_EXISTING ~%EBaldursGate_KeexieTavern_L1%.ARE~ ~override~ // Assign correct bartender, Keexie Tavern downstairs
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF ("%actor%" STRING_EQUAL_CASE "%tutu_var%BART13") OR // in vanilla BG
               ("%actor%" STRING_EQUAL_CASE "%tutu_var%BART12") BEGIN // in DudleyFix'd games
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%BART15~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%EBaldursGate_KeexieTavern_L2%.ARE~ ~override~ // Assign correct bartender, Keexie Tavern upstairs
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "%tutu_var%BART11") BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%BART15~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAV0154.STO~ ~override~ // Keexie Tavern
  SAY 0x0C #20638
BUT_ONLY

////////////////////////////////////////////////////////////////////////////////////////
// Elf Song
COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L1%.ARE~ ~override~ // Restore Elfsong bartender
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "%tutu_var%BART11") BEGIN
        WRITE_ASCIIE actOff + 0x80 ~%tutu_var%BART8~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAV0705.STO~ ~override~ // Elf Song
  SAY 0x0C #11674
BUT_ONLY

////////////////////////////////////////////////////////////////////////////////////////
// Low Lantern
COPY_EXISTING ~%tutu_var%BART15.CRE~ ~override/UBBA0133.CRE~
  WRITE_ASCII 0x2cc ~UBBA0133~

COPY_EXISTING ~%tutu_var%bart15.dlg~ ~override/ubba0133.dlg~

COPY_EXISTING ~%BaldursGate_DocksLowLantern_D1%.ARE~ ~override~ // Restore Low Lantern bartender
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF !("%actor%" STRING_COMPARE_CASE "BART15") BEGIN
        WRITE_ASCII actOff + 0x80 UBBA0133 #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAV0133.STO~ ~override~ // Low Lantern
  SAY 0x0C #14976
BUT_ONLY
END // Bartender fixes

////////////////////////////////////////////////////////////////////////////////////////
// Inn, Tavern, Store name restorations
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN // Already included in BGEE

COPY_EXISTING ~%tutu_var%INN4801.STO~ ~override~ // The Northern Light
  SAY 0x0C #11696
BUT_ONLY

COPY_EXISTING ~%Nashkel%.ARE~ ~override~ // Nashkel
READ_LONG 0x5c trigOff ELSE 0x0
PATCH_IF (trigOff) BEGIN
  FOR (READ_SHORT 0x5a numTrig; numTrig; numTrig -= 0x1) BEGIN
    READ_ASCII trigOff name         // Trigger name
    READ_SHORT trigOff + 0x20 type  // Trigger type
    READ_LONG trigOff + 0x64 string // Info point StrRef
    PATCH_IF ("%name%" STRING_EQUAL_CASE "INFO4801") && (type == 0x1) &&
             ((string == 16266) OR (string == 11683)) BEGIN
      SAY trigOff + 0x64 #11696 // Renames the area info trigger for the inn to say "The Northern Light"
    END
    trigOff += 0xc4
  END
END
BUT_ONLY

COPY_EXISTING ~%tutu_var%STO4906.STO~ ~override~ // Carnival Shop
  SAY 0x0C #11688
BUT_ONLY

COPY_EXISTING ~%tutu_scripts%TOBLACK.STO~ ~override~
  SAY 0x0C @91
BUT_ONLY

COPY_EXISTING ~%tutu_scripts%TOCHEAP.STO~ ~override~ // Lucky Aello's Discount Store
  SAY 0x0C #11818
BUT_ONLY

COPY_EXISTING ~%tutu_scripts%TOSILEN.STO~ ~override~ // Shop of Silence
  SAY 0x0C #20639
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAV0721.STO~ ~override~ // Tavern
  SAY 0x0C @92
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAV1215.STO~ ~override~ // Jopalin's Tavern
  SAY 0x0C #20640
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAV4809.STO~ ~override~ // The Belching Dragon
  SAY 0x0C #11764
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM0002.STO~ ~override~
  SAY 0x0C @93
BUT_ONLY

ACTION_IF GAME_IS ~bg1 totsc bgt~ THEN BEGIN
  COPY_EXISTING ~%tutu_var%tav0810.sto~ ~override~ //Maltz' Weapon Shop
  	SAY NAME2 #11782 //Maltz' Weapon Shop
  BUT_ONLY

  COPY_EXISTING ~%tutu_var%tav0809.sto~ ~override~ //unused General Store
    SAY NAME2 #11685
  BUT_ONLY
END

END // Inn, Tavern, Store name restorations

////////////////////////////////////////////////////////////////////////////////////////
// Item shop in Jovial Juggler
COPY_EXISTING ~%tutu_var%INN3304.STO~ ~override~ // Jovial Juggler
  WRITE_SHORT 0x10 0x4d // Allows purchasing items, stealing items, and identifying
BUT_ONLY

COMPILE EVALUATE_BUFFER ~bg1ub/stores/ubin3304.baf~  // Add stealing script to bartender
COPY_EXISTING ~%tutu_var%BART4.CRE~ ~override~
  WRITE_ASCII 0x260 ~UBIN3304~

////////////////////////////////////////////////////////////////////////////////////////
// Ulgoth's Beard Store and Inn fixes
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN // Already included in BGEE
COPY_EXISTING ~%tutu_var%ULGOTH.STO~ ~override~ // Ulgoth's Beard Store and Inn
  PATCH_IF (%SOURCE_SIZE% > 0x78) BEGIN
    READ_LONG 0x34 saleOff
    FOR (READ_LONG 0x38 numSale; numSale; numSale -= 0x1) BEGIN
      READ_BYTE saleOff + 0x10 flag
      WRITE_BYTE saleOff + 0x10 flag | 0x1 // Mark items identified
      READ_LONG saleOff + 0x14 stock  // Amount available
      READ_LONG saleOff + 0x18 supply // (Un)limited supply
      PATCH_IF ((stock > 0x1) && supply) BEGIN
        WRITE_LONG saleOff + 0x18 0x0 // Removes infinite flag for items with in-stock numbers > 1
      END
    saleOff += 0x1c
    END
  END
BUT_ONLY
END

////////////////////////////////////////////////////////////////////////////////////////
// More Nashkel Carnival Merchants
COPY_EXISTING ~%tutu_var%MERCH2.CRE~ ~override/UBMERCH.CRE~
  WRITE_ASCII 0x2cc ~UBMERCH~

COPY_EXISTING ~%tutu_var%merch2.dlg~ ~override/ubmerch.dlg~

COPY_EXISTING ~%NashkelCarnival%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 2825
    	fj_loc_y          = 3392
    	fj_dest_x         = 2825
    	fj_dest_y         = 3392
    	fj_animation      = 0xC800 // Fat man
    	fj_orientation    = 3      // WSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Merchant
   		fj_cre_resref     = EVALUATE_BUFFER "%tutu_var%MERCHA"
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 2329
    	fj_loc_y          = 3584
    	fj_dest_x         = 2329
    	fj_dest_y         = 3584
    	fj_animation      = 0xC800 // Fat man
    	fj_orientation    = 12      // E
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Merchant
   		fj_cre_resref     = UBMERCH
	END
BUT_ONLY

/* <<<<<<<< .../bg1ub/ub_ar4900.baf
IF
Global("UB_NASHKELMERCH1_SPAWN","%NashkelCarnival%",0)
THEN
RESPONSE #100
CreateCreature("%tutu_var%MERCHA",[2825.3392]%FACE_3%)
SetGlobal("UB_NASHKELMERCH1_SPAWN","%NashkelCarnival%",1)
END

IF
Global("UB_NASHKELMERCH2_SPAWN","%NashkelCarnival%",0)
THEN
RESPONSE #100
CreateCreature("UBMERCH",[2329.3584]%FACE_12%)
SetGlobal("UB_NASHKELMERCH2_SPAWN","%NashkelCarnival%",1)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%NashkelCarnival%" bottom=".../bg1ub/ub_ar4900.baf" END
*/

// Fixes the overused STO1115.STO file
ACTION_IF GAME_IS ~tutu tutu_totsc bgt~ THEN BEGIN
	COPY_EXISTING ~%tutu_var%STO1115.STO~ ~override/UBSTO01.sto~
	COPY_EXISTING ~%tutu_var%STO1115.STO~ ~override/UBSTO02.sto~
	COPY_EXISTING ~%tutu_var%STO1115.STO~ ~override/UBSTO03.sto~
END

////////////////////////////////////////////////////////////////////////////////////////
// SW Baldur's Gate Weapon Shop
ACTION_IF GAME_IS ~bgt~ THEN BEGIN //only BGT for now
      COPY ~bg1ub/%tutuorbgt%/CRE/%tutu_scriptbg%SHOP05.CRE~ ~override~
       SAY NAME1 @10179
       SAY NAME2 @10179
       SAY INITIAL_MEETING @10180
       SAY DAMAGE @10181
       SAY DYING @10182
       SAY SELECT_COMMON1 @10183
       SAY SELECT_COMMON2 @10184
       SAY SELECT_COMMON3 @10185
END

ACTION_IF GAME_IS ~bg1 totsc bgt~ THEN BEGIN // BG1, BGT only
COPY_EXISTING ~%SWBaldursGate_WeaponsStore2%.are~ ~override~ //Fix duplicate weapon store
  PATCH_IF SOURCE_SIZE > 0x11c BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 1) BEGIN
          READ_ASCII actOff + 0x80 actor
          PATCH_IF (~%actor%~ STRING_EQUAL_CASE ~%tutu_scriptbg%SHOP03~ = 1) BEGIN
            WRITE_ASCIIE actOff + 0x80 ~%tutu_scriptbg%SHOP05~ #8
          END
          actOff += 0x110
    END
  END
BUT_ONLY
END

////////////////////////////////////////////////////////////////////////////////////////
// Elfsong Tavern Merchant
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN // Already included in BGEE

COPY_EXISTING ~%tutu_var%merch4.cre~ ~override/ubme0706.cre~ //Copy merchant used in Nashkel Carnival
  WRITE_ASCII 0x2cc ~ubme0706~ #8 //Dialog

COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L2%.are~ ~override~ //Correct Elfsong merchant
  PATCH_IF (SOURCE_SIZE > 0x11c) BEGIN
    READ_LONG 0x54 actOff
    FOR (READ_SHORT 0x58 numAct; numAct; numAct -= 0x1) BEGIN
      READ_ASCII actOff + 0x80 actor
      PATCH_IF (~%actor%~ STRING_EQUAL_CASE ~MERCH4~ = 1) BEGIN
        WRITE_ASCII actOff + 0x80 ~UBME0706~ #8
      END
      actOff += 0x110
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%sto4906.sto~ ~override/ubst0706.sto~ //Merchant
  SAY NAME2 #9432 //Merchant
BUT_ONLY

COPY_EXISTING ~%tutu_var%merch4.dlg~ ~override/ubme0706.dlg~
END // Elfsong


ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/stores/cpm/ubstores.d~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/stores/cpm/ubstores.d~
  COMPILE EVALUATE_BUFFER ~bg1ub/stores/cpm/ubstores_bgtadd.d~
END

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~bg1ub/stores/cpm/ubstores_bgee.d~
END

ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
  COMPILE ~bg1ub/stores/ubstores.d~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Item Corrections and Restorations                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @94

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBItems.UB~

// Ground, inventory and description icon restorations (NiGHTMARE)
COPY_EXISTING ~%tutu_var%book68.itm~ ~override~ // History of the Nether Scrolls
  WRITE_ASCIIE 0x3a "%tutu_var%ibook68"
BUT_ONLY

COPY_EXISTING ~%tutu_var%boot06.itm~ ~override~ // Worn Out Boots
  WRITE_ASCIIE 0x3a "%tutu_var%iboot06"
  WRITE_ASCIIE 0x58 "%tutu_var%cboot06"
BUT_ONLY

COPY_EXISTING ~%tutu_var%brac11.itm~ ~override~ // Bracers Of Binding
  PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    WRITE_ASCIIE 0x3a "%tutu_var%ibrac11"
    WRITE_ASCIIE 0x58 "%tutu_var%cbrac11"
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%clck08.itm~ ~override~ // Algeron's Cloak
  WRITE_ASCIIE 0x58 "%tutu_var%cclck08"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc13.itm~ ~override~ // Samuel's Body
  WRITE_ASCIIE 0x44 "%tutu_var%gmisc13"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc1a.itm~ ~override~ // Bottle Of Wine
  PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    WRITE_ASCIIE 0x3a "%tutu_var%imisc1a"
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc1b.itm~ ~override~ // Butter Knife Of Balduran
  PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    WRITE_ASCIIE 0x58 "%tutu_var%cmisc1b"
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc62.itm~ ~override~ // Dead Cat
  WRITE_ASCIIE 0x44 "%tutu_var%gmisc62"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc65.itm~ ~override~ // Brage's Body
  WRITE_ASCIIE 0x3a "%tutu_var%imisc65"
  WRITE_ASCIIE 0x44 "%tutu_var%gmisc65"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc68.itm~ ~override~ // Abela's Body
  WRITE_ASCIIE 0x3a "%tutu_var%imisc68"
  WRITE_ASCIIE 0x44 "%tutu_var%gmisc68"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc70.itm~ ~override~ // Delorna's Statue
  WRITE_ASCIIE 0x3a "%tutu_var%imisc70"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc71.itm~ ~override~ // Delorna's Spellbook
  WRITE_ASCIIE 0x3a "%tutu_var%imisc71"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc75.itm~ ~override~ // Dagger Of Venom
  WRITE_ASCIIE 0x3a "%tutu_var%imisc75"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc76.itm~ ~override~ // Dream Potion
  WRITE_ASCIIE 0x3a "%tutu_var%imisc76"
  WRITE_ASCIIE 0x58 "%tutu_var%cpotn13"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc77.itm~ ~override~ // Skull Of Kereph
  WRITE_ASCIIE 0x3a "%tutu_var%imisc77"
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc92.itm~ ~override~ // Switch For An Engine
  PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    WRITE_ASCIIE 0x44 "%tutu_var%gmisc92"
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc94.itm~ ~override~ // Mallet Head
  PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    WRITE_ASCIIE 0x44 "%tutu_var%gmisc94"
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%misc95.itm~ ~override~ // Mallet Handle
  PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    WRITE_ASCIIE 0x44 "%tutu_var%gmisc95"
  END
BUT_ONLY

///////////////////////////////////////////////////////////////////////
// History of Waterdeep - Age III, The Bloody Reign of the Guildmasters
COPY_EXISTING ~%Candlekeep_Library_L1%.are~ override
	LPF ADD_CONTAINER_ITEM
		STR_VAR item_to_add = EVALUATE_BUFFER ~%tutu_var%BOOK65~ flags = "IDENTIFIED" container = "Container 10"
	END
BUT_ONLY


///////////////////////////////////////////////////
// Additional Durlag's Tower Book ("Seek No Heirs")
COPY_EXISTING ~%DurlagsTower_D2%.are~ override
	LPF ADD_CONTAINER_ITEM
		STR_VAR item_to_add = EVALUATE_BUFFER ~%tutu_var%BOOK80~ flags = "IDENTIFIED" container = "Container26"
	END
BUT_ONLY

//////////////////////////////////////////////////
// Additional Durlag's Tower Book ("Know My Loss")
COPY_EXISTING ~%DurlagsTower_D2%.are~ override
	LPF ADD_CONTAINER_ITEM
		STR_VAR item_to_add = EVALUATE_BUFFER ~%tutu_var%BOOK85~ flags = "IDENTIFIED" container = "Container18"
	END
BUT_ONLY

//////////////////
// Generic Bracers
COPY_EXISTING ~%Beregost%.are~ override
	LPF ADD_CONTAINER_ITEM
		STR_VAR item_to_add = EVALUATE_BUFFER ~%tutu_var%BRAC05~ flags = "IDENTIFIED" container = "Container 1"
	END
BUT_ONLY

////////////////////////////////
// Gauntlets of Weapon Expertise
/* <<<<<<<< .../bg1ub/br_0102.baf
IF
  Global("BG1UBBRAC10","%BanditCamp_RaemonsTent%",0)
THEN
  RESPONSE #100
      ActionOverride("Container1",CreateItem("%tutu_var%BRAC10",0,0,0))
      SetGlobal("BG1UBBRAC10","%BanditCamp_RaemonsTent%",1)
END
>>>>>>>>

OUTER_SPRINT temp_tutu "_AR1901" OUTER_SPRINT temp_bgt "AR8701" OUTER_SPRINT temp_bg "AR1901" LAUNCH_ACTION_MACRO "getcpmvara"
EXTEND_BOTTOM ~%temp_cpmvar%.bcs~ ~.../bg1ub/br_0102.baf~
  EVALUATE_BUFFER*/

COPY_EXISTING ~%tutu_var%MISC1H.ITM~ ~override~ // Gong Mallet
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    WRITE_ASCIIE ~0x44~ ~%tutu_var%GBLUN06~ // Assign GBLUN06.BAM as the ground icon
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%SW2H05.ITM~ ~override~ //
  SAY NAME1 #16477 // Sets name to "Sword of Chaos"
  SAY NAME2 #16477
BUT_ONLY

ACTION_IF !GAME_IS ~bgt~ THEN BEGIN //BG, Tutu, BGEE
  COPY_EXISTING ~RNDTREAS.2DA~ ~override~
    READ_2DA_ENTRIES_NOW ~_#_#_#rndtreas~ 20
    FOR (row = 0; row < "%_#_#_#rndtreas%"; row += 1) BEGIN
      READ_2DA_ENTRY_FORMER ~_#_#_#rndtreas~ row 0 col_name
      PATCH_IF !("%col_name%" STRING_COMPARE_CASE "POOR") BEGIN //find the first RNDTREAS.2DA entry
        SET_2DA_ENTRY "%row%" 8 20 ~%tutu_var%AMUL02~ // Adds generic amulets to the Random Treasure table
        SET_2DA_ENTRY "%row%" 9 20 ~%tutu_var%RING01~ // Adds generic rings to the Random Treasure table
      END
    END
  BUT_ONLY

  COPY_EXISTING ~RNDSCROL.2DA~ ~override~
    READ_2DA_ENTRIES_NOW ~_#_#_#rndscrol~ 20
    FOR (row = 0; row < "%_#_#_#rndscrol%"; row += 1) BEGIN
      READ_2DA_ENTRY_FORMER ~_#_#_#rndscrol~ row 0 col_name
      PATCH_IF !("%col_name%" STRING_COMPARE_CASE "4th") BEGIN //find the fourth RNDSCRL.2DA entry
        SET_2DA_ENTRY "%row%" 10 20 ~%tutu_var%SCRL2F~ // Adds the Cone of Cold scroll to the Random Scroll table
      END
    END
  BUT_ONLY
END ELSE BEGIN //BGT
  //modified to work with BGT dynamic random treasure patching
  COPY_EXISTING ~RNDEQUIP.2DA~ ~override~
                ~RNDMAGIC.2DA~ ~override~
                ~RNDSCROL.2DA~ ~override~
                ~RNDTREAS.2DA~ ~override~
                ~RNDWEP.2DA~ ~override~
    READ_2DA_ENTRIES_NOW ~_#_#_#rndtreas~ 20
    FOR (row = 0; row < "%_#_#_#rndtreas%"; row += 1) BEGIN
      READ_2DA_ENTRY_FORMER ~_#_#_#rndtreas~ row 0 col_name
      PATCH_IF !("%col_name%" STRING_COMPARE_CASE "POOR_BG1") BEGIN //find the first RNDTREAS.2DA entry
        PATCH_IF !("%SOURCE_RES%" STRING_CONTAINS_REGEXP "RND[EWS].*") BEGIN //if 2DA rolls 1D20
          SET_2DA_ENTRY "%row%" 9 20 ~AMUL02~ // Adds generic amulets to the Random Treasure table
          SET_2DA_ENTRY "%row%" 10 20 ~RING01~ // Adds generic rings to the Random Treasure table
        END ELSE BEGIN //if 2DA rolls 2D10
          SET_2DA_ENTRY "%row%" 8 20 ~AMUL02~ // Adds generic amulets to the Random Treasure table
          SET_2DA_ENTRY "%row%" 9 20 ~RING01~ // Adds generic rings to the Random Treasure table
        END
      END
      PATCH_IF !("%col_name%" STRING_COMPARE_CASE "4thBG") BEGIN //find the fourth RNDSCRL.2DA entry
        SET_2DA_ENTRY "%row%" 10 20 ~SCRL2F~ // Adds the Cone of Cold scroll to the Random Scroll table
      END
    END
  BUT_ONLY
END

COPY_EXISTING ~%tutu_var%SQUIRP.ITM~ ~override~ // Restores the Squirrel Paw icon (devSin)
  PATCH_IF (SOURCE_SIZE > 0x72) BEGIN // see if I can imitate Cam :D
    WRITE_ASCIIE 0x3a ~%tutu_var%ISQUIRL~ #8
    READ_SHORT 0x68 "num_abl"
    PATCH_IF ("%num_abl%" > 0x0) BEGIN
      READ_LONG 0x64 "abl_offset"
      WRITE_ASCIIE "%abl_offset%" + 0x4 ~%tutu_var%ISQUIRL~ #8
    END
  END
BUT_ONLY

COPY_EXISTING ~SPIN109.SPL~ ~override~ // Restores the unused Lay on Hands spell icon (devSin)
  PATCH_IF (SOURCE_SIZE > 0x72) BEGIN
    READ_LONG 0x64 ablOff
    FOR (READ_SHORT 0x68 numAbl; numAbl > 0x0; numAbl -= 0x1) BEGIN
      WRITE_ASCII ablOff + 0x4 ~SPIN109~ #8
      ablOff += 0x28
    END
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%MAGE02.ITM~ ~override~
  SAY NAME2 @95 // Item originally did not have a name
BUT_ONLY

ACTION_IF !GAME_IS ~bgee~ THEN BEGIN // Skip for BGEE
	COPY_EXISTING ~%tutu_var%BOLT03.ITM~ ~override~ // Bolt of Lightning
  		WRITE_ASCIIE 0x11e ~%tutu_scripte%FF_M23C~ // Replace invalid wav file reference
	BUT_ONLY
END

COPY_EXISTING ~%tutu_var%POTN41.ITM~ ~override~ // Potion of Power
              /*~%tutu_var%PTION41.ITM~ ~override~ - unused*/
  WRITE_ASCIIE 0x44 ~%tutu_var%GPOTN01~ // Corrected ground icon
BUT_ONLY

/* unused
COPY_EXISTING ~%tutu_var%PTION41.ITM~ ~override~ // Potion of Power
  WRITE_ASCIIE 0x58 ~%tutu_var%CPOTN41~ #8      // Assigns a description icon
BUT_ONLY */

COPY_EXISTING ~%tutu_var%BLUN06.ITM~ ~override~ // Morningstar
  WRITE_ASCII 0x22 ~MS~ // Changes Inventory Icon ID from Mace to Morningstar
BUT_ONLY

// Weapon colouration now works.  Thanks, CamDawg!
COPY_EXISTING ~%tutu_var%BLUN06.ITM~ ~override~ // Morningstar
              ~%tutu_var%BLUN10.ITM~ ~override~ // The Root of the Problem
              ~%tutu_var%HELM14.ITM~ ~override~ // Kiel's Helmet
PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_LONG  0x6a "fx_off"
    READ_SHORT 0x70 "fx_num"
    FOR (index = 0; index < "%fx_num%"; index = index + 1) BEGIN // searches through global effects
      WRITE_BYTE ("%fx_off%" + 0x0c + ("%index%" * 0x30)) 2 // instant/while equipped
      WRITE_LONG ("%fx_off%" + 0x0e + ("%index%" * 0x30)) 0 // duration
    END
  END
BUT_ONLY

// EFF fix for The Root of the Problem
COPY_EXISTING ~GIANDAM.EFF~ ~override~
              ~MONSTDAM.EFF~ ~override~
              ~UNDEDDAM.EFF~ ~override~
              ~GIANHIT.EFF~ ~override~
              ~MONSTHIT.EFF~ ~override~
              ~UNDEDHIT.EFF~ ~override~
  PATCH_IF (SOURCE_SIZE > 0) THEN BEGIN
    WRITE_SHORT 0x2C 100 //Probability 1
  END
BUT_ONLY

COPY_EXISTING ~%tutu_scriptbg%BISHOP.CRE~ ~override~ // Durlag's Tower Bishop
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    ADD_CRE_ITEM ~%tutu_var%SCRL62~ #1 #0 #0 ~IDENTIFIED~ ~INV1 INV2~ // Adds scroll of Flamestrike to inventory (Andyr)
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%LAMALH.CRE~ ~override~ // Lamalha
  ADD_CRE_ITEM ~%tutu_var%SCRL62~ #1 #0 #0 ~IDENTIFIED~ ~INV1 INV2~ // Adds scroll of Flamestrike to inventory (Andyr)
BUT_ONLY

COPY_EXISTING ~%tutu_var%KIVAN.CRE~ ~override~ // Kivan
              ~%tutu_var%KIVAN4.CRE~ ~override~
              ~%tutu_var%KIVAN6.CRE~ ~override~
              ~%tutu_var%CORAN.CRE~ ~override~ // Coran
              ~%tutu_var%CORAN5.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%BRAC05~ #1 #0 #0 ~IDENTIFIED~ ~GLOVES~ // Assigns generic bracers
BUT_ONLY

COPY_EXISTING /*~%tutu_var%AJANTD.CRE~ ~override~ // Ajantis - unused*/
              ~%tutu_var%AJANTI.CRE~ ~override~ //Ajantis
              ~%tutu_var%AJANTI4.CRE~ ~override~
              ~%tutu_var%AJANTI6.CRE~ ~override~
              ~%tutu_var%KAGAIN.CRE~ ~override~ // Kagain
              ~%tutu_var%KAGAIN2.CRE~ ~override~
              ~%tutu_var%KAGAIN4.CRE~ ~override~
              ~%tutu_var%KAGAIN6.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%BELT01~ #1 #0 #0 ~IDENTIFIED~ ~BELT~ // Assigns girdle
BUT_ONLY

COPY_EXISTING ~%tutu_var%HENTOL.CRE~ ~override~ // Hentold
  ADD_CRE_ITEM ~%tutu_var%DAGG08~ #0 #0 #0 ~IDENTIFIED~ ~INV1 INV5~ // Assign Hentold's Dagger to his inventory
BUT_ONLY

ACTION_IF !GAME_IS ~bgee~ THEN BEGIN // BG, BGT, Tutu
  COPY_EXISTING ~%tutu_var%AMARAN.CRE~ ~override~ // Amarande, the Arch Druid
	REPLACE_CRE_ITEM "%tutu_var%blun10" #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP 
  BUT_ONLY
END ELSE BEGIN  // BGEE
  COPY_EXISTING ~CORSONE.CRE~ ~override~  // Amarande already has Mighty Oak +2 in BGEE, give Corsone "The Root of the Problem" instead
	REPLACE_CRE_ITEM "%tutu_var%blun10" #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP 
  BUT_ONLY
END

COPY_EXISTING ~%tutu_scriptbg%SENDAI.CRE~ ~override~ // Sendai Argrim
	REPLACE_CRE_ITEM "%tutu_var%SW1H22" #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP 
BUT_ONLY

COPY_EXISTING ~%tutu_scriptk%NIGHTSK.CRE~ ~override~ // Undead Knight, AR5201 (Firewine Ruins)
	REPLACE_CRE_ITEM "%tutu_var%SPER03" #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED
BUT_ONLY

COPY_EXISTING ~%tutu_var%KNIGHT6.CRE~ ~override~ // Ghost Knight, AR5201 (Firewine Ruins)
	REPLACE_CRE_ITEM "%tutu_var%BOW04" #0 #0 #0 ~UNSTEALABLE~ ~WEAPON1~ EQUIP TWOHANDED
BUT_ONLY

COPY_EXISTING ~%tutu_var%INN3351.STO~ ~override~ // Feldepost's Inn
  ADD_STORE_ITEM ~%tutu_var%HELM06~ AFTER ~%tutu_var%BRAC01~ #0 #0 #0 ~IDENTIFIED~ #1 // Helmet of Charm Protection
BUT_ONLY

COPY_EXISTING ~%tutu_scripts%TOCHEAP.STO~ ~override~ // Lucky Aello's
  ADD_STORE_ITEM ~%tutu_var%LEAT06~ LAST #0 #0 #0 ~NONE~ #1 // Studded Leather Armor +2 : Missle Attraction
BUT_ONLY

ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
/* In BG1, the raise dead scroll has an incorrect target type. */
	COPY ~bg1ub/ITM/SCRL63.ITM~ ~override~
END

COPY_EXISTING ~%tutu_var%TEM0002.STO~ ~override~ // The Watchful Shield (Temple of Helm, AR0002: BG)
  // ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #5 // Cure Critical Wounds, qty 5
  ADD_STORE_ITEM ~%tutu_var%SCRL62~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Flame Strike scroll, qty 1 (was 3)
  // ADD_STORE_ITEM ~%tutu_var%SCRL63~ AFTER ~%tutu_var%SCRL62~ #1 #0 #0 ~IDENTIFIED~ #3 // Raise Dead scroll, qty 3
  // ADD_STORE_ITEM ~%tutu_var%SCRL5F~ AFTER ~%tutu_var%SCRL63~ #1 #0 #0 ~IDENTIFIED~ #5 // Chaotic Commands, qty 5
  // ADD_STORE_ITEM ~%tutu_var%SCRL5E~ AFTER ~%tutu_var%SCRL5F~ #1 #0 #0 ~IDENTIFIED~ #5 // Champion's Strength, qty 5
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM0131.STO~ ~override~ // High Hall of Wonders (Temple of Gond, AR0131: BG)
  // ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #5 // Cure Critical Wounds, qty 5
  ADD_STORE_ITEM ~%tutu_var%SCRL63~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Raise Dead scroll, qty 1 (was 3)
  // ADD_STORE_ITEM ~%tutu_var%SCRL5F~ AFTER ~%tutu_var%SCRL63~ #1 #0 #0 ~IDENTIFIED~ #5 // Chaotic Commands, qty 5
  // ADD_STORE_ITEM ~%tutu_var%SCRL5E~ AFTER ~%tutu_var%SCRL5F~ #1 #0 #0 ~IDENTIFIED~ #5 // Champion's Strength, qty 5
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM0132.STO~ ~override~ // Lady's Hall (Temple of Tymora, AR0132: BG)
  // ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #5 // Cure Critical Wounds, qty 5
  ADD_STORE_ITEM ~%tutu_var%SCRL5F~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Chaotic Commands, qty 1 (was 5)
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM2304.STO~ ~override~ // Temple of Wisdom (Temple of Garl Glittergold, AR2304: FAI)
  ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Cure Critical Wounds, qty 1 (was 5)
  // ADD_STORE_ITEM ~%tutu_var%SCRL63~ AFTER ~%tutu_var%SCRL61~ #1 #0 #0 ~IDENTIFIED~ #3 // Raise Dead scroll, qty 3
  ADD_STORE_ITEM ~%tutu_var%SCRL5F~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Chaotic Commands, qty 1 (was 5)
  // ADD_STORE_ITEM ~%tutu_var%SCRL5E~ AFTER ~%tutu_var%SCRL5F~ #1 #0 #0 ~IDENTIFIED~ #5 // Champion's Strength, qty 5
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM2601.STO~ ~override~ // Oghma Temple (Temple of Oghma, AR2600: Candlekeep)
  // ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #5 // Cure Critical Wounds, qty 5
  ADD_STORE_ITEM ~%tutu_var%SCRL63~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Raise Dead scroll, qty 1 (was 3)
  // ADD_STORE_ITEM ~%tutu_var%SCRL5E~ AFTER ~%tutu_var%SCRL63~ #1 #0 #0 ~IDENTIFIED~ #5 // Champion's Strength, qty 5
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM3402.STO~ ~override~ // Song of the Morning (Temple of Lathander, AR3402: West of Beregost)
  ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #2 // Cure Critical Wounds, qty 2 (was 5)
  ADD_STORE_ITEM ~%tutu_var%SCRL63~ LAST #1 #0 #0 ~IDENTIFIED~ #1 // Raise Dead scroll, qty 1 (was 3)
  // ADD_STORE_ITEM ~%tutu_var%SCRL5F~ AFTER ~%tutu_var%SCRL63~ #1 #0 #0 ~IDENTIFIED~ #5 // Chaotic Commands, qty 5
  // ADD_STORE_ITEM ~%tutu_var%SCRL5E~ AFTER ~%tutu_var%SCRL5F~ #1 #0 #0 ~IDENTIFIED~ #5 // Champion's Strength, qty 5
BUT_ONLY

COPY_EXISTING ~%tutu_var%TEM4802.STO~ ~override~ // Temple of Helm (Temple of Helm, AR4802: Nashkel)
  // ADD_STORE_ITEM ~%tutu_var%SCRL61~ LAST #1 #0 #0 ~IDENTIFIED~ #5 // Cure Critical Wounds, qty 5
  // ADD_STORE_ITEM ~%tutu_var%SCRL62~ AFTER ~%tutu_var%SCRL61~ #1 #0 #0 ~IDENTIFIED~ #1 // Flame Strike scroll, qty 1
  // ADD_STORE_ITEM ~%tutu_var%SCRL5F~ AFTER ~%tutu_var%SCRL63~ #1 #0 #0 ~IDENTIFIED~ #5 // Chaotic Commands, qty 5
  ADD_STORE_ITEM ~%tutu_var%SCRL5E~ LAST #1 #0 #0 ~IDENTIFIED~ #2   // Champion's Strength, qty 2 (was 5)
  ADD_STORE_ITEM ~%tutu_var%SCRL63~ LAST #1 #0 #0 ~IDENTIFIED~ #3   // Raise Dead scroll, qty 3
BUT_ONLY

COPY_EXISTING ~%tutu_var%ULGOTH.STO~ ~override~ // Ulgoth's Beard Store and Inn
  PATCH_IF (SOURCE_SIZE > 0x74) BEGIN
    ADD_STORE_ITEM ~%tutu_var%SCRL2D~ AFTER ~%tutu_var%SCRL1Y~ #1 #0 #0 ~IDENTIFIED~ #1 // Animate Dead scroll, qty 1
    // ADD_STORE_ITEM ~%tutu_var%SCRL2F~ AFTER ~%tutu_var%SCRL2D~ #1 #0 #0 ~IDENTIFIED~ #1 // Cone of Cold scroll, qty 1
    ADD_STORE_ITEM ~%tutu_var%SCRL2H~ AFTER ~%tutu_var%SCRL2D~ #1 #0 #0 ~IDENTIFIED~ #1 // Shadow Door scroll, qty 1
    ADD_STORE_ITEM ~%tutu_var%SCRL5L~ AFTER ~%tutu_var%SCRL2H~ #1 #0 #0 ~IDENTIFIED~ #1 // Polymorph Other scroll, qty 1
    // ADD_STORE_ITEM ~%tutu_var%SCRL61~ AFTER ~%tutu_var%SCRL5L~ #1 #0 #0 ~IDENTIFIED~ #5 // Cure Critical Wounds, qty 5
    ADD_STORE_ITEM ~%tutu_var%SCRL62~ AFTER ~%tutu_var%SCRL5L~ #1 #0 #0 ~IDENTIFIED~ #1 // Flame Strike scroll, qty 1
    ADD_STORE_ITEM ~%tutu_var%SCRL63~ AFTER ~%tutu_var%SCRL62~ #1 #0 #0 ~IDENTIFIED~ #3 // Raise Dead scroll, qty 3
    // ADD_STORE_ITEM ~%tutu_var%SCRL5F~ AFTER ~%tutu_var%SCRL63~ #1 #0 #0 ~IDENTIFIED~ #5 // Chaotic Commands, qty 5
    // ADD_STORE_ITEM ~%tutu_var%SCRL5E~ AFTER ~%tutu_var%SCRL5F~ #1 #0 #0 ~IDENTIFIED~ #5 // Champion's Strength, qty 5
    ADD_STORE_ITEM ~%tutu_var%BOLT05~ AFTER ~%tutu_var%BOLT03~ #1 #0 #0 ~IDENTIFIED~ #5 // Bolt of Polymorphing, qty 5 (was 10)
  END
BUT_ONLY

COPY_EXISTING ~%tutu_var%ERDANE.STO~ ~override~ // Erdane
  PATCH_IF (SOURCE_SIZE > 0x74) BEGIN
    ADD_STORE_ITEM ~%tutu_var%BOLT05~ AFTER ~%tutu_var%BOLT03~ #1 #0 #0 ~IDENTIFIED~ #7 // Bolt of Polymorphing, qty 7 (was 60)
  END
BUT_ONLY

// COPY_EXISTING ~%tutu_scripth%IGHHEDG.STO~ ~override~ // High Hedge
  // ADD_STORE_ITEM ~%tutu_var%BOLT05~ LAST #1 #0 #0 ~IDENTIFIED~ #200 // Bolt of Polymorphing, qty 200
// BUT_ONLY

COPY_EXISTING ~%tutu_var%FRIEND.STO~ ~override~ // Friendly Arm Inn
  ADD_STORE_ITEM ~%tutu_var%XBOW02~ AFTER ~%tutu_var%XBOW01~ #0 #0 #0 ~IDENTIFIED~ #1 // Heavy Crossbow +1, qty 1
  ADD_STORE_ITEM ~%tutu_var%BRAC05~ LAST #0 #0 #0 ~IDENTIFIED~ #1   // Bracers
  ADD_STORE_ITEM ~%tutu_var%BELT01~ AFTER ~%tutu_var%BRAC05~ #0 #0 #0 ~IDENTIFIED~ #1 // Girdle
BUT_ONLY

COPY_EXISTING ~%tutu_var%INN2616.STO~ ~override~ // Candlekeep Inn
              ~%tutu_var%INN3304.STO~ ~override~ // Jovial Juggler
              ~%tutu_var%STO1112.STO~ ~override~ // General Store
              ~%tutu_var%STO1320.STO~ ~override~ // General Store
  ADD_STORE_ITEM ~%tutu_var%BRAC05~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // Bracers
  ADD_STORE_ITEM ~%tutu_var%BELT01~ AFTER ~%tutu_var%BRAC05~ #0 #0 #0 ~IDENTIFIED~ #1 // Girdle
BUT_ONLY

COPY_EXISTING ~%tutu_var%STO4803.STO~ ~override~ // Nashkel Store
  ADD_STORE_ITEM ~%tutu_var%BRAC05~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // Bracers
  ADD_STORE_ITEM ~%tutu_var%BELT01~ AFTER ~%tutu_var%BRAC05~ #0 #0 #0 ~IDENTIFIED~ #1 // Girdle
BUT_ONLY

COPY_EXISTING ~%tutu_var%TAERUM.STO~ ~override~ // Thunderhammer Smithy
  ADD_STORE_ITEM ~%tutu_var%BRAC05~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // Bracers
  ADD_STORE_ITEM ~%tutu_var%BELT01~ AFTER ~%tutu_var%BRAC05~ #0 #0 #0 ~IDENTIFIED~ #1 // Girdle
BUT_ONLY

COPY_EXISTING ~%tutu_var%STO0703.STO~ ~override~ // Sorcerous Sundries
  ADD_STORE_ITEM ~%tutu_var%BOLT05~ AFTER ~%tutu_var%BOLT06~ #1 #0 #0 ~IDENTIFIED~ #10 // Bolt of Polymorphing, qty 10 (was 100)
BUT_ONLY

// Removes the following sentence from the description of Bala's Axe: "The axe gives the owner the ability to dispel magic once a day."
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN
	COPY_EXISTING ~%tutu_var%AX1H07.ITM~ ~override~
  		PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    		SAY 0x54 @96
  		END
	BUT_ONLY
END ELSE BEGIN
	COPY_EXISTING ~AX1H07.ITM~ ~override~
  		PATCH_IF (%SOURCE_SIZE% > 0) BEGIN
    		SAY 0x54 @115
  		END
	BUT_ONLY
END

/////////////////////////////////////////
// Adds weight to the items' descriptions
ACTION_IF !GAME_IS ~bgee~ THEN BEGIN  // Already included in BGEE
	COPY_EXISTING ~%tutu_var%MISC52.ITM~ ~override~ // Wyvern Head
		SAY 0x54 @97

	COPY_EXISTING ~%tutu_var%MISC01.ITM~ ~override~ // Winter Wolf Pelt
  		SAY 0x50 @98
		SAY 0x54 @98

	COPY_EXISTING ~%tutu_var%MISC12.ITM~ ~override~ // Ankheg Shell
		SAY 0x54 @99
END

//Give CLCK11 electricity bonus
ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN //BG only
  COPY_EXISTING ~%tutu_var%CLCK11.ITM~ ~override~
    LPF ADD_ITEM_EQEFFECT
    	INT_VAR opcode=29 // Opcode: Stat: Electricity Resistance Modifier [29]
    		target=1 // target: self
    		timing=2 // Timing: While equipped
    		parameter1=20 // Parameter 1: Stat modifier
    		parameter2=0 // Parameter 2: Cumulative
    		probability1=100 // Probability 1 - max
    		probability2=0 // Probability 2 - min
    END
END

//Change "Immunity to Charm creature" effect to "Immunity to Hold creature" like Free Action spell
COPY_EXISTING ~%tutu_var%POTN45.ITM~ ~override~
  READ_LONG 0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG 0x6a fx_off

  FOR (i = 0; i < "%abil_num%"; i += 1) BEGIN
    READ_SHORT (%abil_off% + %i% * 0x38 + 0x1E) fx_abil_num
    READ_SHORT (%abil_off% + %i% * 0x38 + 0x20) fx_abil_idx
    FOR (j = %fx_abil_idx%; j < %fx_abil_idx% + %fx_abil_num%; j +=1) BEGIN
      READ_SHORT (%fx_off% + %j% * 0x30) fx_type
      READ_LONG (%fx_off% + %j% * 0x30 + 0x8) fx_param2
      PATCH_IF (%fx_type% = 101) AND (%fx_param2% = 5) BEGIN //Immunity to effect [101]: Charm creature [5]
        WRITE_LONG (%fx_off% + %j% * 0x30 + 0x8) 175 //Hold creature
      END
    END
  END
BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Area Corrections and Restorations                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @100
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBAreas.UB~

/////////////////////////////////
// Restore the Halfling Enforcers

// Fixes the HALFEN dialog so that it, you know, works (thanks, idobek!)
ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN //BGT or Tutu
	COMPILE EVALUATE_BUFFER ~bg1ub/area/cpm/ubarea.d~
END ELSE BEGIN //BG
  	COMPILE ~bg1ub/area/ubarea.d~
END

  // Assign the Halfling Enforcers the INITDLG script
COPY_EXISTING ~%tutu_var%HALFEN.CRE~ ~override~
  	WRITE_ASCIIE 0x258 ~%tutu_var%INITDLG~
BUT_ONLY

  // Script to spawn Halfling Enforcers if caught stealing
COPY_EXISTING ~%Gullykin_JenkalsHouse_Cellar%.ARE~ ~override~ // Gullykin house
		~%Gullykin_House3_Cellar%.ARE~ ~override~ // Gullykin house
        ~%Gullykin_House4_Cellar%.ARE~ ~override~ // Gullykin house
	READ_LONG 0x70 contOff ELSE 0x0
	PATCH_IF (contOff) BEGIN
  	  	FOR (READ_SHORT 0x74 numCont; numCont; numCont -= 0x1) BEGIN
    		READ_ASCII contOff name // Container name
    		PATCH_IF !("%name%" STRING_COMPARE_CASE "LCHEST") BEGIN // LChest container
      			WRITE_ASCIIE contOff + 0x48 ~%tutu_var%ACT13~ #8 // Summon halfling enforcers
      			WRITE_SHORT contOff + 0x2c 0x64     // Trap undetectable
      			WRITE_SHORT contOff + 0x2e 0x64     // Trap unremovable
      			WRITE_SHORT contOff + 0x30 0x1      // Enable trap
    		END
    		contOff += 0xc0
  		END
	END
BUT_ONLY

////////////////////////////////
// Firedrake Script Restorations
COPY_EXISTING ~%DurlagsTower_D2%.ARE~ ~override~ // Durlag's Tower, Firedrake Statue Room
READ_LONG 0x5c trigOff ELSE 0x0
PATCH_IF (trigOff) BEGIN
 FOR (READ_SHORT 0x5a numTrig; numTrig; numTrig -= 0x1) BEGIN
   READ_ASCII trigOff name (10)   // Trigger name
   READ_SHORT trigOff + 0x20 type // Trigger type
   PATCH_IF !("%name%" STRING_COMPARE_CASE "FIREDRAKE1" || type) BEGIN
     WRITE_ASCIIE trigOff + 0x7c ~%tutu_scriptf%DRAKET1~ // Firedrake Trap 1, sets new script
   END ELSE
   PATCH_IF !("%name%" STRING_COMPARE_CASE "FIREDRAKE2" || type) BEGIN
     WRITE_ASCIIE trigOff + 0x7c ~%tutu_scriptf%DRAKET2~ // Firedrake Trap 2, sets new script
   END ELSE
   PATCH_IF !("%name%" STRING_COMPARE_CASE "FIREDRAKE3" || type) BEGIN
     WRITE_ASCIIE trigOff + 0x7c ~%tutu_scriptf%DRAKET3~ // Firedrake Trap 3, sets new script
   END ELSE
   PATCH_IF !("%name%" STRING_COMPARE_CASE "FIREDRAKE4" || type) BEGIN
     WRITE_ASCIIE trigOff + 0x7c ~%tutu_scriptf%DRAKET4~ // Firedrake Trap 4, sets new script
   END ELSE
   PATCH_IF !("%name%" STRING_COMPARE_CASE "FIREDRAKE5" || type) BEGIN
     WRITE_ASCIIE trigOff + 0x7c ~%tutu_scriptf%DRAKET5~ // Firedrake Trap 5, sets new script
   END ELSE
   PATCH_IF !("%name%" STRING_COMPARE_CASE "FIREDRAKE6" || type) BEGIN
     WRITE_ASCIIE trigOff + 0x7c ~%tutu_scriptf%DRAKET6~ // Firedrake Trap 6, sets new script
   END
   trigOff += 0xc4
 END
END
BUT_ONLY

// Game Crash When Entering Area AR0717 Fix
// Author: Idobek
// Email: idobek@gibberlings3.net
// Website: http://www.gibberlings3.net
//DEPRECATED ~Ascension64~

COPY_EXISTING ~%CentralBaldursGate_House6_L1%.are~ ~override~
  READ_LONG 0x54 "actorsoffset"
  READ_SHORT 0x58 "#actors"
  WHILE ("%#actors%" > 0) BEGIN
    SET "actor" = ("%actorsoffset%" + ("%#actors%" - 1) * 0x110)
    READ_LONG ("%actor%" + 0x80) "actorresref1"
    READ_LONG ("%actor%" + 0x84) "actorresref2"
    WHILE (("%actorresref1%" = 0x4c524947) AND ("%actorresref2%" = 0x00314142)) OR //BG
          (("%actorresref1%" = 0x454b3031) AND ("%actorresref2%" = 0x00000047)) BEGIN //BGT or Tutu
      WRITE_ASCIIE ("%actor%" + 0x80) ~%tutu_var%GIRBA1~
      WRITE_SHORT ("%actor%" + 0x86) 0x0000
      READ_LONG ("%actor%" + 0x80) "actorresref1"
      READ_LONG ("%actor%" + 0x84) "actorresref2"
    END
  SET "#actors" = ("%#actors%" - 1)
  END
BUT_ONLY

////////////////////////
// Change Location Flags
COPY_EXISTING ~%CentralBaldursGate_GamblingTent1%.ARE~ ~override~
              ~%CentralBaldursGate_GamblingTent2%.ARE~ ~override~
              ~%CentralBaldursGate_DivinersTent%.ARE~ ~override~
  WRITE_SHORT 0x48 0x06 // Removes Outdoors flag
BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Permanent Corpses                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @101
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE

//DEPRECATED ~Ascension64~

// Author: devSin
// Email:
// Website:

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBCorpses.UB~

COPY_EXISTING ~%tutu_var%CATDEAD.CRE~ ~override~
              ~%tutu_var%CORPSE1.CRE~ ~override~
              ~%tutu_var%CORPSE2.CRE~ ~override~
              ~%tutu_var%DEAD.CRE~ ~override~
              ~%tutu_var%DEAD1.CRE~ ~override~
              ~%tutu_var%DEAD2.CRE~ ~override~
              ~%tutu_scriptd%EADFUCK.CRE~ ~override~
              ~%tutu_scripth%ALFMIRI.CRE~ ~override~
              ~%tutu_var%SKELDED.CRE~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
    READ_BYTE 0x10 flag
    WRITE_BYTE 0x10 flag | 0x4          // Makes the corpses permanent
  END
BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Elven Charm & Sleep Racial Resistance            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @102
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE
REQUIRE_PREDICATE (GAME_IS ~totsc tutu_totsc bgt~) @1
REQUIRE_PREDICATE !FILE_EXISTS_IN_GAME ~cdelfcm0.eff~ @90015

//DEPRECATED ~Ascension64~

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBElf.UB~

COPY_EXISTING ~%tutu_var%clck07.itm~  ~override~ // nymph cloak
              ~%tutu_var%clck08.itm~  ~override~ // algernon's cloak
              ~%tutu_var%misc2p.itm~  ~override~ // greagan's harp
              ~spin119.spl~ ~override~ // charm person
              ~sppr102.spl~ ~override~ // command
              ~sppr204.spl~ ~override~ // charm person or mammal
              ~sppr405.spl~ ~override~ // mental domination
              ~sppr982.spl~ ~override~ // dire charm
              ~spwi004.spl~ ~override~ // stinking cloud
              ~spwi104.spl~ ~override~ // charm person
              ~spwi105.spl~ ~override~ // color spray
              ~spwi116.spl~ ~override~ // sleep
              ~spwi213.spl~ ~override~ // stinking cloud
              ~spwi316.spl~ ~override~ // dire charm
              ~spwi411.spl~ ~override~ // emotion
              ~spwi506.spl~ ~override~ // domination
              ~spwi929.spl~ ~override~ // charm person
              ~spwi930.spl~ ~override~ // charm person
              ~spwi939.spl~ ~override~ // ??
              ~spwi943.spl~ ~override~ // dire charm
              ~spwi996.spl~ ~override~ // ??
              ~%tutu_var%wand08.itm~  ~override~ // wand of sleep
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6a "fx_off"
    PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.spl$" = 0) BEGIN
      SET "abil_length" = 0x28
    END ELSE BEGIN
      SET "abil_length" = 0x38
    END
    SET "fx_delta" = 0
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN // start iterating through abilities
      READ_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "abil_fx_num"
      READ_SHORT  ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) "abil_fx_idx"
      SET "abil_fx_idx" = ("%abil_fx_idx%" + "%fx_delta%")
      WRITE_SHORT ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) ("%abil_fx_idx%")
      SET "add_charm" = 0
      SET "add_sleep" = 0
      FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
        READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
        PATCH_IF (("%opcode%" = 39) OR ("%opcode%" = 5)) BEGIN // if there's a sleep or charm opcode
          READ_BYTE  ("%fx_off%" + 0x12 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "prob1"
          READ_BYTE  ("%fx_off%" + 0x13 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "prob2"
          PATCH_IF ("%opcode%" = 5) BEGIN // if charm
            READ_ASCII ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "charm_clone" (0x30)
            SET "add_charm" = 1
          END ELSE BEGIN
            READ_ASCII ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "sleep_clone" (0x30)
            SET "add_sleep" = 1
          END
        END
      END
      PATCH_IF ("%add_charm%" = 1) BEGIN
        SET "fx_delta" = ("%fx_delta%" + 8)
        SET "abil_fx_num" = ("%abil_fx_num%" + 8)
        FOR (index4 = 2 ; index4 < 4 ; index4 = index4 + 1) BEGIN
          FOR (index3 = 0 ; index3 < 4 ; index3 = index3 + 1) BEGIN
            INSERT_BYTES   ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 0x30              // insert new effect
              WRITE_ASCIIE ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) "%charm_clone%"   // preserve target
              WRITE_SHORT  ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 177               // use eff file
              WRITE_LONG   ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) "%index4%"        // elf or half elf
              WRITE_LONG   ("%fx_off%" + 0x08 + (0x30 * "%abil_fx_idx%")) 4                 // race.ids
              WRITE_BYTE   ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) ("%prob2%" + ((21 - ("%index4%" * 6)) * ("%prob1%" - "%prob2%") / 10)) // 90%
              WRITE_BYTE   ("%fx_off%" + 0x13 + (0x30 * "%abil_fx_idx%")) "%prob2%"         // base prob
              WRITE_ASCIIE ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~cdelfcm%index3%~ // eff file
          END
        END
      END
      PATCH_IF ("%add_sleep%" = 1) BEGIN
        SET "fx_delta" = ("%fx_delta%" + 6)
        SET "abil_fx_num" = ("%abil_fx_num%" + 6)
        FOR (index4 = 2 ; index4 < 4 ; index4 = index4 + 1) BEGIN
          FOR (index3 = 0 ; index3 < 3 ; index3 = index3 + 1) BEGIN
            INSERT_BYTES   ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 0x30              // insert new effect
              WRITE_ASCIIE ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) "%sleep_clone%"         // preserve target
              WRITE_SHORT  ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 177               // use eff file
              WRITE_LONG   ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) "%index4%"        // elf or half elf
              WRITE_LONG   ("%fx_off%" + 0x08 + (0x30 * "%abil_fx_idx%")) 4                 // race.ids
              WRITE_BYTE   ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) ("%prob2%" + ((21 - ("%index4%" * 6)) * ("%prob1%" - "%prob2%") / 10)) // 90%
              WRITE_BYTE   ("%fx_off%" + 0x13 + (0x30 * "%abil_fx_idx%")) "%prob2%"         // base prob
              WRITE_ASCIIE ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~cdelfsl%index3%~ // eff file
          END
        END
      END
      WRITE_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "%abil_fx_num%"
    END
  END
  BUT_ONLY

COPY ~bg1ub/elfcharm~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Original Saga Music Playlist Corrections     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @103
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE
REQUIRE_PREDICATE !GAME_IS ~bgt~ @90015 //not needed for BGT, already fixed

//DEPRECATED ~Ascension64~

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBPlaylist.UB~

// Author: Dudley
// Email:
// Website: http://www.dudleyville.com

COPY ~bg1ub/music~ ~music~



/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Sarevok's Diary Corrections                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @104
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @90016  // Fixed in BGEE
//DEPRECATED ~Ascension64~

// Author: SixOfSpades
// Email: sixospades@yahoo.com
// Website: http://www.geocities.com/sixospades/bg/bgguide.htm

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBSarevok.UB~

ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN //BGT or Tutu
  COMPILE ~bg1ub/%tutuorbgt%/diary/divine.d~
END ELSE BEGIN
  ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN //BG
  	STRING_SET 1355 @105
  	STRING_SET 1359 @106
  	STRING_SET 7238 @107
  	STRING_SET 7243 @108
  END
END

COPY_EXISTING ~%tutu_var%SCRL2S.ITM~ ~override~ // Note for Davaeorn #1
  SAY 0x50 @109

COPY_EXISTING ~%tutu_var%SCRL2T.ITM~ ~override~ // Note for Davaeorn #2
  SAY 0x50 @110

COPY_EXISTING ~%tutu_var%SCRL2U.ITM~ ~override~ // Note for Davaeorn #3
  SAY 0x50 @111

COPY_EXISTING ~%tutu_var%SCRL3F.ITM~ ~override~ // Sarevok's Diary
  WRITE_SHORT 0x1c 0x25 // Sets item category from Scrolls to Books
  WRITE_BYTE 0x4c 0x08 // Sets weight to 8 lbs
  SAY 0x50 @112




/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
// ----------------------------------------------------------------------------
// Name:    Prism and the Emeralds Tweak
// Author:  Sasha al'Therin (aka plainab) (aka you ain't getting my real name LOL)
// Date:    Ongoing
// Version: 0.10
// ----------------------------------------------------------------------------
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\


BEGIN @116 /* ~Prism and the Emeralds Tweak~ */

LOAD_TRA "bg1ub/tra/%LANGUAGE%/ubprism.tra"

ACTION_IF GAME_IS ~bg1~ THEN BEGIN
 INCLUDE ~bg1ub/abPrismEmeralds/tph/prism_bg1_addvars.tpa~
END

ACTION_IF GAME_IS ~totsc~ THEN BEGIN
 INCLUDE ~bg1ub/abPrismEmeralds/tph/prism_totsc_addvars.tpa~
END

ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
  //BG Method specific variables
  OUTER_SET fj_flags = 0b00000001
  OUTER_SPRINT abContainerEnable ~DestroyItem("abgfEyeL")
  DestroyItem("abgfEyeR")
  SetGlobal("abgf_GemInEye","GLOBAL",1)~
END

ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
 INCLUDE ~bg1ub/abPrismEmeralds/tph/prism_tutu_addvars.tpa~
END

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
 INCLUDE ~bg1ub/abPrismEmeralds/tph/prism_bgt_addvars.tpa~
END

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
 INCLUDE ~bg1ub/abPrismEmeralds/tph/prism_bgee_addvars.tpa~
 ADD_JOURNAL EXISTING TITLE (#31383) @127 @128 @129 @130 @131 @132 @133 @134 USING ~bg1ub/tra/%LANGUAGE%/ubprism.tra~
END

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN
  //BG2 Method specific variables
  OUTER_SET fj_flags = 0b00100001
  OUTER_SPRINT abContainerEnable ~DestroyItem("abgfEyeL")
  DestroyItem("abgfEyeR")
  SetGlobal("abgf_GemInEye","GLOBAL",1)
  ContainerEnable("A6StatueRightEye",TRUE)
  ContainerEnable("A6StatueLeftEye",TRUE)~
END

//first container
COPY_EXISTING ~%NashkelMines%.are~ override
LAUNCH_PATCH_FUNCTION ~fj_are_structure~
INT_VAR fj_loc_x = 641
        fj_loc_y = 2713
        fj_type = 6
        fj_lock_diff = 30
//        fj_flags to the bitwise container flags (bit0=locked, bit3=trap resets, bit5=disabled);
        fj_flags = %fj_flags%
//        fj_flags = 0b00100001  //for tutu & bgt (i hope)
        fj_trap_detect = 100
        fj_trap_remove_diff = 100
        fj_trap_active = 1 //to whether the container is trapped (0=no, 1=yes);
        fj_trap_status = 0 // to whether the trap is detected (0=no, 1=yes);
        fj_trap_loc_x = 641
        fj_trap_loc_y = 2713
        fj_box_left = 564
        fj_box_top = 2552
        fj_box_right = 590
        fj_box_bottom = 2563
        fj_vertex_0 = (564 + (2559 << 16))
        fj_vertex_1 = (578 + (2552 << 16))
        fj_vertex_2 = (590 + (2556 << 16))
        fj_vertex_3 = (586 + (2561 << 16))
        fj_vertex_4 = (580 + (2563 << 16))
        fj_vertex_5 = (570 + (2562 << 16))
//        fj_lockpick_strref to the lockpick string reference (default -1);
STR_VAR fj_structure_type = ~container~
        fj_name = ~A6StatueRightEye~
        fj_trap_script = ~abitrp~
//        fj_trap_script to the trap's script;
//        fj_key_resref to the filename of the container's key;
END

 LAUNCH_PATCH_FUNCTION ~fj_are_structure~
    INT_VAR fj_charge0 = 1
            fj_con_itm_idx    = SHORT_AT 0x74 - 1
    STR_VAR fj_name = ~abgfEyeR~
            fj_structure_type = ~itm~
 END

BUT_ONLY
//second container
COPY_EXISTING ~%NashkelMines%.are~ override
LAUNCH_PATCH_FUNCTION ~fj_are_structure~
INT_VAR fj_loc_x = 641
        fj_loc_y = 2713
        fj_type = 6
        fj_lock_diff = 70
//        fj_flags to the bitwise container flags (bit0=locked, bit3=trap resets, bit5=disabled);
        fj_flags = %fj_flags%
//        fj_flags = 0b00100001  //for tutu & bgt (i hope)
        fj_trap_detect = 100
        fj_trap_remove_diff = 100
        fj_trap_active = 1 //to whether the container is trapped (0=no, 1=yes);
        fj_trap_status = 0 // to whether the trap is detected (0=no, 1=yes);
        fj_trap_loc_x = 641
        fj_trap_loc_y = 2713
        fj_box_left = 614
        fj_box_top = 2541
        fj_box_right = 638
        fj_box_bottom = 2555
        fj_vertex_0 = (638 + (2544 << 16))
        fj_vertex_1 = (635 + (2550 << 16))
        fj_vertex_2 = (627 + (2554 << 16))
        fj_vertex_3 = (614 + (2555 << 16))
        fj_vertex_4 = (626 + (2543 << 16))
        fj_vertex_5 = (635 + (2541 << 16))
//        fj_lockpick_strref to the lockpick string reference (default -1);
STR_VAR fj_structure_type = ~container~
        fj_name = ~A6StatueLeftEye~
        fj_trap_script = ~abitrp~
//        fj_trap_script to the trap's script;
//        fj_key_resref to the filename of the container's key;
END

 LAUNCH_PATCH_FUNCTION ~fj_are_structure~
    INT_VAR fj_charge0 = 1
            fj_con_itm_idx    = SHORT_AT 0x74 - 1
    STR_VAR fj_name = ~abgfEyeL~
            fj_structure_type = ~itm~
 END

BUT_ONLY

//create left eye emerald
COPY_EXISTING ~%tutu_var%misc43.itm~ ~override/abgfEyeL.itm~
 SAY UNIDENTIFIED_DESC @117
 SAY IDENTIFIED_DESC @117
BUT_ONLY
//create right eye emerald
COPY_EXISTING ~%tutu_var%misc43.itm~ ~override/abgfEyeR.itm~
 SAY UNIDENTIFIED_DESC @118
 SAY IDENTIFIED_DESC @118
BUT_ONLY
//change the emeralds that prism carries
COPY_EXISTING ~%tutu_var%prism.cre~ ~override~
 REMOVE_CRE_ITEM ~%tutu_var%misc43~
 REPLACE_CRE_ITEM ~abgfEyeL~ #1 #0 #0 ~identified~ ~qitem1~
 REPLACE_CRE_ITEM ~abgfEyeR~ #1 #0 #0 ~identified~ ~qitem2~
 READ_ASCII 0x0248 ~overridescript~
 PATCH_IF (FILE_EXISTS_IN_GAME ~%overridescript%~) BEGIN
  INNER_ACTION BEGIN
   EXTEND_BOTTOM ~%overridescript%.bcs~ ~bg1ub/abPrismEmeralds/abgfprsm.baf~ //add our stuff -- creates new if doesn't exist
  END
 END ELSE BEGIN
  WRITE_ASCIIE 0x0248 ~abgfprsm~
  READ_ASCII 0x0248 ~overridescript~
  INNER_ACTION BEGIN
   EXTEND_BOTTOM ~%overridescript%.bcs~ ~bg1ub/abPrismEmeralds/abgfprsm.baf~ //add our stuff -- creates new if doesn't exist
  END
 END

BUT_ONLY
//compile the script for the eye container traps
COMPILE EVALUATE_BUFFER ~bg1ub/abPrismEmeralds/abitrp.baf~ 

//compile the dialog changes to oublek and prism
COMPILE EVALUATE_BUFFER ~bg1ub/abPrismEmeralds/oublek.d~ 


/* hacking attempt to make the mod compatible with mods I_C_T into Prism state 7 and Oublek state 4: (BG1NPC Project and Gavin), if installed before bg1ub */


/* IF BG1NPC Project "interjections" is installed: */

ACTION_IF FILE_EXISTS_IN_GAME ~override/X#BG1NPCPhase2.G3~ THEN BEGIN

/* this assumes that there is no other "TakePartyItem("%tutu_var%MISC43")" in the Oublek.d */

<<<<<<<< ...inlined/oublek_compatibility.d
REPLACE_ACTION_TEXT %tutu_var%oublek ~TakePartyItem("%tutu_var%MISC43")~ ~TakePartyItem("abgfEyeL") TakePartyItem("abgfEyeR") %ERASEJOURNALENTRY_PRISM_0% %ERASEJOURNALENTRY_PRISM_1% %ERASEJOURNALENTRY_PRISM_2% %ERASEJOURNALENTRY_PRISM_3% %ERASEJOURNALENTRY_PRISM_4% %ERASEJOURNALENTRY_PRISM_5%~
REPLACE_ACTION_TEXT %tutu_var%oublek ~TakePartyItemNum("misc43",2)~ ~TakePartyItem("abgfEyeL") TakePartyItem("abgfEyeR") %ERASEJOURNALENTRY_PRISM_0% %ERASEJOURNALENTRY_PRISM_1% %ERASEJOURNALENTRY_PRISM_2% %ERASEJOURNALENTRY_PRISM_3% %ERASEJOURNALENTRY_PRISM_4% %ERASEJOURNALENTRY_PRISM_5% %ERASEJOURNALBGEE%~
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/oublek_compatibility.d~
END
 

/* IF Gavin OR BG1NPC interjections is installed: */

ACTION_IF (FILE_EXISTS_IN_GAME ~override/X#BG1NPCPhase2.G3~) OR 
	(FILE_EXISTS_IN_GAME ~override/GavinNPC.B~) THEN BEGIN

<<<<<<<< ...inlined/prism_compatibility.d
REPLACE_ACTION_TEXT %tutu_var%prism ~DropInventory()~ ~%abContainerEnable%~
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/prism_compatibility.d~

END

// If you tricked Oublek into giving you Greywolf's gold and your reputation is low, he will refuse to deal with you.
<<<<<<<< ...inlined/fix_oublek_denial.d
ADD_TRANS_TRIGGER ~%tutu_var%oublek~ 10 ~ReactionLT(LastTalkedToBy(),NEUTRAL_LOWER)~ DO 0 UNLESS ~ReactionLT(LastTalkedToBy(),NEUTRAL_LOWER)~
ADD_TRANS_ACTION ~%tutu_var%oublek~ BEGIN 5 END BEGIN 0 END ~EscapeArea()~ UNLESS ~EscapeArea()~
ADD_TRANS_ACTION ~%tutu_var%oublek~ BEGIN 2 END BEGIN 0 END ~ReputationInc(-1)~ UNLESS ~ReputationInc(-1)~

APPEND ~%tutu_var%oublek~
IF ~~ THEN BEGIN AYOublekPatience
  SAY @1005 //~Nashkel has precious little patience for those who would cheat others of their hard-earned livelihood. I have neither time nor will to deal with you today.~//#665 + #664
  IF ~~ THEN DO ~SetGlobal("BroughtBounty","GLOBAL",1)~
  EXIT
END
END // End the append

EXTEND_BOTTOM ~%tutu_var%oublek~ 10
IF ~Global("BroughtBounty","GLOBAL",0) ReactionGT(LastTalkedToBy(),HOSTILE_UPPER)~
THEN REPLY @1004 GOTO AYOublekPatience
END //end the extend bottom
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/fix_oublek_denial.d~


/*weidu isn't replacing the @# with a TRA ref after its been evaluted from a variable
so we have to use a high number and replace again.  only affects BGT, Tutu, & BGEE*/
COPY_EXISTING ~%tutu_var%oublek.dlg~ override
 DECOMPILE_DLG_TO_D
  PATCH_IF (GAME_IS ~bgt~) BEGIN
   REPLACE 99999210 @210
   REPLACE 99999211 @211
   REPLACE 99999212 @212
   REPLACE 99999214 @214
   REPLACE 99999217 @217
   REPLACE 99999218 @218
   REPLACE 99999216 @216
  END
  PATCH_IF (GAME_IS ~tutu tutu_totsc bgee~) BEGIN
   REPLACE 99999210 @127
   REPLACE 99999211 @128
   REPLACE 99999212 @129
   REPLACE 99999214 @131
   REPLACE 99999217 @133
   REPLACE 99999218 @134
   REPLACE 99999216 @132
  END
 COMPILE_D_TO_DLG
BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////
/////Duke Eltan in the Harbor Master's Building
/////by Balquo and jastey
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @219 /* ~Duke Eltan in the Harbor Master's Building~ */


ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN

<<<<<<<< ...inlined/harbor.d
REPLACE_ACTION_TEXT %tutu_var%harbor ~EscapeArea[^)]+)~ ~MoveToPoint([431.171])
CreateCreatureImpassable("%tutu_var%DELTAN2",[449.121],14)
Wait(1)
MoveToPoint([461.197])
Face(0)
DialogInterrupt(TRUE)~
REPLACE_ACTION_TEXT %tutu_var%harbor ~ActionOverride("Eltan2",SetNumTimesTalkedTo(100))~ ~DestroyItem("%tutu_var%MISC55")~
ADD_TRANS_ACTION %tutu_var%harbor BEGIN 9 END BEGIN END ~DialogInterrupt(FALSE)~
REPLACE_TRIGGER_TEXT %tutu_var%deltan2 ~True()~ ~Global("EltanMove","GLOBAL",0)~
ADD_STATE_TRIGGER %tutu_var%deltan2 1 ~Global("EltanMove","GLOBAL",1)~
ADD_TRANS_ACTION %tutu_var%deltan2 BEGIN 1 END BEGIN END ~SetGlobal("EltanMove","GLOBAL",2)~
ADD_STATE_TRIGGER %tutu_var%deltan2 2 ~Global("EltanMove","GLOBAL",2)~
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/harbor.d~

END


ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN

<<<<<<<< ...inlined/harbor.d
REPLACE_ACTION_TEXT harbor ~EscapeArea()~ ~MoveToPoint([406.206])
CreateCreature("DELTAN",[426.160]%FACE_0%)
ActionOverride("Eltan",DestroyItem("PLAT04"))
ActionOverride("Eltan",DestroyItem("SHLD03"))
ActionOverride("Eltan",DestroyItem("SW1H05"))
ActionOverride("Eltan",DestroyItem("BOOK68"))
Wait(1)
MoveToPoint([465.193])
Face(2)
DialogInterrupt(TRUE)~
REPLACE_ACTION_TEXT harbor ~ActionOverride("Eltan2",SetNumTimesTalkedTo(100))~ ~DestroyItem("%tutu_var%MISC55")~
ADD_TRANS_ACTION harbor BEGIN 9 END BEGIN END ~DialogInterrupt(FALSE)~
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/harbor.d~
END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////
/////Nim Furlwing Encounter
/////by AstroBryGuy
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4000 /* ~Nim Furlwing Encounter~ */
FORBID_FILE ~override/NTNIMFUR.CRE~ @90003
FORBID_FILE ~data/JA#NIMFU.CRE~ @90004

COPY ~bg1ub/bg1ubplaceholder~ ~override/BG1UBNimFurlwing.UB~

/* Creatures */
COPY ~bg1ub/nimfur/ubnimfur.cre~ ~override/ubnimfur.cre~
    SAY NAME1 #15611
    SAY NAME2 #15611
    LAUNCH_PATCH_MACRO ~source_cre_sound_cleanup~
    SAY INITIAL_MEETING #11112 
    SAY BATTLE_CRY1 #11131
    SAY DAMAGE #11075
    SAY DYING #11076
    SAY SELECT_COMMON1 #11112
    SAY DIALOG_DEFAULT #11112
    LAUNCH_PATCH_MACRO ~source_cre_script_cleanup~
    LAUNCH_PATCH_MACRO ~cre_dest_res~
	ADD_CRE_ITEM ~UBSCP402~ #1 #0 #0 ~IDENTIFIED~ ~QITEM1 QITEM2 QITEM3~

COPY_EXISTING ~%tutu_var%dogwa.cre~ ~override/ubnimdog.cre~
    SAY NAME1 @4002
    SAY NAME2 @4002
    WRITE_BYTE 0x270 0x80 //  Set Enemy/Ally to neutral
    LAUNCH_PATCH_MACRO ~source_cre_script_cleanup~
    LAUNCH_PATCH_MACRO ~cre_dest_res~

COPY_EXISTING ~%tutu_var%wolfdi.cre~ ~override/ubnimwlf.cre~
    LAUNCH_PATCH_MACRO ~source_cre_script_cleanup~
    LAUNCH_PATCH_MACRO ~cre_dest_res~

/* Items */
COPY ~bg1ub/nimfur/ubscp402.itm~ ~override~

/* Add CREs to Larswood */
COPY_EXISTING ~%Larswood%.are~ override
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4500
    	fj_loc_y          = 2740
    	fj_dest_x         = 4500
    	fj_dest_y         = 2740
    	fj_animation      = 0x6111 // Fighter Female Elf
    	fj_orientation    = 9      // NNE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Nim Furlwing"
   		fj_cre_resref     = UBNIMFUR
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4535
    	fj_loc_y          = 2790
    	fj_dest_x         = 4535
    	fj_dest_y         = 2790
    	fj_animation      = 0x7401 // War Dog
    	fj_orientation    = 9      // NNE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Hunting Dog"
   		fj_cre_resref     = UBNIMDOG
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4563
    	fj_loc_y          = 2765
    	fj_dest_x         = 4563
    	fj_dest_y         = 2765
    	fj_animation      = 0x7401 // War Dog
    	fj_orientation    = 9      // NNE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Hunting Dog"
   		fj_cre_resref     = UBNIMDOG
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4460
    	fj_loc_y          = 2735
    	fj_dest_x         = 4460
    	fj_dest_y         = 2735
    	fj_animation      = 0x7401 // War Dog
    	fj_orientation    = 9      // NNE
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Hunting Dog"
   		fj_cre_resref     = UBNIMDOG
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4525
    	fj_loc_y          = 2600
    	fj_dest_x         = 4525
    	fj_dest_y         = 2600
    	fj_animation      = 0x7B02 // Dire Wolf
    	fj_orientation    = 1      // SSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Wolf
   		fj_cre_resref     = UBNIMWLF
   	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4560
    	fj_loc_y          = 2570
    	fj_dest_x         = 4560
    	fj_dest_y         = 2570
    	fj_animation      = 0x7B02 // Dire Wolf
    	fj_orientation    = 1      // SSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Wolf
   		fj_cre_resref     = UBNIMWLF
	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4570
    	fj_loc_y          = 2500
    	fj_dest_x         = 4570
    	fj_dest_y         = 2500
    	fj_animation      = 0x7B02 // Dire Wolf
    	fj_orientation    = 1      // SSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Wolf
   		fj_cre_resref     = UBNIMWLF
   	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4625
    	fj_loc_y          = 2575
    	fj_dest_x         = 4625
    	fj_dest_y         = 2575
    	fj_animation      = 0x7B02 // Dire Wolf
    	fj_orientation    = 1      // SSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Wolf
   		fj_cre_resref     = UBNIMWLF
   	END
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 4670
    	fj_loc_y          = 2580
    	fj_dest_x         = 4670
    	fj_dest_y         = 2580
    	fj_animation      = 0x7B02 // Dire Wolf
    	fj_orientation    = 1      // SSW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = Wolf
   		fj_cre_resref     = UBNIMWLF
   	END
BUT_ONLY

/* Scripts and Dialogues */
COMPILE EVALUATE_BUFFER ~bg1ub/nimfur/ubnimfur.d~
COMPILE EVALUATE_BUFFER ~bg1ub/nimfur/ubnimfur.baf~
COMPILE EVALUATE_BUFFER ~bg1ub/nimfur/ubnimdog.baf~
COMPILE EVALUATE_BUFFER ~bg1ub/nimfur/ubnimwlf.baf~

/*<<<<<<<< .../bg1ub/ub_ar2900nf.baf
IF %BGT_VAR%
!Exists("UBNIMFUR")
Global("UBNimFurlwingSpawn","%Larswood%",0)
THEN
RESPONSE #100
SetGlobal("UBNimFurlwingSpawn","%Larswood%",1)
CreateCreature("UBNIMFUR",[4500.2740]%FACE_9%)
CreateCreature("UBNIMDOG",[4535.2790]%FACE_9%)
CreateCreature("UBNIMDOG",[4563.2765]%FACE_9%)
CreateCreature("UBNIMDOG",[4460.2735]%FACE_9%)
CreateCreature("UBNIMWLF",[4525.2600]%FACE_1%)
CreateCreature("UBNIMWLF",[4560.2570]%FACE_1%)
CreateCreature("UBNIMWLF",[4570.2500]%FACE_1%)
CreateCreature("UBNIMWLF",[4625.2575]%FACE_1%)
CreateCreature("UBNIMWLF",[4670.2580]%FACE_1%)
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%Larswood%" bottom=".../bg1ub/ub_ar2900nf.baf" END
*/  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////
/////Restored Elfsong Tavern Movie
/////movie by Sams, code by AstroBryGuy
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
BEGIN @4100 /* ~Restored Elfsong Tavern Movie~ */
REQUIRE_PREDICATE (!GAME_IS ~bgee~) @4101 // Unavailable for BGEE
FORBID_FILE ~data/JA#TAVER.MVE~ @90004

/* Copy movie file - SKIP - MAKE_BIFF INSTEAD */
//COPY ~bg1ub/tavern/ubtavern.mve~ ~override~

/* Edit Elfsong area script */
<<<<<<<< .../bg1ub/ub_ar0705tv.baf
IF
	Global("UBEnteredElfSong","%EBaldursGate_ElfsongTavern_L1%",0)
THEN
	RESPONSE #100
		StartMovie("UBTAVERN")
		SetGlobal("UBEnteredElfSong","%EBaldursGate_ElfsongTavern_L1%",1)
		Continue()
END
>>>>>>>>

LAF extend_area_script STR_VAR area="%EBaldursGate_ElfsongTavern_L1%" bottom=".../bg1ub/ub_ar0705tv.baf" END

/* Install movie file (must be added as .bif) */
MAKE_BIFF UBTAVERN BEGIN ~bg1ub/tavern/~ ~ubtavern.mve~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////
/////Svlast, the fallen paladin
/////by AstroBryGuy
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
BEGIN @4200 /* ~Svlast, the fallen paladin~ */
FORBID_FILE ~override/JA#SVLST.CRE~ @90004

LOAD_TRA "bg1ub/tra/%LANGUAGE%/ubsvlast.tra"

COPY_EXISTING ~%FirewineBridge%.ARE~ ~override~
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 2350
    	fj_loc_y          = 2225
    	fj_dest_x         = 2350
    	fj_dest_y         = 2225
    	fj_animation      = 0x6100 // Fighter male human
    	fj_orientation    = 2      // SE
    	fj_schedule       = BIT23 | BIT22 | BIT21 | BIT5 | BIT4 | BIT3 | BIT2 | BIT1 | BIT0 // Appear only at night
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Svlast"
    	fj_cre_resref     = UBSVLAST
  	END
BUT_ONLY

COPY ~bg1ub/svlast/UBSVLAST.CRE~ override
	LPM source_cre_sound_cleanup
	LPM source_cre_script_cleanup
	LPM cre_source_res
	SAY NAME1 @4201 // ~Svlast~
	SAY NAME2 @4201 // ~Svlast~
	SAY INITIAL_MEETING @4202 // ~[LOTHANDER 2] My life is in shambles.~ [LOTHR02]
	SAY DAMAGE @4203 // ~[LOTHANDER 3]~ [LOTHR03]
	SAY DYING @4204 // ~[LOTHANDER 4]~ [LOTHR04]
	SAY SELECT_COMMON1 @4202 // ~[LOTHANDER 2] My life is in shambles.~ [LOTHR02]
	REMOVE_CRE_ITEMS
	ADD_CRE_ITEM ~%tutu_var%PLAT01~ #0 #0 #0 ~IDENTIFIED~ ~armor~
	ADD_CRE_ITEM ~%tutu_var%SHLD03~ #0 #0 #0 ~IDENTIFIED~ ~shield~
	ADD_CRE_ITEM ~%tutu_var%SW1H02~ #0 #0 #0 ~IDENTIFIED~ ~weapon1~ EQUIP

COPY ~bg1ub/svlast/UBSVLREV.CRE~ override
	LPM source_cre_sound_cleanup
	LPM source_cre_script_cleanup
	LPM cre_source_res
	SAY NAME1 @4210 // ~Revenant~
	SAY NAME2 @4210 // ~Revenant~
	SAY BATTLE_CRY1 @4214 // ~[REVENANT 04] You... shall... pay...~ [%tutu_var%REVEN04]
	SAY ATTACK1 @4215 // ~[REVENANT 05] ~ [%tutu_var%REVEN05]
	SAY ATTACK2 @4216 // ~[REVENANT 06] ~ [%tutu_var%REVEN06]
	SAY DAMAGE @4218 // ~[REVENANT 08] ~ [%tutu_var%REVEN08]
	SAY DYING @4219 // ~[REVENANT 09] ~ [%tutu_var%REVEN09]
	SAY SELECT_COMMON1 @4212 // ~[REVENANT 02] Must... revenge...~ [%tutu_var%REVEN02]
	SAY EXISTANCE1 @4211 // ~[REVENANT 01] ~ [%tutu_var%REVEN01]
	REMOVE_CRE_ITEMS
	ADD_CRE_ITEM ~%tutu_var%IMMUNE1~ #0 #0 #0 ~UNDROPPABLE~ ~lring~
	ADD_CRE_ITEM ~%tutu_var%RING95~ #0 #0 #0 ~UNDROPPABLE~ ~rring~
	ADD_CRE_ITEM ~%tutu_var%REVENT2~ #0 #0 #0 ~UNDROPPABLE~ ~weapon1~ EQUIP
	ADD_CRE_ITEM ~UBSVLHLM~ #0 #0 #0 ~UNDROPPABLE~ ~helmet~

COPY ~bg1ub/svlast/UBSVLHLM.ITM~ override

COMPILE EVALUATE_BUFFER ~bg1ub/svlast/UBSVLAST.BAF~
COMPILE EVALUATE_BUFFER ~bg1ub/svlast/UBSVLREV.BAF~
LAF extend_area_script STR_VAR area="%FirewineBridge%" bottom="bg1ub/svlast/UB4500_SVL.BAF" END

COMPILE EVALUATE_BUFFER ~bg1ub/svlast/UBSVLAST.D~


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////
/////Mal-Kalen, the Ulcaster Ghost
/////by AstroBryGuy
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
BEGIN ~Mal-Kalen, the Ulcaster Ghost~
FORBID_FILE ~override/JA#MALKA.CRE~ @90004

LOAD_TRA "bg1ub/tra/%LANGUAGE%/ubmalkal.tra"

COPY_EXISTING ~%UlcasterRuins%.ARE~ ~override~
	LPF fj_are_structure
    	INT_VAR
    	fj_loc_x          = 2000
    	fj_loc_y          = 950
    	fj_dest_x         = 2000
    	fj_dest_y         = 950
    	fj_animation      = 0x6200 // Mage male human
    	fj_orientation    = 6      // NW
    	STR_VAR
    	fj_structure_type = actor
    	fj_name           = "Mal-Kalen"
    	fj_cre_resref     = UBMALKAL
  	END
BUT_ONLY

COPY ~bg1ub/malkal/UBMALKAL.CRE~ ~override/UBMALKAL.CRE~
		~bg1ub/malkal/UBMALKAL.CRE~ ~override/UBMALKA2.CRE~
	LPM source_cre_sound_cleanup
	LPM source_cre_script_cleanup
	LPM cre_dest_res
	SAY NAME1 @4301 // ~Mal-Kalen~
	SAY NAME2 @4301 // ~Mal-Kalen~
	SAY DIALOG_DEFAULT @4303 // ~[MAL-KALEN 2] Release the body! Live again, I!~ [MALKL02]
	SAY DAMAGE @4304 // ~[MAL-KALEN 3]~ [MALKL03]
	SAY DYING @4305 // ~[MAL-KALEN 4]~ [MALKL04]
	SAY EXISTANCE1 @4306 // ~[REVENANT 01] ~ [%tutu_var%REVEN01]
	SAY BATTLE_CRY1 @4307 // ~[REVENANT 02] Must... revenge...~ [%tutu_var%REVEN02]
	SAY BATTLE_CRY2 @4303 // ~[MAL-KALEN 2] Release the body! Live again, I!~ [MALKL02]
	SAY ATTACK1 @4308 // ~[REVENANT 05] ~ [%tutu_var%REVEN05]
	SAY ATTACK2 @4309 // ~[REVENANT 06] ~ [%tutu_var%REVEN06]
	WRITE_ASCIIE DIALOG ~UBMALKAL~ #8 // dialogue
    WRITE_ASCIIE SCRIPT_DEFAULT ~%tutu_scriptw%dasight~ #8 //  default script
	REMOVE_CRE_ITEMS
	ADD_CRE_ITEM ~%tutu_var%IMMUNE1~ #0 #0 #0 ~UNDROPPABLE~ ~lring~
	ADD_CRE_ITEM ~%tutu_var%RING95~ #0 #0 #0 ~UNDROPPABLE~ ~rring~
	ADD_CRE_ITEM ~%tutu_var%CLCK13~ #0 #0 #0 ~UNDROPPABLE~ ~armor~
	ADD_CRE_ITEM ~UBMKHLM~ #0 #0 #0 ~UNDROPPABLE~ ~helmet~
	ADD_CRE_ITEM ~UBMKATK~ #0 #0 #0 ~UNDROPPABLE~ ~weapon1~ EQUIP

COPY_EXISTING ~UBMALKA2.CRE~ override
	ADD_CRE_ITEM ~%tutu_var%mage01~ #0 #0 #0 ~UNDROPPABLE~ ~amulet~

COPY ~bg1ub/malkal/UBMKHLM.ITM~ override
COPY ~bg1ub/malkal/UBMKATK.ITM~ override
COPY ~bg1ub/malkal/UBMKLIV.SPL~ override
COPY ~bg1ub/malkal/UBMKDTH.SPL~ override

COMPILE EVALUATE_BUFFER ~bg1ub/malkal/UBMALKAL.BAF~
COMPILE EVALUATE_BUFFER ~bg1ub/malkal/UBMALKA2.BAF~

COMPILE EVALUATE_BUFFER ~bg1ub/malkal/UBMALKAL.D~
